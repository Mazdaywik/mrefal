//FROM Driver
$EXTERN CompileFile;

//FROM LibraryEx
$EXTERN Map, ArgList;

//FROM Library
$EXTERN Success, Fails, WriteLine, System;

//------------------------------------------------------------------------------

$FORWARD Main;

$ENTRY Go {
  = <Main <ArgList>>;
}

$FORWARD Compile-And-Link, FindFiles-Compilers;

//FROM ParseCmdLine
$EXTERN ParseCommandLine, NoCompile, CompileCommand, CmdLineError;

Main {
  (e.ProgName) e.Arguments =
    <Compile-And-Link
      <FindFiles-Compilers
        <ParseCommandLine e.Arguments>
      >
    >;
}

//FROM FindFile
$EXTERN FindFiles;

FindFiles-Compilers {
  t.Compilers (e.Folders) e.Files =
    t.Compilers <FindFiles (e.Folders) e.Files>;
}

//FROM FindFile
$EXTERN Source, Output, NotFound;

$FORWARD PrintNotFound, CompileEachFile, Link;

Compile-And-Link {
  (CmdLineError e.Message) =
    <WriteLine 'COMMAND LINE ERROR: ' e.Message>;

  t.Compiler e.Files-B (NotFound e.FileName) e.Files-E =
    <Map PrintNotFound (NotFound e.FileName) e.Files-E>;

  t.Compiler e.Files = <Link t.Compiler <Map CompileEachFile e.Files>>;
}

PrintNotFound {
  (NotFound e.FileName) =
    <WriteLine 'COMMAND LINE ERROR: file ' e.FileName ' not found'>;

  (Output e.FileName) = ;

  (Source (e.Source) e.Output) = ;
}


$FORWARD SwCompiled;

CompileEachFile {
  (Output e.OutputName) =
    <WriteLine '+Linking ' e.OutputName> (e.OutputName);

  (Source (e.Source) e.Output) =
    <SwCompiled
      <WriteLine '*Compiling ' e.Source ':'>
      <CompileFile (e.Source) e.Output>
    >;
}

SwCompiled {
  Success e.OutputName = (e.OutputName);

  Fails = Fails;
}

UnBracketSpace {
  (e.FileName) = ' ' e.FileName;
}

Link {
  (NoCompile) e.Files = ;

  (CompileCommand e.Command) e.Outputs-B Fails e.Outputs-E = ;

  (CompileCommand e.Command) e.Outputs =
    <System e.Command <Map UnBracketSpace e.Outputs>>
    <WriteLine '** Compilation successed **'>;
}
