
.mref:

Module = ModuleHeader ModuleBody ModuleTailer .
ModuleHeader = ModuleType ModuleName ';' | '$program' ModuleName ';' .
ModuleType = '$module' | '$program' | '$clearmodule' .
ModuleTailer = '$end' ModuleName '.' .
ModuleName = QualifiedName .
ModuleBody = ModuleElement* .

QualifiedName = IDENTIFIER | QualifiedName '::' IDENTIFIER .

ModuleElement = ImportBlock | FunctionDefinition | SwapDefinition
	| ADTDefinition | ';' .

ImportBlock =
	'$import' ModuleImport { ',' ModuleImport } ';' .
ModuleImport = [ ModuleAlias '=' ] ModuleName .
ModuleAlias = IDENTIFIER .

SwapDefinition = '$swap' SwapList ';' .
SwapList = SwapName | SwapName ',' SwapList .
SwapName = IDENTIFIER .

FunctionDefinition = [ '$entry' ] FunctionName FunctionBody .
FunctionName = IDENTIFIER .
FunctionBody = Block | Sentence .
Block = '{' Sentence* '}' .

Sentence = PatternExpr '=' ResultExpr ';' .
PatternExpr = PatternTerm* .
PatternTerm = IDENTIFIER | VARIABLE | LITERAL | MACRO
	| '(' PatternExpr ')' | DQWORD .
ResultExpr = ResultTerm* .
ResultTerm = IDENTIFIER | VARIABLE | LITERAL | MACRO
	| '(' ResultExpr ')' | DQWORD | FunctionCall .
FunctionCall = '<' Callable ResultExpr '>' .
Callable = QualifiedName | SwapName | S-VARIABLE .

ADTDefinition = '$data' ADTList .
ADTList = ADTName | ADTName ',' ADTList .
ADTName = INENTIFIER .

.msym:

SymModule = ModuleHeader SymModuleBody ModuleTailer .
SymModuleBody = SymModuleElement* .
SymModuleElement = FunctionDeclaration | ImportBlock .
FunctionDeclaration = '$entry' FunctionName ';' .