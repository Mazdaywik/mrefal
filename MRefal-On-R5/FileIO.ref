*$FROM FileHandles
$EXTERN GetHandle, ReleaseHandle;

*$FROM Error
$EXTERN Error;

$ENTRY FOpen {
	'r' e.FileName =
		<CheckExistAndOpenForRead
			<ExistFile e.FileName>
			e.FileName
		>;
	'w' e.FileName =
		<SwFOpen <GetHandle> 'w' e.FileName>;

	s.OtherMode e.FileName =
		<Error
			'F' s.OtherMode ' -- unknown open file mode ('
			file '\'' e.FileName '\')'
		>;
}

CheckExistAndOpenForRead {
	True e.FileName =
		<SwFOpen <GetHandle> 'r' e.FileName>;

	False e.FileName =
		<Error 'F' 'Can\'t open file \'' e.FileName '\' for reading' >;
}

SwFOpen {
	None s.Mode e.FileName =
		<Error
			'F' 'Can\'t open file ' e.FileName ': no free handles'
		>;

	s.Handle s.Mode e.FileName =
		(s.Handle e.FileName) <Open s.Mode s.Handle e.FileName>;
}

$ENTRY FClose {
	(s.Handle e.FileName) =
		<Open 'r' s.Handle e.FileName>
		<ReleaseHandle s.Handle>;
}

$ENTRY FReadLine {
	(s.Handle e.FileName) =
		(s.Handle e.FileName) <Get s.Handle>;
}

$ENTRY FWriteLine {
	(s.Handle e.FileName) e.Line =
		(s.Handle e.FileName) <Putout s.Handle e.Line>;
}

$ENTRY FWriteLine-T {
	(s.Handle e.FileName) e.Line =
		(s.Handle e.FileName) <Put s.Handle e.Line>;
}

$ENTRY FExtractFileName {
	(s.Handle e.FileName) = e.FileName;
}