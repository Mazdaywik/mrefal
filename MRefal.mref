$MODULE MRefal;

$IMPORT Context;
$IMPORT Compiler;
$IMPORT Linker;
$IMPORT MError;
$IMPORT Modules;
$IMPORT Refal5;

$ENTRY Go { = <StartCompiling>; }

StartCompiling {
	= <Results
		<Linker::Link
			<CompileList <Context::CreateContext> <LoadModuleList> >
		>
	>;
}

Results {
	t.Context = <Context::DeleteContext t.Context>;
}

LoadModuleList {
	= <DoLoadModuleList () 2 <Refal5::Arg 1>>;
}

DoLoadModuleList {
	(e.Modules) 2 =
		<MError::Error 'F' 'Command line error: use MRefal module1 module2'>;

	(e.Modules) s.NextNumArg =
		e.Modules;

	(e.Modules) s.NextNumArg e.ModuleOrFile =
		<DoLoadModuleList
			(e.Modules <ModuleOrFile e.ModuleOrFile>)
			<Refal5::Add s.NextNumArg 1>
			<Refal5::Arg s.NextNumArg>
		>;
}

ModuleOrFile {
	e.Name = <SwModuleOrFile (<Refal5::Lower e.Name>) e.Name>;
}

SwModuleOrFile {
	( e.FnameName '.mref' ) e.FnameNameExtCase =
		(File e.FnameNameExtCase);
	( e.ModNameLower ) e.ModNameCase =
		(Module <Modules::CanonicalModuleName e.ModNameCase>);
}

CompileList {
	t.Context e.Modules =
		<CompileFilesFromList t.Context e.Modules (Delim)>;
}

CompileFilesFromList {
	t.Context (File e.FileName) e.Modules =
		<CompileFilesFromList
			<Compiler::CompileByFileName t.Context e.FileName> e.Modules
		>;
	
	t.Context (Module e.ModName) e.Modules =
		<CompileFilesFromList
			t.Context e.Modules (Module e.ModName)
		>;

	t.Context (Delim) e.ModulesOnly =
		<CompileModulesFromList
			t.Context e.ModulesOnly
		>;
}

CompileModulesFromList {
	t.Context (Module e.ModName) e.Modules =
		<CompileModulesFromList
			<Compiler::CompileByModuleName t.Context e.ModName> e.Modules
		>;

	t.Context =
		t.Context;
}

$END MRefal.
