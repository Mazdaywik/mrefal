$MODULE MRefal;

$IMPORT Context;
$IMPORT Compiler;
$IMPORT MError;
$IMPORT MLinker;
$IMPORT Modules;
$IMPORT MOS;
$IMPORT Math;
$IMPORT MStrings;
$IMPORT MVersion;

$IMPORT MSelfDiag;

$ENTRY Go =
	<MVersion::Show>
	<StartCompiling>;

StartCompiling {
	= <Results
		<MLinker::Link
			<CompileList <Context::Create> <LoadModuleList> >
		>
	>;
}

Results {
	t.Context =
		<Context::Destroy t.Context>;
}

LoadModuleList {
	= <DoLoadModuleList () 2 <MOS::Arg 1>>;
}

DoLoadModuleList {
	(e.Modules) 2 =
		<MError::Fatal 'Command line error: use MRefal module1 module2'>;

	(e.Modules) s.NextNumArg =
		e.Modules;

	(e.Modules) s.NextNumArg e.ModuleOrFile =
		<DoLoadModuleList
			(e.Modules <ModuleOrFile e.ModuleOrFile>)
			<Math::Add s.NextNumArg 1>
			<MOS::Arg s.NextNumArg>
		>;
}

ModuleOrFile {
	e.Name = <SwModuleOrFile (<MStrings::Lower e.Name>) e.Name>;
}

SwModuleOrFile {
	( e.FnameName '.mref' ) e.FnameNameExtCase =
		(File e.FnameNameExtCase);

	( e.ModNameLower ) e.ModNameCase =
		(Module <Modules::CanonicalModuleName e.ModNameCase>);
}

CompileList {
	t.Context e.Modules =
		<CompileFilesFromList t.Context e.Modules (Delim)>;
}

CompileFilesFromList {
	t.Context (File e.FileName) e.Modules =
		<CompileFilesFromList
			<Compiler::CompileByFileName t.Context e.FileName> e.Modules
		>;
	
	t.Context (Module e.ModName) e.Modules =
		<CompileFilesFromList
			t.Context e.Modules (Module e.ModName)
		>;

	t.Context (Delim) e.ModulesOnly =
		<CompileModulesFromList
			t.Context e.ModulesOnly
		>;
}

CompileModulesFromList {
	t.Context (Module e.ModName) e.Modules =
		<CompileModulesFromList
			<Compiler::CompileByModuleName t.Context e.ModName> e.Modules
		>;

	t.Context =
		t.Context;
}

$END MRefal.
