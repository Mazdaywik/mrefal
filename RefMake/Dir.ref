$ENTRY FileDateTime {
	e.FileName =
		<ExtractField Date <FileInfo e.FileName>>
		<ExtractField Time <FileInfo e.FileName>>
		;
}

$ENTRY LongFileName {
	e.FileName =
		<ExtractField LongName <FileInfo e.FileName>>;
}

ExtractField {
	s.Field (e.Info-B (s.Field e.Hours e.Minutes) e.Info-E) =
		e.Hours e.Minutes;
	s.Field t.First e.Other =
		<Prout s.Field 'Warning: many files found: ' e.Other>
		<ExtractField s.Field t.First>;
	s.Field e.Other =
		<Prout 'ERROR in <ExtractField ' s.Field ' ...>'>
		<Exit 1>;
}

$ENTRY CompareDateTime {
	(s.D1 s.Month1 s.Y1 s.H1 s.M1) s.D2 s.Month2 s.Y2 s.H2 s.M2 =
		<CompareDateTime
			s.D1 s.Month1 s.Y1 s.H1 s.M1
			s.D2 s.Month2 s.Y2 s.H2 s.M2
		>;

	s.D1 s.Month1 s.Y1 s.H1 s.M1
	s.D1 s.Month1 s.Y1 s.H1 s.M2 =
		<CvtCompareRef5 <Compare s.M1 s.M2>>;

	s.D1 s.Month1 s.Y1 s.H1 s.M1
	s.D1 s.Month1 s.Y1 s.H2 s.M2 =
		<CvtCompareRef5 <Compare s.H1 s.H2>>;

	s.D1 s.Month1 s.Y1 s.H1 s.M1
	s.D2 s.Month1 s.Y1 s.H2 s.M2 =
		<CvtCompareRef5 <Compare s.D1 s.D2>>;

	s.D1 s.Month1 s.Y1 s.H1 s.M1
	s.D2 s.Month2 s.Y1 s.H2 s.M2 =
		<CvtCompareRef5 <Compare s.Month1 s.Month2>>;

	s.D1 s.Month1 s.Y1 s.H1 s.M1
	s.D2 s.Month2 s.Y2 s.H2 s.M2 =
		<CvtCompareRef5 <Compare s.Y1 s.Y2>>;
	s.D1 s.Month1 s.Y1 s.H1 s.M1
	s.D2 s.Month2 s.Y2 s.H2 s.M2 =
		<CvtCompareRef5 <Compare >>;
}

CvtCompareRef5 {
	'-' = '<';
	'0' = '=';
	'+' = '>';
}

$ENTRY FileInfo {
	e.FileName =
		<Nil <System 'dir \"' e.FileName '\"> ~tmp.lst'>>
		<Open 'r' 1 '~tmp.lst'>
		<ParseDirLst <LoadFile 1>>
		<Open 'r' 1 'nul'>
		<RemoveCheck <RemoveFile '~tmp.lst'>>;
}

RemoveCheck {
	True () = ;
	False (e.Error) = <Prout 'Error deleting file ' e.Error> <Exit 1>;
}

Nil {
	e.X =;
}

LoadFile {
	s.FDescr =
		<DoLoadFile s.FDescr <Get s.FDescr>>;
}

DoLoadFile {
	s.FDescr e.Line 0 =
		e.Line;
	s.FDescr e.Line =
		e.Line CrLf <DoLoadFile s.FDescr <Get s.FDescr>>;
}

ParseDirLst {
	e.Head CrLf CrLf e.Listing-and-Tail =
		<ParseListing e.Listing-and-Tail>;
	e.Other =
		<Prout 'ERROR dir listing'>;
}

ParseListing {
	e.Line CrLf e.Tail =
		<ParseLine e.Line> <ParseListing e.Tail>;
	 = ;
}

ParseLine {
	/* Итоговая информация о файлах и каталогах -- игнорируем */
	'         ' e.Tail =
		;

	/* Имя MS-DOS */
	s.f1 s.f2 s.f3 s.f4 s.f5 s.f6 s.f7 s.f8 ' ' s.e1 s.e2 s.e3
	/* Размер */
	s.s1 s.s2 s.s3 s.s4 s.s5 s.s6 s.s7 s.s8 s.s9 s.s10 s.s11 s.s12 s.s13 s.s14
	/* Дата */
	'  ' s.D1 s.D2 '.' s.M1 s.M2 '.' s.Y1 s.Y2 '  '
	/* Время */
	s.H1 s.H2 ':' s.Mi1 s.Mi2 ' '
	/* Имя Windows 95 */
	e.LongName =
		( <ParseDosName
			(s.f1 s.f2 s.f3 s.f4 s.f5 s.f6 s.f7 s.f8) s.e1 s.e2 s.e3
			>
			<ParseSize s.s1 s.s2 s.s3 s.s4 s.s5 s.s6 s.s7
				s.s8 s.s9 s.s10 s.s11 s.s12 s.s13 s.s14
			>
			<ParseDate (s.D1 s.D2) (s.M1 s.M2) (s.Y1 s.Y2)>
			<ParseTime (s.H1 s.H2) (s.Mi1 s.Mi2)>
			<ParseLongName e.LongName>
		)
}

ParseDosName {
	(e.Name) e.Ext =
		(DosName (<Trim e.Name>) <Trim e.Ext>);
}

Trim {
	' ' e.Line = <Trim e.Line>;
	e.Line ' ' = <Trim e.Line>;
	e.Line = e.Line;
}

ParseSize {
	e.Spaces1 '<' e.Dir '>' e.Spaces2 =
		(Directory);
	e.Size-B ' ' e.Size-E =
		<ParseSize e.Size-B e.Size-E>;
	e.Size =
		(Size <Numb e.Size>);
}

ParseDate {
	(e.Day) (e.Month) ('0' e.Year) =
		(Date <Numb e.Day> <Numb e.Month> <Numb '200' e.Year>);
	(e.Day) (e.Month) (e.Year) =
		(Date <Numb e.Day> <Numb e.Month> <Numb '19' e.Year>);
}

ParseTime {
	(e.Hours) (e.Minutes) =
		(Time <Numb <Trim e.Hours>> <Numb e.Minutes>);
}

ParseLongName {
	e.LongName =
		(LongName <Trim e.LongName>);
}
