$MODULE MChangeCalls;

// Функции данного модуля в процессе сканирования таблицу не изменяют.

$IMPORT MExtTable;
$IMPORT MFilesLdSv;
$IMPORT MEnv;

$ENTRY Change
	t.Table e.FileContent =
		<DoChange t.Table ( /* Output */ ) e.FileContent>;

DoChange {
	t.Table (e.Converted) ( e.Begin '<' e.End ) e.Tail =
		<DoChange
			t.Table
			( e.Converted
				( e.Begin '<'
					<ChangeCallee
						t.Table <FindCalleeEnd e.End>
					>
				)
			) e.Tail
		>;

	t.Table (e.Converted) t.NextLine e.Tail =
		<DoChange t.Table (e.Converted t.NextLine) e.Tail>;

	t.Table (e.Converted) =
		<MFilesLdSv::Save
			( <MEnv::GetOutName> )
			('$MODULE ' <MEnv::GetModuleName> ';')
			() e.Converted ()
			('$END ' <MEnv::GetModuleName> '.')
		>;
}

$IMPORT Refal5;

FindCalleeEnd {
	' ' e.Tail = (' ' e.Tail);
	'>' e.Tail = ('>' e.Tail);
	'(' e.Tail = ('(' e.Tail);
	'<' e.Tail = ('<' e.Tail);
	 = ();

	s.Other e.Tail = s.Other <FindCalleeEnd e.Tail>;
}

ChangeCallee {
	t.Table e.Callee (e.Rest) =
		<MExtTable::Qualify t.Table e.Callee>
		<ChangeIfNeed t.Table e.Rest>;
}

ChangeIfNeed {
	t.Table e.Begin '<' e.End =
		e.Begin '<' <ChangeCallee t.Table <FindCalleeEnd e.End> >;

	t.Table e.Line = e.Line;
}

$END MChangeCalls.