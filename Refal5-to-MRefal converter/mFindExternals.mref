$MODULE MFindExternals;

$IMPORT MExtTable;
$IMPORT MFilesLdSv;
$IMPORT MR2M-Error;

$ENTRY BuildTable
	e.FileName =
		<DoBuildTable
			<MExtTable::Create>
			( /* Scanned */ )
			<MFilesLdSv::Load e.FileName>
		>;

DoBuildTable {
	t.Table ( e.Scanned )
	('*$FROM Error') ('$EXTERN ' e.FnList) e.Tail =
		<DoBuildTable
			<MExtTable::Add t.Table ('MError') e.FnList>
			( e.Scanned ('$IMPORT MError;') ) e.Tail
		>;

	t.Table ( e.Scanned )
	('*$FROM ' e.ModName) ('$EXTERN ' e.FnList) e.Tail =
		<DoBuildTable
			<MExtTable::Add t.Table (e.ModName) e.FnList>
			( e.Scanned ('$IMPORT ' e.ModName ';') ) e.Tail
		>;

	t.Table ( e.Scanned ) ('*$FROM ' e.ModName) e.OtherTail =
		<MR2M-Error::Halt '$EXTERN after *$FROM expected'>;

	t.Table ( e.Scanned ) ('$EXTERN ' e.FnList) e.OtherTail =
		<MR2M-Error::Halt 'Unexpected $EXTERN'>;

	t.Table ( e.Scanned ) t.Other e.Tail =
		<DoBuildTable
			t.Table ( e.Scanned t.Other ) e.Tail
		>;

	t.Table ( e.Scanned ) = t.Table e.Scanned;
}

$END MFindExternals.