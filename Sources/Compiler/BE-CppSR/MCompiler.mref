$MODULE BE-CppSR::MCompiler;

$IMPORT Algorithm = BE-CppSR::Algorithm;
$IMPORT Generator = BE-CppSR::Generator;
$IMPORT MLambda;
$IMPORT MPredeclGenerator;
$IMPORT MQualifiedName;
$IMPORT MStrings;
$IMPORT BE-CppSR::MFunctionsAndIdents;

/**
  <Generate t.hIModule e.OutName>
    == t.hIModule
*/
$ENTRY Generate {
  t.hIModule e.OutName '.cpp' =
    <Generate-Aux
      <BE-CppSR::MFunctionsAndIdents::Extract t.hIModule> e.OutName
    >;
}

Generate-Aux {
  t.hIModule (e.Functions) (e.Identifiers) e.OutName =
    <MPredeclGenerator::Generate-Generic
      <MLambda::BindLeft & Generate-Properties (e.Functions) (e.Identifiers)>
      t.hIModule e.OutName '.cpp'
    >;
}

Generate-Properties {
  (e.Functions) (e.Identifiers) GenCommonHeaders
    = <Generator::GenCommonHeaders>
      <Generator::GenExternsEnum e.Functions>
      <Generator::GenIdentsEnum e.Identifiers>;

  (e.Functions) (e.Identifiers) e.Other
    = <Generate-Properties-Old e.Other>;
}

Generate-Properties-Old {
  GenSwap = & Generator.GenSwap;
  SwitchEnum Entry = & Generator.GenEnum;
  SwitchEnum Local = & Generator.GenEnum;
  GenCommonHeaders = <Generator::GenCommonHeaders>;
  GenIdent = & GenIdent;
  GenFunction-Decl = & GenFunction-Decl;
  Generate-EntryCode-Go = & Generate-EntryCode-Go;
  Generate-EntryCode-InitFinal = & Generate-EntryCode-InitFinal;
  GenFunction-Code = & GenFunction-Code;
  GenNativeInsertion = & Generator.GenNativeInsertion;
  GenNativeInsertion-Body = & Generator.GenNativeInsertion-Body;
}

GenIdent
  e.Name = <Generator::GenIdent <MQualifiedName::Create (e.Name)>>;

GenFunction-Decl {
  Entry t.FunctionName =
    <Generator::GenExtern t.FunctionName>;

  Local t.FunctionName =
    <Generator::GenExtern t.FunctionName>;

  ADT t.FunctionName =
    <Generator::GenADT t.FunctionName>;

  Swap t.FunctionName =
    <Generator::GenExtern t.FunctionName>;

  InitFinal t.FunctionName =
    <Generator::GenExtern t.FunctionName>;
}

GenFunction-Code {
  t.FuncName s.ScopeClass e.Sentences =
    <Generator::GenFunction
      t.FuncName s.ScopeClass
      <Algorithm::MakeAlgorithm e.Sentences>
    >;
}

Generate-EntryCode-Go
  t.FnProperties t.ModuleName (e.Result) e.Imports =
    <MPredeclGenerator::GenFunction
      t.FnProperties
      <MQualifiedName::Create (Go)>
      Entry
      (
        (Pattern)
        (Result
          <MLambda::Map & GenInitImport e.Imports>
          e.Result
          (Function
            (Entry NoPos <MQualifiedName::Create ('Exit' Entry)>)
            (Macro 0) // код возврата
          )
        )
      )
    >
    <MPredeclGenerator::GenFunction
      t.FnProperties
      <MQualifiedName::Create ('Exit' Entry)>
      Entry
      (
        (Pattern (SVar 'RetCode'))
        (Result
          <MLambda::Map
            & GenFinalImport
            <MStrings::Reverse e.Imports>
          >
          (Function
            (Entry NoPos <MQualifiedName::Create ('Exit')>)
            (SVar 'RetCode')
          )
        )
      )
    >;

Generate-EntryCode-InitFinal
  t.FnProperties t.ModuleName t.InitSeq t.FinalSeq e.Imports =
    <MPredeclGenerator::GenFunction
      t.FnProperties
      <MQualifiedName::AddSubName t.ModuleName 'Init' Entry>
      InitFinal
      (
        (Pattern (Identifier NoPos 'Init'))
        Init
        (Result
          <MLambda::Map & GenInitImport e.Imports>
          <GenEntry t.InitSeq>
        )
      )
      (
        (Pattern (Identifier NoPos 'Final'))
        Final
        (Result
          <GenEntry t.FinalSeq>
          <MLambda::Map
            & GenFinalImport
            <MStrings::Reverse e.Imports>
          >
        )
      )
    >;

GenEntry {
  None = ;
  (Result e.Result) = e.Result;
}

GenInitImport {
  t.ImportName =
    (Function
      (
        Entry NoPos
        <MQualifiedName::AddSubName t.ImportName 'Init' Entry>
      )
      (Identifier NoPos 'Init')
    );
}

GenFinalImport {
  t.ImportName =
    (Function
      (
        Entry NoPos
        <MQualifiedName::AddSubName t.ImportName 'Init' Entry>
      )
      (Identifier NoPos 'Final')
    );
}

$END BE-CppSR::MCompiler.

