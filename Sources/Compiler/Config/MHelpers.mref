$MODULE Config::MHelpers;

$IMPORT MError;
$IMPORT MFileSystem;
$IMPORT MLambda;
$IMPORT CmdLine::MParser;

$ENTRY TuneEndConfig = ();

/**
  <GenExtent e.ExtentDescription>
    == t.FnExtent

  e.ExtentDescription ::=
    (s.OptionId (e.OptionName) e.DefaultValue)*
*/
$ENTRY GenExtent
  e.ExtentDescription =
    (
      & TuneEndConfig
      <MLambda::Map
        & GenExtentEntry e.ExtentDescription
      >
    );

GenExtentEntry {
  (Single s.OptionId (e.OptionName) e.DefaultValue) =
    ((Param (e.OptionName))
      <MLambda::BindLeft & Handler-Single s.OptionId (e.OptionName)>
      (Optional)
      OneTime
    );

  (SinglePath s.OptionId (e.OptionName) e.DefaultValue) =
    ((Param (e.OptionName))
      <MLambda::BindLeft & Handler-SinglePath s.OptionId (e.OptionName)>
      (Optional)
      OneTime
    );

  (FileList s.OptionId (e.OptionName) e.DefaultValue) =
    ((Naked (e.OptionName))
      <MLambda::BindLeft & Handler-FilePathList s.OptionId (e.OptionName)>
      (Optional)
      OneTime
      Required & FileList-GroupHandler
    );

  (PathList s.OptionId (e.OptionName) e.DefaultValue) =
    ((Naked (e.OptionName))
      <MLambda::BindLeft & Handler-FilePathList s.OptionId (e.OptionName)>
      (Optional)
      OneTime
      Required & PathList-GroupHandler
    );
}

Handler-Single
  s.OptionName (e.OptionText)
  t.hErrorList (e.TunerConfig) t.SrcPos (e.Option) NoGroup =
    t.hErrorList
    (e.TunerConfig (s.OptionName (e.OptionText) t.SrcPos e.Option));

Handler-SinglePath
  s.OptionName (e.OptionText)
  t.hErrorList (e.TunerConfig) t.SrcPos (e.StrPath) NoGroup =
    t.hErrorList
    (e.TunerConfig
      (s.OptionName (e.OptionText) t.SrcPos <FixupPath-Str t.SrcPos e.StrPath>)
    );

FixupPath-Str {
  t.SrcPos e.StrPath =
    <CmdLine::MParser::FixupPath-Str t.SrcPos e.StrPath>;
}

Handler-FilePathList
  s.OptionName (e.OptionText)
  t.hErrorList (e.TunerConfig) t.SrcPos NoParam (e.FileList) =
    t.hErrorList
    (e.TunerConfig (s.OptionName (e.OptionText) t.SrcPos e.FileList));

FileList-GroupHandler
  =
    (
      & FilePathList-Config
      ((Param (File))
        & FileList-FileHandler
        (Optional)
        Multiply
      )
      ((Naked (EOG))
        & FilePathList-EOGHandler
        (Required)
        OneTime
      )
    );

FilePathList-Config = ();

FileList-FileHandler
  t.hErrorList (e.FileList) t.SrcPos (e.PtFileName) NoGroup =
    t.hErrorList (e.FileList (e.PtFileName));

FilePathList-EOGHandler
  t.hErrorList (e.FileList) t.SrcPos NoParam NoGroup =
    t.hErrorList (e.FileList);

PathList-GroupHandler
  =
    (
      & FilePathList-Config
      ((Param (File))
        & PathList-FileHandler
        (Optional)
        Multiply
      )
      ((Naked (EOG))
        & FilePathList-EOGHandler
        (Required)
        OneTime
      )
    );

PathList-FileHandler
  t.hErrorList (e.FileList) t.SrcPos (e.PtFileName) NoGroup =
    t.hErrorList (e.FileList (<FixupPath-Pt t.SrcPos e.PtFileName>));

FixupPath-Pt {
  t.SrcPos e.PtFileName =
    <CmdLine::MParser::FixupPath-Pt t.SrcPos e.PtFileName>;
}

/**
  <InitConfig s.FnExtentDescription e.Tunings>
    == (e.Config)

  <s.FnExtentDescription>
    == e.ExtentDescription
*/
$ENTRY InitConfig
  s.FnDescription e.Tunings =
    (
      <MLambda::Map
        <MLambda::BindLeft & InitTuning e.Tunings>
        <s.FnDescription>
      >
    );

InitTuning
  e.Tunings (s.OptionType s.OptionId (e.OptionName) e.Default) =
    (<GetTuning s.OptionId e.Tunings (e.Default)>);

/*
  <GetTuning s.Tuning e.TunerList (e.Default)>
    == e.TunerValue
*/
GetTuning
  s.Tuning e.Tunings (e.Default) =
    <GetTuning-Aux
      s.Tuning
      e.Tunings (s.Tuning ('default') Default e.Default)
    >;

GetTuning-Aux
  s.Tuning e.Tunings-B (s.Tuning (e.Name) t.SrcPos e.Value) e.Tunigs-E =
    s.Tuning e.Value;

/**
  <ConfigExtract t.Config s.Option>
    == e.Value
*/
$ENTRY ConfigExtract
  (e.Config-B (s.Option e.Value) e.Config-E) s.Option = e.Value;

$END Config::MHelpers.
