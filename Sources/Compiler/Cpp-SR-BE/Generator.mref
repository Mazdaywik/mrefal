$MODULE Cpp-SR-BE::Generator;

$IMPORT Cpp-SR-BE::Algorithm;
$IMPORT Math;
$IMPORT MLambda;
$IMPORT MStrings;
$IMPORT MQualifiedName;

QNameCode {
  t.Name = <DoQNameCode <MQualifiedName::Parse t.Name>>;
}

DoQNameCode {
  (e.First) = <NameCode e.First>;

  (e.First) (e.Second) =
    <NameCode e.First> 'P_' <NameCode e.Second>;

  (e.First) (e.Second) e.Tail =
    <NameCode e.First> 'P_' <DoQNameCode (e.Second) e.Tail>;
}

NameCode {
  e.Name = <MLambda::Map & NameCodeChar e.Name>;
}

NameCodeChar {
  '!' = 'B_';
  '?' = 'Q_';
  '_' = 'U_';
  '-' = 'D_';
  Entry = 'E_';
  s.Other = s.Other;
}

FuncArguments {
  = 'refalrts::Iter arg_begin, refalrts::Iter arg_end';
}

NakedArguments {
  = 'refalrts::Iter, refalrts::Iter';
}

TextFromMemoryClass {
  Local = 'static '; Entry = ; InitFinal = ;
}

SwGenFnStart {
  s.MemoryClass s.FnArguments t.Name =
    (
      <TextFromMemoryClass s.MemoryClass>
      'refalrts::FnResult '
      <QNameCode t.Name>
      '(' <s.FnArguments> ') {'
    )
    ('  refalrts::this_is_generated_function();');
}

$ENTRY GenInitCounter {
  InitFinal =
    ('  static unsigned s_counter = 0;')
    ('  refalrts::use_counter(s_counter);');

  s.Other = ;
}

$ENTRY GenFnStart {
  s.MemoryClass t.Name =
    <SwGenFnStart s.MemoryClass & FuncArguments t.Name>;
}

$ENTRY GenFnEnd {
  = ('  return refalrts::cRecognitionImpossible;') ('}') ();
}

$ENTRY GenEEnum {
  t.Name = <SwGenFnStart Entry & NakedArguments t.Name> <GenFnEnd>;
}

$ENTRY GenEnum {
  t.Name = <SwGenFnStart Local & NakedArguments t.Name> <GenFnEnd>;
}

$ENTRY GenExtern {
  t.Name =
    (
      'extern refalrts::FnResult '
      <QNameCode t.Name>
      '(' <FuncArguments> ');'
    )
    ();
}

$ENTRY GenForward {
  t.Name =
    (
      'static refalrts::FnResult '
      <QNameCode t.Name>
      '(' <FuncArguments> ');'
    )
    ();
}

$ENTRY GenESwap {
  t.Name = <GenSwap_Aux Entry t.Name>;
}

$ENTRY GenSwap {
  t.Name = <GenSwap_Aux Local t.Name>;
}

GenSwap_Aux {
  s.MemoryClass t.Name =
    <GenFnStart s.MemoryClass t.Name>
    ('  refalrts::Iter func_name = arg_begin;')
    ()
    ('  refalrts::move_left( func_name, arg_end );')
    ()
    ('  refalrts::Iter info_b = func_name;')
    ('  refalrts::Iter info_e = arg_end;')
    ()
    ('  refalrts::move_left( info_b, info_e );')
    ('  refalrts::move_right( info_b, info_e );')
    ()
    ('  static refalrts::Iter s_head = refalrts::initialize_swap_head( func_name );')
    ()
    ('  refalrts::Iter saved_b;')
    ('  refalrts::Iter saved_e;')
    ()
    ('  refalrts::swap_info_bounds( saved_b, saved_e, s_head );')
    ('  refalrts::splice_evar( arg_begin, saved_b, saved_e );')
    ('  refalrts::swap_save( s_head, info_b, info_e );')
    ('  refalrts::splice_to_freelist( arg_begin, arg_end );')
    ()
    ('  return refalrts::cSuccess;')
    ('}')
    ();
}

$ENTRY GenIdent {
  t.Name =
    ('//$LABEL ' <MQualifiedName::ToPrintable-Dot t.Name>)
    ('template <typename T>')
    ('struct ' <QNameCode t.Name> 'L_ {')
    ('  static const char *name() {')
    ('    return "' <MQualifiedName::ToPrintable-Dot t.Name> '";')
    ('  }')
    ('};')
    ();
}

$ENTRY GenCommonHeaders {
  = ('// Automatically generated file. Don\'t edit!')
  ('#include "refalrts.h"') () ();
}


$ENTRY GenSentence {
  e.Algorithm =
    ('  do {')
    ('    refalrts::Iter ' <BracketsVar_B 0> ' = arg_begin;')
    ('    refalrts::Iter ' <BracketsVar_E 0> ' = arg_end;')
    ('    refalrts::move_left( ' <BracketsVar_B 0> ', ' <BracketsVar_E 0> ' );')
    ('    refalrts::move_left( ' <BracketsVar_B 0> ', ' <BracketsVar_E 0> ' );')
    ('    refalrts::move_right( ' <BracketsVar_B 0> ', ' <BracketsVar_E 0> ' );')
    <GenSentence_Aux
      <MLambda::MapReduce
        & GenCommand
        ('    ' /* indent */)
        e.Algorithm
      >
    >;
}

GenSentence_Aux {
  (e.Indent) e.Generated =
    e.Generated <GenCloseBrackets e.Indent> ();
}

BracketsVar_B {
  s.BracketNumber = 'bb_' <MStrings::Symb s.BracketNumber>;
}

BracketsVar_E {
  s.BracketNumber = 'be_' <MStrings::Symb s.BracketNumber>;
}

//BracketsVar-OE {
//  s.BracketNumber = 'boe_' <StrFromInt s.BracketNumber>;
//}

/*
  Величина отступа также определяет режим работы. Отступ в 4 пробела --
  вне цикла по открытой e-переменной, отступ больше -- внутри оного цикла.
*/

PrintStkBracket {
  e.Indent s.StkBracket =
    (e.Indent
      'refalrts::Iter ' <BracketsVar_B s.StkBracket> '_stk = '
        <BracketsVar_B s.StkBracket> ';'
    )
    (e.Indent
      'refalrts::Iter ' <BracketsVar_E s.StkBracket> '_stk = '
        <BracketsVar_E s.StkBracket> ';'
    );
}

PrintRecoverStackBracket {
  e.Indent s.StkBracket =
    (e.Indent
      <BracketsVar_B s.StkBracket> ' = ' <BracketsVar_B s.StkBracket> '_stk,'
    )
    (e.Indent
      <BracketsVar_E s.StkBracket> ' = ' <BracketsVar_E s.StkBracket> '_stk,'
    );
}

GenCommand {
  (e.Indent) (CmdComment e.Text) = (e.Indent) (e.Indent '//' e.Text);

  (e.Indent) (CmdDeclareEVar e.VarInfo) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <PrintEVar_B e.VarInfo> ';')
    (e.Indent 'refalrts::Iter ' <PrintEVar_E e.VarInfo> ';');

  (e.Indent) (CmdDeclareVar e.VarInfo) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <PrintVar e.VarInfo> ';');

  (e.Indent) (CmdChar s.Direction s.BracketNumber s.Char) =
    (e.Indent)
    <PrintMatchChar (e.Indent) s.Direction s.BracketNumber s.Char>;

  (e.Indent) (CmdNumber s.Direction s.BracketNumber s.Number) =
    (e.Indent)
    <PrintMatchNumber (e.Indent) s.Direction s.BracketNumber s.Number>;

  (e.Indent) (CmdName s.Direction s.BracketNumber t.Name) =
    (e.Indent)
    <PrintMatchName (e.Indent) s.Direction s.BracketNumber t.Name>;

  (e.Indent) (CmdIdent s.Direction s.BracketNumber t.Name) =
    (e.Indent)
    <PrintMatchIdent (e.Indent) s.Direction s.BracketNumber t.Name>;

  (e.Indent) (CmdBrackets s.Direction s.BracketNumber s.InnerBrackets) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <BracketsVar_B s.InnerBrackets> ' = 0;')
    (e.Indent 'refalrts::Iter ' <BracketsVar_E s.InnerBrackets> ' = 0;')
    <PrintMatchBrackets (e.Indent) s.Direction s.BracketNumber s.InnerBrackets>;

  (e.Indent) (CmdADT s.Direction s.BracketNumber s.InnerBrackets e.Name) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <BracketsVar_B s.InnerBrackets> ' = 0;')
    (e.Indent 'refalrts::Iter ' <BracketsVar_E s.InnerBrackets> ' = 0;')
    <PrintMatchADT
      (e.Indent) s.Direction s.BracketNumber s.InnerBrackets e.Name
    >;

  (e.Indent) (CmdEmpty s.BracketNumber) =
    (e.Indent)
    <PrintMatchEmpty (e.Indent) s.BracketNumber>;

  (e.Indent) (CmdVar s.Direction s.BracketNumber s.Mode e.Info) =
    (e.Indent)
    <PrintMatchSTVar (e.Indent) s.Direction s.BracketNumber s.Mode e.Info>;

  (e.Indent) (CmdRepeated s.Direction s.BracketNumber e.VarInfo) =
    (e.Indent)
    <PrintMatchRepeated (e.Indent) s.Direction s.BracketNumber e.VarInfo>;

  (e.Indent) (CmdClosedE AlgLeft s.BracketNumber 'e' e.Index) =
    (e.Indent)
    (e.Indent
      <PrintEVar_B 1 'e' e.Index> ' = ' <BracketsVar_B s.BracketNumber> ';'
    )
    (e.Indent 'refalrts::use( ' <PrintEVar_B 1 'e' e.Index> ' );')
    (e.Indent
      <PrintEVar_E 1 'e' e.Index> ' = ' <BracketsVar_E s.BracketNumber> ';'
    )
    (e.Indent 'refalrts::use( ' <PrintEVar_E 1 'e' e.Index> ' );');

  (e.Indent)
  (CmdOpenedE AlgLeft s.BracketNumber 'e' e.Index (e.BracketsForSave)) =
    (e.Indent '  ')
    <MLambda::Map
      <MLambda::BindLeft & PrintStkBracket e.Indent>
      e.BracketsForSave
    >
    (e.Indent 'for( ')
    (e.Indent '  refalrts::Iter')
    (e.Indent
      '    ' <PrintEVar_B 1 'e' e.Index> ' = '
        <BracketsVar_B s.BracketNumber> '_stk,'
    )
    (e.Indent
      '    ' <PrintEVar_OE 1 'e' e.Index> ' = '
        <BracketsVar_B s.BracketNumber> '_stk,'
    )
    (e.Indent
      '    ' <BracketsVar_B s.BracketNumber> ' = '
        <BracketsVar_B s.BracketNumber> '_stk,'
    )
    (e.Indent
      '    ' <BracketsVar_E s.BracketNumber> ' = '
        <BracketsVar_E s.BracketNumber> '_stk;'
    )
    (e.Indent '  '
      '! empty_seq( ' <PrintEVar_OE 1 'e' e.Index> ', '
      <BracketsVar_E s.BracketNumber> ' );'
    )
    <MLambda::Map
      <MLambda::BindLeft & PrintRecoverStackBracket e.Indent '  '>
      e.BracketsForSave
    >
    (e.Indent '  '
      'refalrts::next_term( ' <PrintEVar_OE 1 'e' e.Index> ', '
      <BracketsVar_E s.BracketNumber> ' )'
    )
    (e.Indent ') {')
    (e.Indent '  '
      <BracketsVar_B s.BracketNumber> ' = ' <PrintEVar_OE 1 'e' e.Index> ';'
    )
    (e.Indent '  '
      <PrintEVar_B 1 'e' e.Index> ' = ' <BracketsVar_B s.BracketNumber> '_stk;'
    )
    (e.Indent '  '
      <PrintEVar_E 1 'e' e.Index> ' = ' <PrintEVar_OE 1 'e' e.Index> ';'
    )
    (e.Indent
      '  refalrts::move_right( ' <PrintEVar_B 1 'e' e.Index> ', '
      <PrintEVar_E 1 'e' e.Index> ' );'
    );

  (e.Indent) (CmdEmptyResult) =
    (e.Indent)
    ()
    (e.Indent 'refalrts::reset_allocator();')
    (e.Indent 'refalrts::Iter res = arg_begin;');

  (e.Indent) (CmdCopyEVar s.Usings 'e' e.Index) =
    (e.Indent)
    (e.Indent
      'if( ! refalrts::copy_evar( ' <PrintEVar_B s.Usings 'e' e.Index> ', '
      <PrintEVar_E s.Usings 'e' e.Index> ', '
      <PrintEVar_B <Math::Dec s.Usings> 'e' e.Index> ', '
      <PrintEVar_E <Math::Dec s.Usings> 'e' e.Index>
      ' ) )'
    )
    (e.Indent '  return refalrts::cNoMemory;');

  (e.Indent) (CmdCopyVar s.Usings s.Mode e.Index) =
    (e.Indent)
    (e.Indent
      'if( ! refalrts::copy_stvar( ' <PrintVar s.Usings s.Mode e.Index> ', '
      <PrintVar <Math::Dec s.Usings> s.Mode e.Index> ' ) )'
    )
    (e.Indent '  return refalrts::cNoMemory;');

  (e.Indent) (CmdAllocateElem s.ElemNumber e.ElemInfo) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <PrintElemVar s.ElemNumber> ' = 0;')
    <PrintAllocateElem (e.Indent) s.ElemNumber e.ElemInfo>;

  (e.Indent) (CmdAllocateString s.BeginNumber s.EndNumber s.Len e.String) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <PrintElemVar s.BeginNumber> ' = 0;')
    (e.Indent 'refalrts::Iter ' <PrintElemVar s.EndNumber> ' = 0;')
    (e.Indent 'if( ! refalrts::alloc_chars( '
      <PrintElemVar s.BeginNumber> ', ' <PrintElemVar s.EndNumber> ', '
      '\"' <MLambda::Map & Escape e.String> '\", ' <MStrings::Symb s.Len> ' ) )'
    )
    (e.Indent '  return refalrts::cNoMemory;');

  (e.Indent) (CmdLinkBrackets s.Left s.Right) =
    (e.Indent)
    (e.Indent
      'refalrts::link_brackets( '
      <PrintElemVar s.Left> ', ' <PrintElemVar s.Right>
      ' );'
    );

  (e.Indent) (CmdPushStack s.ElemNumber) =
    (e.Indent)
    (e.Indent 'refalrts::push_stack( ' <PrintElemVar s.ElemNumber> ' );');

  (e.Indent) (CmdInsertElem ElElem s.ElemNumber) =
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_elem( res, ' <PrintElemVar s.ElemNumber> ' );'
    );

  (e.Indent) (CmdInsertElem ElSTVar s.Usings s.Mode e.Index) =
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_stvar( res, '
      <PrintVar s.Usings s.Mode e.Index> ' );'
    );

  (e.Indent) (CmdInsertString s.NumBegin s.NumEnd) =
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_evar( res, '
      <PrintElemVar s.NumBegin> ', ' <PrintElemVar s.NumEnd> ' );'
    );

  (e.Indent) (CmdInsertEVar s.Usings 'e' e.Index) =
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_evar( res, '
      <PrintEVar_B s.Usings 'e' e.Index> ', '
      <PrintEVar_E s.Usings 'e' e.Index> ' );'
    );

  (e.Indent) (CmdReturnResult) =
    (e.Indent)
    (e.Indent 'refalrts::use( res );')
    (e.Indent 'refalrts::splice_to_freelist( arg_begin, arg_end );')
    (e.Indent 'return refalrts::cSuccess;');

  (e.Indent) (CmdStartEntryCode s.EntryType) =
    (e.Indent '  ')
    (e.Indent 'if( ' <EntryCondition s.EntryType> ' ) {');

  (e.Indent '  ') (CmdEndEntryCode) =
    (e.Indent)
    (e.Indent '}');
}

EntryCondition {
  Init = '(s_counter ++) == 0';
  Final = '(-- s_counter) == 0';
}

PrintVar {
  s.Usings s.Mode e.Index =
    s.Mode <NameCode e.Index> '_' <MStrings::Symb s.Usings>;
}

PrintEVar_B {
  s.Usings s.Mode e.Index =
    s.Mode <NameCode e.Index> '_b_' <MStrings::Symb s.Usings>;
}

PrintEVar_E {
  s.Usings s.Mode e.Index =
    s.Mode <NameCode e.Index> '_e_' <MStrings::Symb s.Usings>;
}

PrintEVar_OE {
  s.Usings s.Mode e.Index =
    s.Mode <NameCode e.Index> '_oe_' <MStrings::Symb s.Usings>;
}

Escape {
  '\n' = '\\n';
  '\t' = '\\t';
  '\r' = '\\r';
  '\\' = '\\\\';
  '\'' = '\\' '\'';
  '\"' = '\\' '\"';
  s.Other = s.Other;
}

PrintMatchChar {
  (e.Indent) s.Direction s.BracketNumber s.Char =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('char') '\'' <Escape s.Char> '\''
    >;
}

PrintMatchNumber {
  (e.Indent) s.Direction s.BracketNumber s.Number =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('number') <MStrings::Symb s.Number> 'UL'
    >;
}

PrintMatchName {
  (e.Indent) s.Direction s.BracketNumber t.Name =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('function') '& ' <QNameCode t.Name>
    >;
}

PrintMatchIdent {
  (e.Indent) s.Direction s.BracketNumber t.Name =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('ident') ' & ' <QNameCode t.Name> 'L_<int>::name'
    >;
}

PrintMatchBrackets {
  (e.Indent) s.Direction s.BracketNumber s.InnerNum =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('brackets')
      <BracketsVar_B s.InnerNum> ', ' <BracketsVar_E s.InnerNum>
    >;
}

PrintMatchADT {
  (e.Indent) s.Direction s.BracketNumber s.InnerNum t.Name =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('adt')
      <BracketsVar_B s.InnerNum> ', '
      <BracketsVar_E s.InnerNum> ', '
      '& ' <QNameCode t.Name>
    >;
}

BreakerFromIndent {
  '    ' = 'break;';

  e.OtherIndent = 'continue;';
}

PrintMatchEmpty {
  (e.Indent) s.BracketNumber =
    (e.Indent
      'if( ! empty_seq( ' <BracketsVar_B s.BracketNumber> ', '
      <BracketsVar_E s.BracketNumber> ' ) )'
    )
    (e.Indent '  ' <BreakerFromIndent e.Indent>);
}

StrFromDirection {
  AlgLeft = 'left'; AlgRight = 'right';
}

VarMatchFunction { 's' = 'svar'; 't' = 'tvar'; }

PrintMatchSTVar {
  (e.Indent) s.Direction s.BracketNumber s.Mode e.Index =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      (<VarMatchFunction s.Mode>) <PrintVar 1 s.Mode e.Index>
    >;
}

PrintMatchRepeated {
  (e.Indent) s.Direction s.BracketNumber s.Usings 'e' e.Index =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('repeated_evar')
      <PrintEVar_B s.Usings 'e' e.Index> ', '
      <PrintEVar_E s.Usings 'e' e.Index> ', '
      <PrintEVar_B <Math::Dec s.Usings> 'e' e.Index> ', '
      <PrintEVar_E <Math::Dec s.Usings> 'e' e.Index>
    >;

  (e.Indent) s.Direction s.BracketNumber s.Usings s.Mode e.Index =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('repeated_stvar') <PrintVar s.Usings s.Mode e.Index>
      ', ' <PrintVar <Math::Dec s.Usings> s.Mode e.Index>
    >;
}

PrintMatch {
  (e.Indent) s.Direction s.BracketNumber (e.Function) e.TextRep =
    (e.Indent
      'if( ! refalrts::' e.Function '_' <StrFromDirection s.Direction>
      '( ' e.TextRep ', ' <BracketsVar_B s.BracketNumber> ', '
      <BracketsVar_E s.BracketNumber> ' ) ) '
    )
    (e.Indent '  ' <BreakerFromIndent e.Indent>);
}

PrintElemVar {
  s.Number = 'n' <MStrings::Symb s.Number>;
}

PrintAllocateElem {
  (e.Indent) s.ElemNumber s.ElementType e.Info =
    <PrintAllocate_Aux
      (e.Indent) s.ElemNumber
      ( <SwAllocator s.ElementType> )
      ( <SwInfo s.ElementType e.Info> )
    >
    (e.Indent '  return refalrts::cNoMemory;');
}

PrintAllocate_Aux {
  (e.Indent) s.ElemNumber (e.Constructor) (e.Value) =
    (e.Indent
      'if( ! refalrts::' e.Constructor '( ' <PrintElemVar s.ElemNumber>
      e.Value ' ) )'
    );
}

SwAllocator {
  ElChar = 'alloc_char';
  ElName = 'alloc_name';
  ElNumber = 'alloc_number';
  ElIdent = 'alloc_ident';

  ElOpenADT = 'alloc_open_adt';
  ElCloseADT = 'alloc_close_adt';

  ElOpenBracket = 'alloc_open_bracket';
  ElCloseBracket = 'alloc_close_bracket';

  ElOpenCall = 'alloc_open_call';
  ElCloseCall = 'alloc_close_call';
}

SwInfo {
  ElChar s.Char = ', ' '\'' <Escape s.Char> '\'';

  ElName t.Name = ', & ' <QNameCode t.Name> <Info-Ident t.Name>;

  ElNumber s.Number = ', ' <MStrings::Symb s.Number> 'UL';

  ElIdent t.Name = <Info-Ident t.Name>;

  // Остальное -- скобки
  s.Other = ;
}

Info-Ident t.Name = ', & ' <QNameCode t.Name> 'L_<int>::name';

GenCloseBrackets {
  '    ' = ('  } while ( 0 );');

  '  ' e.Indent =
    (e.Indent '}') <GenCloseBrackets e.Indent>;
}

$END Cpp-SR-BE::Generator.
