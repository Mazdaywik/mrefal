$MODULE Cpp-SR-BE::LibraryEx;
// Расширения библиотеки и функции, реализуемые
// средствами самого Простого Рефала

$ENTRY Apply {
  s.Fn e.Argument = <s.Fn e.Argument>;

  (t.Closure e.Bounded) e.Argument =
    <& Apply t.Closure e.Bounded e.Argument>;
}

$ENTRY Map {
  t.Fn t.Next e.Tail = <& Apply t.Fn t.Next> <& Map t.Fn e.Tail>;

  t.Fn = ;
}

$ENTRY MapReduce {
  t.Fn t.Acc e.Tail =
    <& DoMapReduce t.Fn t.Acc () e.Tail>;
}

 

DoMapReduce {
  t.Fn t.Acc (e.Scanned) t.Next e.Tail =
    <& DoMapReduce
      t.Fn
      <& MapReduce__AddScanned
        <& Apply t.Fn t.Acc t.Next> (e.Scanned)
      >
      e.Tail
    >;

  t.Fn t.Acc (e.Scanned) = t.Acc e.Scanned;
}

MapReduce__AddScanned {
  t.Acc e.StepScanned (e.Scanned) =
    t.Acc (e.Scanned e.StepScanned);
}

$IMPORT Cpp-SR-BE::Library;

$ENTRY Inc {
  s.Num = <& Cpp-SR-BE.Library.Add s.Num 1>;
}

$ENTRY Dec {
  s.Num = <& Cpp-SR-BE.Library.Sub s.Num 1>;
}

$END Cpp-SR-BE::LibraryEx.
