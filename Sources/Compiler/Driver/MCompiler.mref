$MODULE Driver::MCompiler;

$IMPORT MLambda;

$IMPORT Driver::MConfig;
$IMPORT Driver::MModuleFinder;
$IMPORT Driver::MModuleIF;
$IMPORT Driver::MResolver;
$IMPORT Driver::MUtils;
$IMPORT MError;
$IMPORT MIModule;
$IMPORT MOutModuleStatus;

$IMPORT MLookupTable = Driver::Tables::MLookup;
$IMPORT MModuleTable = Driver::Tables::MModules;
$IMPORT MModuleTree  = Driver::Tables::MModuleTree;
$IMPORT MTables      = Driver::MTables;

/**
  <Make t.hTables t.hErrorList t.Config t.SrcPos t.RootModuleName>
    == t.hTables t.hErrorList Success
    == t.hTables t.hErrorList Fails
*/
$ENTRY Make
  t.hTables t.hErrorList t.Config t.SrcPos t.RootModuleName =
    <Make-SwResult
      t.Config t.RootModuleName
      <ExternalName
        Recompile
        () t.Config t.hTables t.hErrorList
        t.SrcPos t.RootModuleName
      >
    >;

Make-SwResult {
  t.Config t.RootModuleName
  t.hTables t.hErrorList CantLoadInterface =
    t.hTables t.hErrorList Fails;

  t.Config t.RootModuleName
  t.hTables t.hErrorList t.ModuleIF =
    <Make-SwMainModule
      t.Config t.hTables t.hErrorList t.RootModuleName
      <Driver::MModuleIF::GetModuleType t.ModuleIF>
    >;
}

Make-SwMainModule {
  t.Config t.hTables t.hErrorList t.RootModuleName Main =
    <Make-Aux
      <Driver::MConfig::Link t.Config t.hTables t.RootModuleName>
      t.hErrorList
    >;

  t.Config t.hTables t.hErrorList t.RootModuleName Regular =
    <MOutModuleStatus::Flush>
    t.hTables t.hErrorList Success;
}

Make-Aux
  t.hTables s.Result t.hErrorList =
    t.hTables t.hErrorList s.Result;

//------------------------------------------------------------------------------

/**
  <ExternalName
    s.Type (e.Stack)
    t.Config t.hTables t.hErrorList t.SrcPos t.ImportName
  >
    == t.hTables t.hErrorList t.Interface_
  s.Type ::= Recompile | Updated | Library
  t.Interface_ ::= t.ModuleIF | CantLoadInterface
*/
ExternalName {
  s.Type (e.Stack-B t.ImportName e.Stack-E)
  t.Config t.hTables t.hErrorList t.SrcPos t.ImportName =
    t.hTables
    <MError::AddRecursiveLoop
      t.hErrorList t.SrcPos t.ImportName e.Stack-E t.ImportName
    >
    CantLoadInterface;

  s.Type (e.Stack)
  t.Config t.hTables t.hErrorList t.SrcPos t.ImportName =
    <ExternalName-CheckLookup
      s.Type (e.Stack)
      t.Config t.hTables t.hErrorList t.SrcPos t.ImportName
    >;
}

ExternalName-CheckLookup {
  s.Type (e.Stack)
  t.Config t.hTables t.hErrorList t.SrcPos t.ImportName =
    <ExternalName-SwSuccessModule
      s.Type (e.Stack) t.SrcPos t.ImportName
      <ExternalName-SwKnown
        s.Type (e.Stack) t.Config
        <MError::IsErrorList t.hErrorList>
        t.SrcPos t.ImportName
        <MTables::Lookup
          t.hTables & MLookupTable.LookupResult t.ImportName
        >
      >
    >;
}

ExternalName-SwKnown {
  s.Type (e.Stack) t.Config
  t.hErrorList t.SrcPos t.ImportName t.hTables Unknown =
    <OnUnknown
      (e.Stack) t.Config t.hTables t.hErrorList t.ImportName
    >;

  s.Type (e.Stack) t.Config
  t.hErrorList t.SrcPos t.ImportName t.hTables e.Known =
    t.hTables <MError::IsErrorList t.hErrorList> e.Known;
}

ExternalName-SwSuccessModule {
  s.Type (e.Stack) t.SrcPos t.ImportName
  t.hTables t.hErrorList Success =
    <ExternalName-OnKnown-StatusType
      s.Type (e.Stack)
      t.hErrorList t.SrcPos t.ImportName
      <MTables::Modules
        t.hTables &MModuleTable.GetStatusType t.ImportName
      >
    >;

  s.Type (e.Stack) t.SrcPos t.ImportName
  t.hTables t.hErrorList Error e.Error =
    t.hTables
    <ExternalName-AddImportError
      <MError::IsErrorList t.hErrorList>
      t.SrcPos t.ImportName e.Error
    >
    CantLoadInterface;
}

ExternalName-AddImportError {
  t.hErrorList t.SrcPos t.ImportName
  Error-CantLoadModule =
    <MError::AddBrokenImport
      <MError::IsErrorList t.hErrorList>
      t.SrcPos t.ImportName
    >;

  t.hErrorList t.SrcPos t.ImportName
  Error-MismatchWithInternal t.InternalName =
    <MError::AddMismatchInternalAndExternalNames
      <MError::IsErrorList t.hErrorList>
      t.SrcPos t.InternalName t.ImportName
    >;

  t.hErrorList t.SrcPos t.ImportName
  Error-Tree-ModulePacket s.Conflict t.KnownName =
    <MError::AddTreeModulePacketConflict
      <MError::IsErrorList t.hErrorList>
      t.SrcPos t.ImportName s.Conflict t.KnownName
    >;

  t.hErrorList t.SrcPos t.ImportName
  Error-Tree-CaseError t.ValidCaseName =
    <MError::AddTreeCaseError
      <MError::IsErrorList t.hErrorList>
      t.SrcPos t.ImportName t.ValidCaseName
    >;
}

ExternalName-OnKnown-StatusType {
  s.TypeMode (e.Stack) t.hErrorList t.SrcPos t.ImportName
  t.hTables Success Ready s.Type =
    <ExternalName-OnKnown-StatusType-SwTranslated
      (e.Stack) t.hTables t.hErrorList
      t.SrcPos t.ImportName <TranslateType s.TypeMode s.Type>
    >;

  //s.TypeMode (e.Stack) t.hErrorList t.SrcPos t.ImportName
  //t.hTables Success Cluster s.Type =
  //  <ExternalName-OnKnown-StatusType-SwTranslated
  //    (e.Stack) t.hTables t.hErrorList
  //    t.SrcPos t.ImportName <TranslateType s.TypeMode s.Type>
  //  >;
}

TranslateType {
  s.TypeMode Errors    = CantLoadInterface;
  Updated    Recompile = CantLoadInterface;
  Library    Recompile = IsNotLibrary;
  Library    Updated   = IsNotLibrary;
  s.TypeMode s.Type    = LoadInterface;
}

ExternalName-OnKnown-StatusType-SwTranslated {
  (e.Stack) t.hTables t.hErrorList
  t.SrcPos t.ImportName CantLoadInterface =
    t.hTables
    <MError::AddBrokenImport
      t.hErrorList t.SrcPos t.ImportName
    >
    CantLoadInterface;

  (e.Stack) t.hTables t.hErrorList
  t.SrcPos t.ImportName IsNotLibrary =
    t.hTables
    <MError::DependedFromSource
      t.hErrorList t.SrcPos t.ImportName
    >
    CantLoadInterface;

  (e.Stack) t.hTables t.hErrorList
  t.SrcPos t.ImportName LoadInterface =
    <ExternalName-CheckModuleType
      <Step-From-Stack e.Stack>
      <Driver::MModuleIF::LoadCached t.hTables t.ImportName>
      t.hErrorList t.SrcPos t.ImportName
    >;
}

Step-From-Stack {
  /* пустой стек */ = RootModule;
  e.NotEmptyStack   = ImportModule;
}

ExternalName-CheckModuleType {
  s.Step t.hTables t.ModuleIF
  t.hErrorList t.SrcPos t.ImportName =
    <ExternalName-SwCheckModuleType
      t.hTables t.hErrorList t.SrcPos t.ImportName
      t.ModuleIF s.Step
      <Driver::MModuleIF::GetModuleType t.ModuleIF>
    >;
}

ExternalName-SwCheckModuleType {
  t.hTables t.hErrorList t.SrcPos t.ImportName
  t.ModuleIF ImportModule Main =
    t.hTables
    <MError::AddImportHead
      t.hErrorList t.SrcPos t.ImportName
    >
    CantLoadInterface;

  t.hTables t.hErrorList t.SrcPos t.ImportName
  t.ModuleIF s.Step s.ModuleType =
    t.hTables t.hErrorList t.ModuleIF;
}

//------------------------------------------------------------------------------

/**
  <OnUnknown (e.Stack) t.Config t.hTables t.hErrorList t.ImportName>
    == t.hTables t.hErrorList e.KnownModuleState
  e.KnownModuleState ::=
    Success |
    Error Error-CantLoadModule |
    Error Error-MismatchWithInternal t.InternalName |
    Error Error-Tree-ModulePacket s.Conflict t.KnownName |
    Error Error-Tree-CaseError t.ValidCaseName
*/
OnUnknown {
  (e.Stack) t.Config t.hTables t.hErrorList t.ImportName =
    <OnUnknown-CheckModuleTree
      (e.Stack) t.Config
      <MTables::ModuleTree
        t.hTables
        & MModuleTree.CheckSpace t.ImportName
      >
      t.hErrorList t.ImportName
    >;
}

OnUnknown-CheckModuleTree {
  (e.Stack) t.Config
  t.hTables AvailSpace
  t.hErrorList t.ImportName =
    <OnUnknown-UpdateTable
      t.ImportName
      <OnUnknown-CheckLookupModule
        (e.Stack) t.Config t.ImportName
        <Driver::MConfig::CheckNeedCompile
          t.hErrorList t.hTables t.Config t.ImportName
        >
      >
    >;

  (e.Stack) t.Config
  t.hTables Collision s.CollisionType t.KnownName t.InputName
  t.hErrorList t.ImportName =
    <OnUnknown-CreateErrorResult
      t.hErrorList t.hTables
      <OnUnknown-CheckModuleTree-CreateError
        t.ImportName s.CollisionType t.KnownName
      >
    >;

  // Вариант ModuleExists не рассматриваем, т.к. модуль здесь неизвестен.
}

OnUnknown-CheckModuleTree-CreateError {
  t.ImportName ModuleOverPacket t.KnownPacketName =
    (Error Error-Tree-ModulePacket ModuleOverPacket t.KnownPacketName)
    & MLookupTable.AddError-Tree-ModulePacket
    t.ImportName ModuleOverPacket t.KnownPacketName;

  t.ImportName PacketOverModule t.KnownModuleName =
    (Error Error-Tree-ModulePacket PacketOverModule t.KnownModuleName)
    & MLookupTable.AddError-Tree-ModulePacket
    t.ImportName PacketOverModule t.KnownModuleName;

  t.ImportName s.CaseCollisionError t.CaseValidName =
    (Error Error-Tree-CaseError t.CaseValidName)
    & MLookupTable.AddError-Tree-CaseError t.ImportName t.CaseValidName;
}

OnUnknown-CreateErrorResult {
  t.hErrorList t.hTables (e.ErrorResult)
  s.FnUpdateLookup e.UpdateLookupParam =
    <MTables::Lookup
      t.hTables s.FnUpdateLookup e.UpdateLookupParam
    >
    <MError::IsErrorList t.hErrorList>
    e.ErrorResult;
}

OnUnknown-UpdateTable {
  t.ImportName t.hTables t.hErrorList Success =
    t.hTables t.hErrorList Success;

  t.ImportName t.hTables t.hErrorList
  Error Error-CantLoadModule =
    <MOutModuleStatus::Fails-Q t.ImportName>
    <MTables::Lookup
      t.hTables & MLookupTable.AddError-CantLoadModule t.ImportName
    >
    t.hErrorList Error Error-CantLoadModule;

  t.ImportName t.hTables t.hErrorList
  Error Error-MismatchWithInternal t.InternalName =
    <MOutModuleStatus::Fails-Q t.ImportName>
    <MTables::Lookup
      t.hTables & MLookupTable.AddError-MismatchWithInternal
      t.ImportName t.InternalName
    >
    t.hErrorList Error Error-MismatchWithInternal t.InternalName;
}

OnUnknown-CheckLookupModule {
  (e.Stack) t.Config t.ImportName
  t.hErrorList t.hTables Success Cluster Errors =
    t.hTables t.hErrorList Error Error-CantLoadModule;

  (e.Stack) t.Config t.ImportName
  t.hErrorList t.hTables Success Cluster s.Type =
    <Driver::MUtils::OutModuleStatus-Start-ByType
      s.Type t.ImportName
    >
    <Compile
      s.Type (e.Stack) t.Config t.hTables t.hErrorList t.ImportName
    >;
}

/**
  <Compile
    s.Type (e.Stack) t.Config t.hTables t.hErrorList t.ModuleName
  >
    == t.hTables t.hErrorList e.KnownModuleState
  e.KnownModuleState ::=
    Success |
    Error Error-CantLoadModule |
    Error Error-MismatchWithInternal t.InternalName |
*/
Compile {
  s.Type (e.Stack) t.Config t.hTables t.hErrorList t.ImportName =
    <Compile-SwLoad
      s.Type (e.Stack) t.Config t.ImportName
      <Driver::MUtils::LoadModule
        s.Type t.Config t.hTables t.hErrorList t.ImportName
      >
    >;
}

Compile-SwLoad {
  s.Type (e.Stack) t.Config t.ModuleName
  t.hTables t.hErrorList Success s.ParseResult t.hRawIModule =
    <Compile-CompareWithPhysical
      <MakeReturnStatus s.ParseResult>
      s.Type (e.Stack) t.Config t.ModuleName
      t.hTables t.hErrorList
      <MIModule::GetModuleName t.hRawIModule>
    >;

  s.Type (e.Stack) t.Config t.ModuleName
  t.hTables t.hErrorList Fails =
    <MTables::Lookup
      t.hTables
      & MLookupTable.AddError-CantLoadModule t.ModuleName
    >
    t.hErrorList Error Error-CantLoadModule;
}

MakeReturnStatus {
  Success = (Success);
  Warnings = (Success);
  Errors = (Error Error-CantLoadModule);
}

Compile-CompareWithPhysical {
  t.ReturnStatus s.Type (e.Stack) t.Config t.ImportName
  t.hTables t.hErrorList
  t.hRawIModule t.InternalName =
    <Compile-CompareWithPhysical-Aux
      t.ReturnStatus s.Type (e.Stack) t.Config
      t.ImportName t.InternalName
      t.hTables t.hErrorList
      <MIModule::GetModuleAttribute t.hRawIModule Position>
      <Driver::MModuleFinder::PhysicalCompare
        t.ImportName t.InternalName
      >
    >;
}

Compile-CompareWithPhysical-Aux {
  t.ReturnStatus s.Type (e.Stack) t.Config
  t.ImportName t.InternalName
  t.hTables t.hErrorList t.hRawIModule Found t.SrcPos
  Equal =
    <Compile-CheckModuleTree
      t.ReturnStatus
      s.Type (e.Stack) t.Config t.ImportName t.InternalName
      <MTables::ModuleTree
        t.hTables & MModuleTree.CheckSpace t.InternalName
      >
      t.hErrorList t.hRawIModule t.SrcPos
    >;

  t.ReturnStatus s.Type (e.Stack) t.Config
  t.ImportName t.InternalName
  t.hTables t.hErrorList t.hRawIModule Found t.SrcPos
  NotEqual =
    <Compile-CheckImports
      (Error Error-CantLoadModule)
      s.Type (e.Stack) t.Config t.InternalName
      t.hTables
      <MError::AddMismatchInternalAndPhysicalNames
        t.hErrorList t.SrcPos t.InternalName t.ImportName
      >
      t.hRawIModule
    >;
}

Compile-CheckModuleTree {
  t.ReturnStatus s.Type (e.Stack) t.Config
  t.ImportName t.InternalName
  t.hTables AvailSpace
  t.hErrorList t.hRawIModule t.SrcPos =
    <Compile-AddModuleToTreeTable
      t.ReturnStatus s.Type (e.Stack) t.Config
      t.ImportName t.InternalName
      <MTables::ModuleTree
        t.hTables & MModuleTree.InsertModule t.InternalName
      >
      t.hErrorList t.hRawIModule
    >;

  /*
    ModuleExist не рассматриваем, т.к. это недопустимо:
    если бы он был в дереве модулей, то мы бы сюда не попали.

    Добавление имени в дерево модулей возможно только при добавлении
    модуля в таблицу поисков (иначе нарушение инварианта).
  */

  t.ReturnStatus s.Type (e.Stack) t.Config
  t.ImportName t.InternalName
  t.hTables Collision s.CollisionType t.KnownName t.InputName
  t.hErrorList t.hRawIModule t.SrcPos =
    <Compile-CheckImports
      (Error Error-CantLoadModule)
      s.Type (e.Stack) t.Config t.InternalName
      t.hTables
      <Compile-CheckModuleTree-AddCollisionError
        t.hErrorList t.SrcPos t.InternalName s.CollisionType t.KnownName
      >
      t.hRawIModule
    >;
}

Compile-CheckModuleTree-AddCollisionError {
  t.hErrorList t.SrcPos t.InternalName
  ModuleOverPacket t.KnownPacketName =
    <MError::AddTreeModulePacketConflict
      t.hErrorList t.SrcPos t.InternalName
      ModuleOverPacket t.KnownPacketName
    >;

  t.hErrorList t.SrcPos t.InternalName
  PacketOverModule t.KnownModuleName =
    <MError::AddTreeModulePacketConflict
      t.hErrorList t.SrcPos t.InternalName
      PacketOverModule t.KnownModuleName
    >;

  t.hErrorList t.SrcPos t.InternalName
  s.CaseError t.ValidCaseName =
    <MError::AddTreeCaseError
      t.hErrorList t.SrcPos t.InternalName t.ValidCaseName
    >;
}

Compile-AddModuleToTreeTable {
  t.ReturnStatus s.Type (e.Stack) t.Config
  t.ImportName t.InternalName
  t.hTables Success t.hErrorList t.hRawIModule =
    <Compile-AddModuleToLookup
      t.ReturnStatus s.Type (e.Stack) t.Config
      t.ImportName t.InternalName
      <MTables::Lookup
        t.hTables & MLookupTable.AddSuccessImport t.InternalName
      >
      t.hErrorList t.hRawIModule
    >;

  // Других вариантов быть не должно
}

Compile-AddModuleToLookup {
  t.ReturnStatus s.Type (e.Stack) t.Config
  t.Equal_ImportInternal t.Equal_ImportInternal
  t.hTables t.hErrorList t.hRawIModule =
    <Compile-CheckImports
      t.ReturnStatus s.Type (e.Stack) t.Config
      t.Equal_ImportInternal
      t.hTables t.hErrorList t.hRawIModule
    >;

  t.ReturnStatus s.Type (e.Stack) t.Config
  t.ImportName t.InternalName
  t.hTables t.hErrorList t.hRawIModule =
    <Compile-CheckImports
      (Error Error-MismatchWithInternal t.InternalName)
      s.Type (e.Stack) t.Config t.InternalName
      t.hTables t.hErrorList t.hRawIModule
    >;
}

/**
  <Compile-CheckImports
    t.ReturnStatus s.Type (e.Stack) t.Config t.InternalName
    t.hTables t.hErrorList t.hRawIModule
  >
    == t.hTables t.hErrorList e.KnownModuleStatus
  t.ReturnStatus ::= (e.KnownModuleStatus)
  e.KnownModuleStatus ::=
    Success |
    Error Error-CantLoadModule |
    Error Error-MismatchWithInternal t.InternalName
*/
Compile-CheckImports {
  t.ReturnStatus s.Type (e.Stack) t.Config t.InternalName
  t.hTables t.hErrorList t.hRawIModule =
    <Compile-CheckImports-LoadInterfaces
      t.ReturnStatus s.Type (e.Stack) t.Config t.InternalName
      t.hTables
      <Driver::MUtils::GetImportList
        t.hErrorList t.hRawIModule
      >
    >;
}

Compile-CheckImports-LoadInterfaces {
  t.ReturnStatus s.Type (e.Stack) t.Config t.InternalName
  t.hTables
  s.ListResult t.hErrorList t.hRawIModule e.ImportNames =
    <Synthesis
      <UpdateReturnStatus t.ReturnStatus s.ListResult>
      s.Type t.Config
      <Driver::MModuleIF::FromIModule-T t.hRawIModule>
      <MLambda::MapReduce
        <MLambda::BindLeft
          & LoadInterface
          s.Type (e.Stack t.InternalName) t.Config
        >
        (t.hTables t.hErrorList) e.ImportNames
      >
    >;
}

UpdateReturnStatus {
  t.ReturnStatus Success = t.ReturnStatus;
  t.ReturnStatus Fails = (Error Error-CantLoadModule);
  t.ReturnStatus Errors = (Error Error-CantLoadModule);
}

LoadInterface {
  s.Type (e.Stack) t.Config (t.hTables t.hErrorList)
  (t.SrcPos t.AliasName t.RealName) =
    <LoadInterface-Aux
      t.AliasName
      <ExternalName
        s.Type (e.Stack)
        t.Config t.hTables t.hErrorList t.SrcPos t.RealName
      >
    >;
}

LoadInterface-Aux {
  t.AliasName t.hTables t.hErrorList t.Interface_ =
    (t.hTables t.hErrorList) (t.AliasName t.Interface_);
}

Synthesis {
  t.ReturnStatus Recompile t.Config
  t.hRawIModule t.ModuleIF
  (t.hTables t.hErrorList) e.ImportInterfaces =
    <Synthesis-Recompile-Resolve
      t.ReturnStatus t.Config
      t.hTables t.hErrorList t.hRawIModule t.ModuleIF
      e.ImportInterfaces
    >;

  t.ReturnStatus s.UpdateLibrary t.Config
  t.hSymIModule t.ModuleIF
  (t.hTables t.hErrorList) e.ImportInterfaces =
    <MIModule::Destroy t.hSymIModule>
    <Synthesis-Update
      s.UpdateLibrary
      <UpdateReturnStatus-ImportInterfaces
        t.ReturnStatus e.ImportInterfaces
      >
      t.Config
      t.hTables t.hErrorList t.ModuleIF
    >;
}

Synthesis-Recompile-Resolve {
  t.ReturnStatus t.Config
  t.hTables t.hErrorList t.hRawIModule t.ModuleIF
  e.ImportInterfaces =
    <Synthesis-Recompile-Resolve-TablePrepared
      <UpdateReturnStatus-ImportInterfaces
        t.ReturnStatus e.ImportInterfaces
      >
      t.Config t.hTables t.hErrorList t.hRawIModule t.ModuleIF
      <MLambda::MapReduce
        <MLambda::Composite
          & MLambda.UnBracket-H
          & Driver.MResolver.AddModuleInfo-IF
        >
        <Driver.MResolver.CreateTable>
        e.ImportInterfaces
      >
    >;
}

UpdateReturnStatus-ImportInterfaces {
  t.ReturnStatus e.If-B (t.Alias CantLoadInterface) e.If-E =
    (Error Error-CantLoadModule);

  t.ReturnStatus e.Interfaces = t.ReturnStatus;
}

Synthesis-Recompile-Resolve-TablePrepared {
  t.ReturnStatus t.Config t.hTables t.hErrorList
  t.hRawIModule t.ModuleIF t.hResolveTable =
    <Synthesis-Recompile-Resolve-TablePrepared-Aux
      t.ReturnStatus t.Config
      t.hTables t.hErrorList
      t.ModuleIF
      <Driver::MResolver::AddModuleInfo
        t.hResolveTable NoPrefix t.hRawIModule
      >
    >;
}

Synthesis-Recompile-Resolve-TablePrepared-Aux {
  t.ReturnStatus t.Config t.hTables t.hErrorList t.ModuleIF
  t.hResolveTable t.hRawIModule =
    <Synthesis-Recompile-Resolve-Resolved
      t.ReturnStatus t.Config
      t.hTables t.ModuleIF
      <Driver::MResolver::Resolve
        t.hRawIModule t.hErrorList t.hResolveTable
      >
    >;
}

Synthesis-Recompile-Resolve-Resolved {
  t.ReturnStatus t.Config t.hTables t.ModuleIF
  t.hIModule s.ResolveResult t.hErrorList =
    <Synthesis-Recompile-Generate
      <UpdateReturnStatus t.ReturnStatus s.ResolveResult>
      t.Config t.hTables t.hErrorList t.hIModule t.ModuleIF
    >;
}

Synthesis-Recompile-Generate {
  (Success) t.Config
  t.hTables t.hErrorList t.hIModule t.ModuleIF =
    <Synthesis-Recompile-SwGenerate
      t.hErrorList t.ModuleIF
      <Driver::MConfig::Generate
        t.Config t.hTables t.hIModule
        <Driver::MModuleIF::GetName t.ModuleIF>
      >
    >;

  (Error e.Error) t.Config
  t.hTables t.hErrorList t.hIModule t.ModuleIF =
    <MIModule::Destroy t.hIModule>
    t.hTables t.hErrorList Error e.Error;
}

Synthesis-Recompile-SwGenerate {
  t.hErrorList t.ModuleIF t.hTables t.hIModule Success =
    <MOutModuleStatus::Compiled-Q
      <Driver::MModuleIF::GetName t.ModuleIF>
    >
    <MIModule::Destroy t.hIModule>
    <GuardSuccess
      <MTables::Modules
        <Driver::MModuleIF::SaveCache t.hTables t.ModuleIF>
        & MModuleTable.FinishModule
        <Driver::MModuleIF::GetName t.ModuleIF>
        <Driver::MModuleIF::GetModuleType t.ModuleIF>
        Recompile
      >
    >
    t.hErrorList Success;

  t.hErrorList t.ModuleIF t.hTables t.hIModule Fails =
    <MIModule::Destroy t.hIModule>
    t.hTables
    <MError::AddFailGeneration
      t.hErrorList
      <Driver::MModuleIF::GetSrcPos t.ModuleIF>
      <Driver::MModuleIF::GetName t.ModuleIF>
    >
    Error Error-CantLoadModule;
}

GuardSuccess {
  t.hTables Success = t.hTables;
}

Synthesis-Update {
  s.Type
  (Success) t.Config t.hTables t.hErrorList t.ModuleIF =
    <Driver::MUtils::OutModuleStatus-Finish-ByType
      s.Type <Driver::MModuleIF::GetName t.ModuleIF>
    >
    <GuardSuccess
      <MTables::Modules
        <Driver::MModuleIF::SaveCache t.hTables t.ModuleIF>
        & MModuleTable.FinishModule
        <Driver::MModuleIF::GetName t.ModuleIF>
        <Driver::MModuleIF::GetModuleType t.ModuleIF>
        s.Type
      >
    >
    t.hErrorList Success;

  s.Type
  (Error e.Error) t.Config t.hTables t.hErrorList t.ModuleIF =
    t.hTables t.hErrorList Error e.Error;
}

$END Driver::MCompiler.

