$MODULE Driver::MCompiler;

$IMPORT Driver::MResolveTable;
$IMPORT Driver::MResolver;
$IMPORT MConfig = Driver::MConfig;
$IMPORT MContext = Driver::MContext;
$IMPORT MError;
$IMPORT MIModule;
$IMPORT MLambda;
$IMPORT MOutModuleStatus;
$IMPORT MSelfDiag;
$IMPORT MSymModule = Driver::MSymModule;
$IMPORT MQualifiedName;

/**
  <Make t.hContext t.hErrorList t.Config t.SrcPos t.RootModuleName>
    == t.hContext t.hErrorList Success
    == t.hContext t.hErrorList Fails
*/
$ENTRY Make
  t.hContext t.hErrorList t.Config t.SrcPos t.RootModuleName =
    <Make-SwResult
      t.Config t.RootModuleName
      <ScanSubModule
        Recompile
        () t.Config (t.hContext t.hErrorList)
        t.SrcPos t.RootModuleName
      >
    >;

Make-SwResult {
  t.Config t.RootModuleName
  (t.hContext t.hErrorList) Success t.hIModule Main =
    <MIModule::Destroy t.hIModule>
    <Make-Aux
      <MConfig::Link t.Config t.hContext t.RootModuleName>
      t.hErrorList
    >;

  t.Config t.RootModuleName
  (t.hContext t.hErrorList) Success t.hIModule Regular =
    <MIModule::Destroy t.hIModule>
    <MOutModuleStatus::Flush>
    t.hContext t.hErrorList Success;

  t.Config t.RootModuleName
  (t.hContext t.hErrorList) Fails t.ModuleName =
    t.hContext t.hErrorList Fails;
}

Make-Aux
  t.hContext s.Result t.hErrorList =
    t.hContext t.hErrorList s.Result;

//------------------------------------------------------------------------------

/**
  <ScanSubModule
    s.Type
    (e.Stack) t.Config (t.hContext t.hErrorList) t.SrcPos t.ImportName
  >
    == (t.hContext t.hErrorList) Success t.hIModule s.ModuleType
    == (t.hContext t.hErrorList) Fails t.RealName
    == (t.hContext t.hErrorList) IsNotLibrary t.RealName
  s.Type ::= Recompile | Updated | Library
*/
ScanSubModule {
  s.Type
  (e.Stack) t.Config (t.hContext t.hErrorList) t.SrcPos t.ImportName =
    <ScanSubModule-SwNeedRecompile
      s.Type (e.Stack) t.Config
      <MConfig::CheckNeedCompile
        t.hErrorList t.hContext t.Config t.ImportName
      >
    >;
}

ScanSubModule-SwNeedRecompile {
  s.TypeMode (e.Stack) t.Config t.hErrorList
  t.hContext t.ModuleName Success s.Status s.Type =
    <ScanSubModule-SwSelect
      <ScanSubModule-Select s.TypeMode s.Status s.Type>
      (e.Stack) t.Config t.hErrorList t.hContext
      t.ModuleName
    >;

  s.Type
  (e.Stack) t.Config t.hErrorList
  t.hContext t.ModuleName Fails MismatchCase =
    (t.hContext t.hErrorList) Fails t.ModuleName;
}

ScanSubModule-Select {
  s.TypeMode s.Status Errors    = Error           Fails        ;

  Updated    s.Status Recompile = Error           Fails        ;
  Library    s.Status Recompile = Error           IsNotLibrary ;

  Library    s.Status Updated   = Error           IsNotLibrary ;

  s.TypeMode Cluster  s.Type    = Compile         s.Type       ;
  s.TypeMode Ready    s.Type    = SuccessCompiled              ;
}

ScanSubModule-SwSelect {
  Error s.ErrorType
  (e.Stack) t.Config t.hErrorList t.hContext t.ModuleName =
    (t.hContext t.hErrorList) s.ErrorType t.ModuleName;

  SuccessCompiled
  (e.Stack) t.Config t.hErrorList t.hContext t.ModuleName =
    <Compile-OnSuccessCompiled
      t.ModuleName
      <MSymModule::LoadInfo t.hContext t.hErrorList t.ModuleName>
    >;

  Compile s.Type
  (e.Stack) t.Config t.hErrorList t.hContext t.ModuleName =
    <Compile
      s.Type
      (e.Stack) t.Config t.hErrorList t.hContext t.ModuleName
    >;
}

Compile-OnSuccessCompiled {
  t.ModuleName t.hContext t.hErrorList Success s.Errors t.hSymIModule =
    (t.hContext t.hErrorList)
    <MakeSuccessResult
      <MIModule::GetModuleAttribute t.hSymIModule ModuleType>
    >;

  t.ModuleName t.hContext t.hErrorList Fails =
    /*
      Compile-OnSuccessCompiled вызывается только на стадии Ready,
      это значит, что стадия грозди была выполнена успешно,
      а на ней считывается информация о межмодульных связях.
      Поэтому ошибочное прочтение тут совершенно неожиданно.
    */
    <MSelfDiag::Error
      'Compile-OnSuccessCompiled-Aux:\n'
      '  Unexpected fail on reading module interface information (MII).\n'
      '  Glitch appears while loading MII in module '
      <MQualifiedName::ToPrintable-Dot t.ModuleName> '.'
    >;
}

MakeSuccessResult
  t.hIModule Found s.ModuleType =
    Success t.hIModule s.ModuleType;

//------------------------------------------------------------------------------

/**
  <Compile
    s.Type
    (e.Stack) t.Config t.hErrorList t.hContext t.ModuleName
  >
    == (t.hContext t.hErrorList) Success t.hIModule s.ModuleType
    == (t.hContext t.hErrorList) Fails t.ModuleName

  s.Type ::= Updated | Library
*/
Compile {
  s.Type
  (e.Stack) t.Config t.hErrorList t.hContext t.ModuleName =
    <OutModuleStatus-Start-ByType s.Type t.ModuleName>
    <UpdateContext
      s.Type
      <Compile-Aux
        s.Type
        (e.Stack) t.Config t.ModuleName
        <MLambda::Apply
          <LoadInfo-ByType s.Type>
          t.Config t.hContext t.hErrorList t.ModuleName
        >
      >
    >;
}

LoadInfo-ByType {
  Recompile = & Parse;
  s.Other   = & LoadInfo;
}

OutModuleStatus-Start-ByType {
  Recompile t.ModuleName = <MOutModuleStatus::Compiling-Q t.ModuleName>;
  s.Other   t.ModuleName = /* ничего */;
}

/**
  <Compile-Aux
    s.Type
    (e.Stack) t.Config t.ModuleName
    t.hContext t.hErrorList Success s.Errors t.hSymIModule / Fails
  >
    == (t.hContext t.hErrorList) Success t.hIModule s.ModuleType
    == (t.hContext t.hErrorList) Fails t.ModuleName

  s.Type ::= Recompile | Updated | Library
*/
Compile-Aux {
  s.Type
  (e.Stack) t.Config t.ModuleName
  t.hContext t.hErrorList Success s.Errors t.hSymIModule =
    <Compile-AnalyzeImports
      s.Type s.Errors t.Config
      <GetImportList
        (e.Stack) t.ModuleName
        t.hContext t.hErrorList t.hSymIModule
      >
    >;

  s.Type
  (e.Stack) t.Config t.ModuleName
  t.hContext t.hErrorList Fails =
    (t.hContext t.hErrorList) Fails t.ModuleName;
}

SelectResult {
  s.Result Errors = Errors;
  s.Result Success = s.Result;
}

Compile-AnalyzeImports {
  s.Type s.Result t.Config
  s.RecResult (e.Stack) t.ModuleName
  t.hContext t.hErrorList t.hSymIModule e.Imports =
    <Compile-AnalyzeImports-Aux
      s.Type
      (e.Stack) t.Config t.ModuleName
      t.hContext <SelectResult s.Result s.RecResult>
      t.hErrorList t.hSymIModule e.Imports
    >;
}

Compile-AnalyzeImports-Aux {
  s.Type
  (e.Stack) t.Config t.ModuleName
  t.hContext s.Result
  t.hErrorList t.hSymIModule e.Imports =
    <Compile-ScanSubModules
      s.Type t.Config t.ModuleName t.hSymIModule s.Result
      <MLambda::MapReduce
        <MLambda::BindLeft
          & CallScanSubModule
          s.Type (e.Stack t.ModuleName) t.Config
        >
        (t.hContext t.hErrorList) e.Imports
      >
    >;
}

CallScanSubModule {
  s.Type (e.Stack) t.Config
  (t.hContext t.hErrorList) (t.SrcPos t.AliasName t.RealName) =
    <Scan-SubModule-Aux
      s.Type t.AliasName t.SrcPos
      <ScanSubModule
        s.Type (e.Stack) t.Config
        (t.hContext t.hErrorList) t.SrcPos t.RealName
      >
    >;
}

Scan-SubModule-Aux {
  s.Type t.AliasName t.SrcPos
  (t.hContext t.hErrorList) Success t.hIModule s.ModuleType =
    (t.hContext t.hErrorList)
    (Success t.hIModule s.ModuleType t.SrcPos t.AliasName);

  s.Type t.AliasName t.SrcPos
  (t.hContext t.hErrorList) Fails t.RealName =
    (t.hContext t.hErrorList)
    (Fails t.RealName t.SrcPos t.AliasName);

  Library t.AliasName t.SrcPos
  (t.hContext t.hErrorList) IsNotLibrary t.RealName =
    (t.hContext t.hErrorList)
    (IsNotLibrary t.RealName t.SrcPos t.AliasName);
}

Compile-ScanSubModules {
  s.Type t.Config t.ModuleName t.hRawIModule s.Result
  (t.hContext t.hErrorList) e.ScannedImports =
    <Compile-Resolve
      s.Type t.Config t.ModuleName t.hContext
      <MLambda::MapReduce
        & AnalyzeValidSubModule
        (t.hErrorList t.hRawIModule s.Result)
        e.ScannedImports
      >
    >;
}

/**
  <Compile-Resolve
    s.Type t.Config t.ModuleName t.hContext s.Result
    (t.hErrorList t.hRawModule)
    (t.SubModuleAlias t.hISubModule)*
  >
*/
Compile-Resolve {
  Recompile t.Config t.ModuleName t.hContext
  (t.hErrorList t.hRawIModule s.Result) e.ImportInfo =
    <Compile-Recompile-Generate
      t.Config t.ModuleName t.hContext
      <Resolve
        t.hRawIModule t.hErrorList s.Result e.ImportInfo
      >
    >;

  s.Type t.Config t.ModuleName t.hContext
  (t.hErrorList t.hRawIModule s.Result) e.ImportInfo =
    <Compile-SwScanSubModules
      t.hContext t.ModuleName
      (t.hErrorList t.hSymIModule s.Result)
      <MLambda::Map & OnlyIModule e.ImportInfo>
    >;
}

OnlyIModule {
  (t.Alias t.hSymIModule) = t.hSymIModule;
}

Compile-Recompile-Generate {
  t.Config t.ModuleName t.hContext
  t.hIModule t.hErrorList Errors e.Imports =
    <MLambda::Map & MIModule.Destroy t.hIModule e.Imports>
    (t.hContext t.hErrorList) Fails t.ModuleName;

  t.Config t.ModuleName t.hContext
  t.hIModule t.hErrorList s.SuccessOrWarnings e.Imports =
    <Compile-Recompile-SwGenerate
      t.hErrorList t.ModuleName
      <MConfig::Generate t.Config t.hContext t.hIModule t.ModuleName>
      e.Imports
    >;
}

Compile-Recompile-SwGenerate {
  t.hErrorList t.ModuleName
  t.hContext t.hIModule Success e.Imports =
    <Compile-Cache
      t.hContext t.hErrorList
      <MIModule::GetModuleName
        <MSymModule::SymReduce t.hIModule>
      >
      e.Imports
    >;

  t.hErrorList t.ModuleName
  t.hContext t.hIModule Fails e.Imports =
    <MIModule::Destroy t.hIModule e.Imports>
    (t.hContext t.hErrorList) Fails t.ModuleName;
}

Compile-SwScanSubModules {
  t.hContext t.ModuleName
  (t.hErrorList t.hSymIModule Errors)
  e.InvalidScannedImports =
    (t.hContext t.hErrorList) Fails t.ModuleName
    <MIModule::Destroy t.hSymIModule>
    <MLambda::Map & MIModule.Destroy e.InvalidScannedImports>;

  t.hContext t.ModuleName
  (t.hErrorList t.hSymIModule s.SuccessOrWarnings)
  e.ValidScannedImports =
    <Compile-Cache
      t.hContext t.hErrorList
      <MIModule::GetModuleName t.hSymIModule>
      e.ValidScannedImports
    >;
}

/**
  <Compile-Cache
    t.hContext t.hErrorList t.hIModule t.ModuleName e.ValidScannedImports
  >
*/
Compile-Cache {
  t.hContext t.hErrorList t.hIModule t.ModuleName e.ValidScannedImports =
    <Compile-ScanSubModules-Cache-Aux
      t.hErrorList
      <MIModule::GetModuleAttribute t.hIModule ModuleType>
      <MContext::SetCachedImports_
        t.hContext t.ModuleName
        <MLambda::Map
          <MLambda::Composite
            & MIModule.GetModuleName
            & DestroySymModule
          >
          e.ValidScannedImports
        >
      >
    >;
}

DestroySymModule {
  t.hSymIModule t.ModuleName =
    <MIModule::Destroy t.hSymIModule>
    t.ModuleName;
}

Compile-ScanSubModules-Cache-Aux {
  t.hErrorList
  t.hSymIModule Found s.ModuleType
  t.hContext Success =
    (t.hContext t.hErrorList) Success t.hSymIModule s.ModuleType;
}

//------------------------------------------------------------------------------

/**
  <UpdateContext s.Type e.CompileResult>
    == e.CompileResult
  e.CompileResult ::=
    (t.hContext t.hErrorList) e.SubResult
  e.SubResult ::= Success t.hIModule s.ModuleType | Fails t.ModuleName
*/

UpdateContext {
  s.Type
  (t.hContext t.hErrorList) Success t.hIModule s.ModuleType =
    <UpdateContext-Aux
      s.Type (t.hContext t.hErrorList) s.ModuleType
      <MIModule::GetModuleName t.hIModule>
    >;

  s.Type
  (t.hContext t.hErrorList) Fails t.ModuleName =
    <MOutModuleStatus::Fails-Q t.ModuleName>
    (
      <GuardUpdate
        <MContext::FinishModule_
          t.hContext t.ModuleName Regular Errors
        >
      >
      t.hErrorList
    )
    Fails t.ModuleName;
}

GuardUpdate
  t.hContext Success = t.hContext;

UpdateContext-Aux {
  s.Type (t.hContext t.hErrorList) s.ModuleType
  t.hIModule t.ModuleName =
    <OutModuleStatus-Finish-ByType s.Type t.ModuleName>
    <UpdateContext-AfterUpdate
      t.hErrorList t.hIModule s.ModuleType
      <MContext::FinishModule_
        t.hContext t.ModuleName s.ModuleType s.Type
      >
    >;
}

UpdateContext-AfterUpdate
  t.hErrorList t.hIModule s.ModuleType t.hContext Success =
    (t.hContext t.hErrorList) Success t.hIModule s.ModuleType;

OutModuleStatus-Finish-ByType {
  Recompile t.ModuleName = <MOutModuleStatus::Compiled-Q t.ModuleName>;
  Updated   t.ModuleName = <MOutModuleStatus::Updated-Q  t.ModuleName>;
  Library   t.ModuleName = <MOutModuleStatus::Library-Q  t.ModuleName>;
}

//------------------------------------------------------------------------------

/**
  <LoadInfo t.Config t.hContext t.hErrorList t.ModuleName>
    == t.hContext t.hErrorList Success s.Result t.hSymIModule
    == t.hContext t.hErrorList Fails

  s.Result ::= Success | Warnings | Errors
*/
LoadInfo {
  t.Config t.hContext t.hErrorList t.ModuleName =
    <MSymModule::LoadInfo t.hContext t.hErrorList t.ModuleName>;
}

//------------------------------------------------------------------------------

/**
  <Parse t.Config t.hContext t.hErrorList t.ModuleName>
    == t.hContext t.hErrorList Success s.Result t.hRawIModule
    == t.hContext t.hErrorList Fails

  s.Result ::= Success | Warnings | Errors
*/
Parse
  t.Config t.hContext t.hErrorList t.ModuleName =
    <Parse-SelectFE
      t.Config t.hErrorList t.ModuleName
      <MContext::GetFrontEnd t.hContext t.ModuleName>
    >;

Parse-SelectFE {
  t.Config t.hErrorList t.ModuleName t.hContext Success None =
    // Неожиданно. Вообще-то тут не может быть библиотека.
    t.hContext t.hErrorList Fails;

  t.Config t.hErrorList t.ModuleName t.hContext Success s.FEName e.Path =
    t.hContext
    <MConfig::Load t.Config s.FEName t.hErrorList t.ModuleName e.Path>;

  t.Config t.hErrorList t.ModuleName t.hContext Fails s.Reason =
    t.hContext t.hErrorList Fails;
}

//------------------------------------------------------------------------------

/**
  <Resolve
    t.hIModule t.hErrorList s.Result e.ImportInfo
  >
    == t.hIModule t.hErrorList s.Result t.hImportIModule*
  e.ImportInfo ::= (t.RelativeAlias t.hImportIModule)*
  s.Result ::= Success | Errors
*/
Resolve {
  t.hIModule t.hErrorList s.Result e.ImportInfo =
    <Resolve-Aux
      t.hErrorList s.Result
      <MLambda::MapReduce
        <MLambda::Composite
          & MLambda.UnBracket-H
          & Driver.MResolver.AddModuleInfo
        >
        <Driver::MResolver::Create>
        (NoPrefix t.hIModule) e.ImportInfo
      >
    >;
}

Resolve-Aux {
  t.hErrorList s.Result t.hResolveTable t.hIModule e.ImportModules =
    <Resolve-SwResolve
      s.Result
      <Driver::MResolver::Resolve
        t.hIModule t.hErrorList t.hResolveTable
      >
      e.ImportModules
    >;
}

Resolve-SwResolve {
  s.PrevResult t.hIModule s.Result t.hErrorList e.ImportedModules =
    t.hIModule t.hErrorList
    <MultiplyResult s.PrevResult s.Result>
    e.ImportedModules;
}

//------------------------------------------------------------------------------

/**
  <GetImportList
    (e.Stack) t.ModuleName t.hContext t.hErrorList t.hIModule
  >
    == s.Result (e.Stack) t.ModuleName
      t.hContext t.hErrorList t.hIModule e.Import
  s.Result ::= Success | Fails
  e.Import ::= (t.SrcPos t.AliasName t.RealName)*
*/
GetImportList {
  (e.Stack) t.ModuleName t.hContext t.hErrorList t.hIModule =
    <GetImportList-CheckValidNames
      <GetImportList-AnalyzeRecursiveLinks
        (e.Stack) t.ModuleName t.hContext t.hErrorList
        <MLambda::MapReduce
          & PrepareImportList-Aliases
          <MIModule::GetImportList-Aliases t.hIModule>
        >
      >
    >;
}

/*
  <PrepareImportList-Aliases t.hIModule t.NextImportAlias>
    == t.hIModule (t.NextImport t.RealName)
*/
PrepareImportList-Aliases
  t.hIModule t.NextImport =
    <PrepareImportList-RealName
      t.NextImport
      <MIModule::GetImportAttribute-AliasName
        t.hIModule RealName t.NextImport
      >
    >;

PrepareImportList-RealName {
  t.NextImport t.hIModule Found t.RealName =
    <PrepareImportList-SrcPos
      t.NextImport t.RealName
      <MIModule::GetImportAttribute-AliasName
        t.hIModule Position t.NextImport
      >
    >;
}

PrepareImportList-SrcPos {
  t.NextImport t.RealName t.hIModule Found t.SrcPos =
    t.hIModule (t.SrcPos t.NextImport t.RealName);
}

/*
  <GetImportList-AnalyzeRecursiveLinks
    (e.Stack) t.ModuleName
    t.hContext t.hErrorList t.hIModule e.Imports
  >
    == Success (e.Stack) t.ModuleName
    t.hContext t.hErrorList t.hIModule e.NonRecursiveImports
    == Fails (e.Stack) t.ModuleName
    t.hContext t.hErrorList t.hIModule e.Imports
  e.Imports ::= (t.AliasName t.RealName)
*/
GetImportList-AnalyzeRecursiveLinks {
  (e.Stack-B t.RecursiveLink e.Stack-E) t.ModuleName
  t.hContext t.hErrorList t.hIModule
  e.Imports-B (t.SrcPos t.Alias t.RecursiveLink) e.Imports-E =
    <GetImportList-TypeRecursiveImports
      t.hContext t.hErrorList t.ModuleName
      (
        <Recursives-SetDifference
          (e.Imports-B e.Imports-E)
          (e.Stack-B e.Stack-E)
        >
      )
      (e.Stack-B t.RecursiveLink e.Stack-E)
      <MLambda::MapReduce
        & RelativeImportName
        t.hIModule
        <Recursives-SetIntersect
          (e.Imports-B (t.SrcPos t.Alias t.RecursiveLink) e.Imports-E)
          (e.Stack-B t.RecursiveLink e.Stack-E)
        >
      >
    >;

  (e.Stack) t.ModuleName
  t.hContext t.hErrorList t.hIModule e.Imports =
    Success (e.Stack) t.ModuleName
    t.hContext t.hErrorList t.hIModule e.Imports;
}

Recursives-SetDifference {
  (e.Imports-B (t.SrcPos t.Alias t.Recursive) e.Imports-E)
  (e.Stack-B t.Recursive e.Stack-E) =
    <Recursives-SetDifference
      (e.Imports-B e.Imports-E) (e.Stack-B e.Stack-E)
    >;

  (e.Imports) (e.Stack) = e.Imports;
}

Recursives-SetIntersect {
  (e.Imports-B (t.SrcPos t.Alias t.Recursive) e.Imports-E)
  (e.Stack-B t.Recursive e.Stack-E) =
    (t.SrcPos t.Alias)
    <Recursives-SetIntersect
      (e.Imports-B e.Imports-E) (e.Stack-B e.Stack-E)
    >;

  (e.Imports) (e.Stack) = ;
}

RelativeImportName
  t.hIModule (t.SrcPos t.ImportNameAlias) =
    <RelativeImportName-Aux
      t.SrcPos
      <MIModule::RelativeName
        t.hIModule t.ImportNameAlias
      >
    >;

RelativeImportName-Aux
  t.SrcPos t.hIModule Found t.ImportName =
    t.hIModule (t.SrcPos t.ImportName);

GetImportList-TypeRecursiveImports
  t.hContext t.hErrorList t.ModuleName
  (e.Imports) (e.Stack)
  t.hIModule e.RecursiveLinks =
    Errors (e.Stack) t.ModuleName
    t.hContext
    <MLambda::MapReduce
      <MLambda::Composite
        & MLambda.UnBracket-H
        & MError.AddRecursiveLink
      >
      t.hErrorList e.RecursiveLinks
    >
    t.hIModule e.Imports;

GetImportList-CheckValidNames {
  s.Result (e.Stack) t.ModuleName
  t.hContext t.hErrorList t.hIModule e.ImportList =
    <GetImportList-CheckValidNames-UpdateResult
      s.Result (e.Stack) t.ModuleName t.hContext
      <DoCheckValidNames
        Success t.hErrorList t.hIModule () e.ImportList
      >
    >;
}

GetImportList-CheckValidNames-UpdateResult {
  s.Result (e.Stack) t.ModuleName t.hContext
  s.SubResult t.hErrorList t.hIModule e.ImportList =
    <MultiplyResult s.Result s.SubResult>
    (e.Stack) t.ModuleName
    t.hContext t.hErrorList t.hIModule e.ImportList;
}

MultiplyResult {
  Errors s.Result2 = Errors;
  s.Result1 Errors = Errors;
  Warnings s.Result2 = Warnings;
  s.Result1 Warnings = Warnings;
  Success Success = Success;
}

DoCheckValidNames {
  s.Result t.hErrorList t.hIModule (e.Scanned)
  (t.SrcPos t.Alias t.RealName) e.Tail =
    <DoCheckValidNames-SwValid
      s.Result t.hErrorList t.hIModule (e.Scanned)
      t.SrcPos t.Alias <CheckName t.RealName>
      e.Tail
    >;

  s.Result t.hErrorList t.hIModule (e.Scanned) =
    s.Result t.hErrorList t.hIModule e.Scanned;
}

CheckName {
  t.RealName =
    <CheckName-Parsed
      <MQualifiedName::Parse-T t.RealName>
    >;
}

CheckName-Parsed {
  t.RealName e.Segments-B (e.Name-B '?' e.Name-E) e.Segments-E =
    Invalid t.RealName;

  t.RealName e.Segments =
    Valid t.RealName;
}

DoCheckValidNames-SwValid {
  s.Result t.hErrorList t.hIModule (e.Scanned)
  t.SrcPos t.Alias Valid t.RealName e.Tail =
    <DoCheckValidNames
      s.Result t.hErrorList t.hIModule
      (e.Scanned (t.SrcPos t.Alias t.RealName)) e.Tail
    >;

  s.Result t.hErrorList t.hIModule (e.Scanned)
  t.SrcPos t.Alias Invalid t.RealName e.Tail =
    <DoCheckValidNames
      Fails
      <MError::AddInvalidImportName
        t.hErrorList t.SrcPos t.RealName
      >
      t.hIModule
      (e.Scanned) e.Tail
    >;
}

//------------------------------------------------------------------------------

/**
  <AnalyzeValidSubModule (t.hErrorList t.hIModule s.Result) t.ImportRes>
    == (t.hErrorList t.hIModule s.Result) (t.hISubModule t.SubModuleAlias)
    == (t.hErrorList t.hIModule Errors)
  t.ImportRes ::=
    (Success t.hISubModule s.ModuleType t.SrcPos t.Alias)
    | (Fails t.RealName t.SrcPos t.Alias)
    | (IsNotLibrary t.RealName t.SrcPos t.Alias)
*/
AnalyzeValidSubModule {
  (t.hErrorList t.hRawIModule s.Result)
  (Fails t.ImportName t.SrcPos t.Alias) =
    (
      <MError::AddBrokenImport t.hErrorList t.SrcPos t.ImportName>
      t.hRawIModule
      Errors
    );

  /*
    Возникает только при анализе зависимостей библиотеки.
  */
  (t.hErrorList t.hRawIModule s.Result)
  (IsNotLibrary t.ImportName t.SrcPos t.Alias) =
    (
      <MError::DependedFromSource t.hErrorList t.SrcPos t.ImportName>
      t.hRawIModule
      Errors
    );

  (t.hErrorList t.hRawIModule s.Result)
  (Success t.hISubModule Main t.SrcPos t.Alias) =
    <MIModule::Destroy t.hISubModule>
    (
      <MError::AddImportHead t.hErrorList t.SrcPos t.Alias>
      t.hRawIModule
      Errors
    );

  (t.hErrorList t.hRawIModule s.Result)
  (Success t.hISubModule Regular t.SrcPos t.Alias) =
    <RelativeModuleName
      t.hErrorList s.Result t.hISubModule
      <MIModule::RelativeName t.hRawIModule t.Alias>
    >;
}

RelativeModuleName
  t.hErrorList s.Result t.hImportIModule
  t.hRawIModule Success t.RelativeImportAlias =
    (t.hErrorList t.hRawIModule s.Result)
    (t.RelativeImportAlias t.hImportIModule);

$END Driver::MCompiler.
