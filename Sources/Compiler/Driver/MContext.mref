$MODULE Driver::MContext;

$IMPORT MCaseTable = Driver::MCaseTable;
$IMPORT MQualifiedName;
$IMPORT MStrings;
$IMPORT MSymTable;

/*------------------------------------------------------------------------------
  Внутренняя структура:
    t.Context ::= [Context t.SymTable t.CaseTable]
------------------------------------------------------------------------------*/
$DATA Context;

//------------------------------------------------------------------------------

/**
  <Create> == t.Context
*/
$ENTRY Create = [Context <MSymTable::Create> <MCaseTable::Create>];

//------------------------------------------------------------------------------

/**
  <Destroy t.Context> == empty
*/
$ENTRY Destroy
  [Context t.SymTable t.CaseTable] =
    <MSymTable::Destroy t.SymTable>
    <MCaseTable::Destroy t.CaseTable>;

/**---------------------------------------------------------------------------*/
//STARTCUT
DefaultOptions
  =
    // Вид сущности
    (Kind Module)

    // Информация о входных и выходных частях
    (FrontEnd Unknowns) // Unknowns | None | s.FrontEnd e.FileName
    (BackEnds Unknowns) // e.BackEnds ::= Unknowns | (s.BackEnd e.FileName)*

    // Информация о настоящем имени модуля (контекст индексируется без учёта
    // регистра, но регистр надо проверять)
    (RealName Unknown) // Unknown | t.QualifiedName

    // Информация о имени целевого файла
    (Target Default) // Default | e.PtTargetName

    // Кэширование списка зависимостей
    (CachedImports None) // None | t.ModuleName*

    // Информация о статусе компилирования

    (Type Errors) // Errors | Changed | Updated | Library
    (Status Found) // Found | Raw | Ready

    /*
      Время последней модификации. Нужно для того, чтобы определить,
      необходима ли перекомпиляция: импортирующие модули должны быть
      откомпилированы позже импортируемых.
      Unknown -- информация не доступна
      (e.MinTime) (e.MaxTime) -- см. MainStructures.txt
    */
    (MinMaxTime Unknown)

    (ModuleType Main) // Main | Regular
    (CachedSym None) // None | t.IModule
    ;
//ENDCUT
/**---------------------------------------------------------------------------*/

/*
  Внутренние функции реализации контекста
*/

//------------------------------------------------------------------------------

/*
  <Explode t.QualifiedName> == e.ExplodedName
*/
Explode
  t.QualifiedName =
    <MStrings::Lower <MQualifiedName::Parse t.QualifiedName>>;

/**
  e.ContextErrorMessage ::=
    MismatchModuleCase t.KnownModuleName t.InputModuleName
    MismatchPacketCase t.KnownPacketName t.InputPacketName
    ModuleOverPacket t.KnownPacketName t.InputModuleName
    PacketOverModule t.KnownModuleName t.InputPacketName
*/

/*
  <IsKnown t.hContext t.ModuleName>
    == t.hContext Known
    == t.hContext NotKnown
    == t.hContext Error e.ContextErrorMessage
*/
IsKnown2 {
  [Context t.SymTable t.CaseTable] t.ModuleName =
    <IsKnown-SwCheckSpace
      t.SymTable
      <MCaseTable::CheckSpace t.CaseTable t.ModuleName>
    >;
}

IsKnown-SwCheckSpace {
  t.SymTable t.CaseTable AvailSpace =
    [Context t.SymTable t.CaseTable] NotKnown;

  t.SymTable t.CaseTable ModuleExist t.ModuleName =
    [Context t.SymTable t.CaseTable] Known;

  t.SymTable t.CaseTable Collision s.Collision t.Known t.Input =
    [Context t.SymTable t.CaseTable]
    Error <ContextErrorFromCollision s.Collision> t.Known t.Input;
}

ContextErrorFromCollision {
  IllegalModuleCase = MismatchModuleCase;
  IllegalPacketCase = MismatchPacketCase;

  ModuleOverPacket = ModuleOverPacket;
  PacketOverModule = PacketOverModule;
}

/*
  <IsKnown t.hContext t.ModuleName>
    == t.hContext Known
    == t.hContext MismatchCase
    == t.hContext NotKnown
*/
?IsKnown
  t.Context t.ModuleName =
    <MSelfDiag::Log '<IsKnown>'>
    <MSelfDiag::Log '  t.Context = ' t.Context>
    <MSelfDiag::Log '  t.ModuleName = ' t.ModuleName>
    <MSelfDiag::Log>
    <IsKnown??
      <?IsKnown t.Context t.ModuleName>
    >;

$import MSelfDiag;

IsKnown??
  t.hContext s.Result =
    <MSelfDiag::Log '</IsKnown>'>
    <MSelfDiag::Log '  t.hContext = ' t.hContext>
    <MSelfDiag::Log '  s.Result = ' s.Result>
    <MSelfDiag::Log>
    t.hContext s.Result;

IsKnown_
  [Context t.SymTable t.CaseTable] t.ModuleName =
    <IsKnown-CheckName
      t.CaseTable
      <MSymTable::GetAttrib
        t.SymTable <Explode t.ModuleName> RealName
      >
      t.ModuleName
    >;

IsKnown-CheckName {
  t.CaseTable t.SymTable Found Unknown t.RealName =
    [Context t.SymTable t.CaseTable] Known;

  t.CaseTable t.SymTable Found t.RealName t.RealName =
    [Context t.SymTable t.CaseTable] Known;

  t.CaseTable t.SymTable Found t.RealName-SymTable t.RealName-Argument =
    [Context t.SymTable t.CaseTable] MismatchCase;

  t.CaseTable t.SymTable NoName t.RealName =
    [Context t.SymTable t.CaseTable] NotKnown;
}

/*
  <UpdateInfo t.hContext t.QualifiedName e.NewOptionList>
    == t.hUpdatedContext Success
    == t.hContext Fails s.Error
  s.Error ::= UnknownOption | MismatchCase
*/
?UpdateInfo
  t.Context t.ModuleName e.NewOptions =
    <MSelfDiag::Log '<UpdateInfo>'>
    <MSelfDiag::Log '  t.Context = ' t.Context>
    <MSelfDiag::Log '  t.ModuleName = ' t.ModuleName>
    <MSelfDiag::Log '  e.NewOptions = ' e.NewOptions>
    <MSelfDiag::Log>
    <UpdateInfo??
      <?UpdateInfo t.Context t.ModuleName e.NewOptions>
    >;

UpdateInfo?? {
  t.Context Success =
    <MSelfDiag::Log '</UpdateInfo> Success'>
    <MSelfDiag::Log '  t.Context = ' t.Context>
    <MSelfDiag::Log>
    t.Context Success;

  t.Context Fails s.Result =
    <MSelfDiag::Log '</UpdateInfo> Fails>'>
    <MSelfDiag::Log '  t.Context = ' t.Context>
    <MSelfDiag::Log '  s.Result = ' s.Result>
    <MSelfDiag::Log>
    t.Context Fails s.Result;
}

/*
  <UpdateInfo t.hContext t.QualifiedName e.NewOptionList>
    == t.hUpdatedContext Success
    == t.hContext Fails UnknownOption
    == t.hContext Fails e.ContextErrorMessage
*/
UpdateInfo2 {
  [Context t.SymTable t.CaseTable] t.ModuleName e.NewOptions =
    <UpdateInfo-SwInsertModule
      t.SymTable t.ModuleName
      <MCaseTable::InsertModule t.CaseTable t.ModuleName>
      e.NewOptions
    >;
}

UpdateInfo-SwInsertModule {
  t.SymTable t.ModuleName
  t.CaseTable Success e.NewOptions =
    /* TODO!!! */;

  t.SymTable t.ModuleName
  t.CaseTable ModuleExist t.ModuleName e.NewOptions =
    /* TODO!!! */;

  t.SymTable t.ModuleName
  t.CaseTable Collision s.Collision t.Known t.Input =
    [Context t.SymTable t.ModuleName]
    Fails <ContextErrorFromCollision s.Collision> t.Known t.Input;
}

//UpdateInfo-FindName {
//  t.CaseTable t.SymTable Found Unknown
//  t.ModuleName (e.KeyName) e.NewOptions =
//    <UpdateInfo-UpdateOptions
//      t.CaseTable t.SymTable (e.KeyName) e.NewOptions
//    >;
//
//  t.CaseTable t.SymTable Found t.RealName
//  t.RealName (e.KeyName) e.NewOptions =
//    <UpdateInfo-UpdateOptions
//      t.CaseTable t.SymTable (e.KeyName) e.NewOptions
//    >;
//
//  t.CaseTable t.SymTable Found t.RealName
//  t.ModuleName (e.KeyName) e.NewOptions =
//    [Context t.SymTable t.CaseTable] Fails MismatchCase;
//
//  t.CaseTable t.SymTable NoName
//  t.ModuleName (e.KeyName) e.NewOptions =
//    <UpdateInfo-UpdateOptions
//      t.CaseTable
//      <MSymTable::UpdateAttribSet
//        t.SymTable (e.KeyName) <DefaultOptions>
//      >
//      (e.KeyName) e.NewOptions
//    >;
//}

UpdateInfo {
  [Context t.SymTable t.CaseTable] t.ModuleName e.NewOptions =
    <UpdateInfo-Aux
      t.CaseTable t.SymTable
      t.ModuleName (<Explode t.ModuleName>) e.NewOptions
    >;
}

UpdateInfo-Aux {
  t.CaseTable t.SymTable t.ModuleName (e.KeyName) e.NewOptions =
    <UpdateInfo-FindName
      t.CaseTable
      <MSymTable::GetAttrib
        t.SymTable e.KeyName RealName
      >
      t.ModuleName (e.KeyName) e.NewOptions
    >;
}

UpdateInfo-FindName {
  t.CaseTable t.SymTable Found Unknown
  t.ModuleName (e.KeyName) e.NewOptions =
    <UpdateInfo-UpdateOptions
      t.CaseTable t.SymTable (e.KeyName) e.NewOptions
    >;

  t.CaseTable t.SymTable Found t.RealName
  t.RealName (e.KeyName) e.NewOptions =
    <UpdateInfo-UpdateOptions
      t.CaseTable t.SymTable (e.KeyName) e.NewOptions
    >;

  t.CaseTable t.SymTable Found t.RealName
  t.ModuleName (e.KeyName) e.NewOptions =
    [Context t.SymTable t.CaseTable] Fails MismatchCase;

  t.CaseTable t.SymTable NoName
  t.ModuleName (e.KeyName) e.NewOptions =
    <UpdateInfo-UpdateOptions
      t.CaseTable
      <MSymTable::UpdateAttribSet
        t.SymTable (e.KeyName) <DefaultOptions>
      >
      (e.KeyName) e.NewOptions
    >;
}

UpdateInfo-UpdateOptions {
  t.CaseTable t.SymTable (e.KeyName) e.Options =
    <UpdateInfo-UpdateOptions-ExistOptions
      t.CaseTable t.SymTable (e.KeyName) () e.Options
    >;
}

UpdateInfo-UpdateOptions-ExistOptions {
  t.CaseTable t.SymTable (e.KeyName) (e.Scanned)
  (s.NextOption e.NextValue) e.Options =
    <UpdateInfo-UpdateOptions-ExistOptions-SwExist
      t.CaseTable
      <MSymTable::GetAttrib t.SymTable e.KeyName s.NextOption>
      (e.KeyName) (e.Scanned) (s.NextOption e.NextValue) (e.Options)
    >;

  t.CaseTable t.SymTable (e.KeyName) (e.Scanned) =
    [Context
      <MSymTable::UpdateAttribSet t.SymTable (e.KeyName) e.Scanned>
      t.CaseTable
    ]
    Success;
}

UpdateInfo-UpdateOptions-ExistOptions-SwExist {
  t.CaseTable t.SymTable Found e.Value
  (e.KeyName) (e.Scanned) (s.NextOption e.NewValue) (e.Options) =
    <UpdateInfo-UpdateOptions-ExistOptions
      t.CaseTable
      t.SymTable (e.KeyName) (e.Scanned (s.NextOption e.NewValue)) e.Options
    >;

  t.CaseTable t.SymTable NoAttrib
  (e.KeyName) (e.Scanned) (s.NextOption e.NewValue) (e.Options) =
    [Context t.SymTable t.CaseTable] Fails UnknownOption;

  // NoName не рассматриваем
}

//------------------------------------------------------------------------------

/*
  <ExtractOption t.hContext t.QualifiedName s.Option>
    == t.hContext Found e.Option
    == t.hContext OptionNotFound
    == t.hContext ModuleNotFound
    == t.hContext MismatchCase
*/
?ExtractOption
  t.Context t.ModuleName s.Option =
    <MSelfDiag::Log '<ExtractOption>'>
    <MSelfDiag::Log '  t.Context = ' t.Context>
    <MSelfDiag::Log '  t.ModuleName = ' t.ModuleName>
    <MSelfDiag::Log '  s.Option = ' s.Option>
    <MSelfDiag::Log>
    <ExtractOption??
      <?ExtractOption t.Context t.ModuleName s.Option>
    >;

ExtractOption?? {
  t.Context Found e.Option =
    <MSelfDiag::Log '</ExtractOption> Found'>
    <MSelfDiag::Log '  t.Context = ' t.Context>
    <MSelfDiag::Log '  e.Option = ' e.Option>
    <MSelfDiag::Log>
    t.Context Found e.Option;

  t.Context s.Error =
    <MSelfDiag::Log '</ExtractOption>'>
    <MSelfDiag::Log '  t.Context = ' t.Context>
    <MSelfDiag::Log '  s.Error = ' s.Error>
    <MSelfDiag::Log>
    t.Context s.Error;
}

ExtractOption {
  [Context t.SymTable t.CaseTable] t.QualifiedName s.Option =
    <ExtractOption-Aux
      t.CaseTable t.SymTable
      t.QualifiedName <Explode t.QualifiedName> s.Option
    >;
}

ExtractOption-Aux {
  t.CaseTable t.SymTable t.ModuleName e.KeyName s.Option =
    <ExtractOption-CheckName
      t.CaseTable
      <MSymTable::GetAttrib t.SymTable e.KeyName s.Option>
      t.ModuleName (e.KeyName)
    >;
}

ExtractOption-CheckName {
  t.CaseTable t.SymTable Found e.Info t.ModuleName (e.KeyName) =
    <ExtractOption-CheckName-SwMatch
      e.Info t.ModuleName t.CaseTable
      <MSymTable::GetAttrib t.SymTable e.KeyName RealName>
    >;

  t.CaseTable t.SymTable NoAttrib t.ModuleName (e.KeyName) =
    [Context t.SymTable t.CaseTable] OptionNotFound;

  t.CaseTable t.SymTable NoName t.ModuleName (e.KeyName) =
    [Context t.SymTable t.CaseTable] ModuleNotFound;
}

ExtractOption-CheckName-SwMatch {
  e.Info t.ModuleName t.CaseTable t.SymTable Found Unknown =
    [Context t.SymTable t.CaseTable] Found e.Info;

  e.Info t.RealName t.CaseTable t.SymTable Found t.RealName =
    [Context t.SymTable t.CaseTable] Found e.Info;

  e.Info t.ModuleName t.CaseTable t.SymTable Found t.RealName =
    [Context t.SymTable t.CaseTable] MismatchCase;

  // Остальные варианты не рассматриваем — нарушение инварианта
}

//------------------------------------------------------------------------------

/**
  <GetStatusType t.hContext t.ModuleName>
    == t.hContext Success s.Status s.Type
    == t.hContext Fails ModuleNotFound
    == t.hContext Fails MismatchCase
*/
$ENTRY GetStatusType_
  t.hContext t.ModuleName =
    <GetStatusType-SwKnown
      <IsKnown_ t.hContext t.ModuleName> t.ModuleName
    >;

GetStatusType-SwKnown {
  t.hContext Known t.ModuleName =
    <GetStatusType-ReadStatus
      <ExtractOption t.hContext t.ModuleName Status>
      t.ModuleName
    >;

  t.hContext MismatchCase t.ModuleName =
    t.hContext Fails MismatchCase;

  t.hContext NotKnown t.ModuleName =
    t.hContext Fails ModuleNotFound;
}

GetStatusType-ReadStatus
  t.hContext Found s.Status t.ModuleName =
    <GetStatusType-ReadType
      s.Status
      <ExtractOption t.hContext t.ModuleName Type>
    >;

GetStatusType-ReadType
  s.Status t.hContext Found s.Type =
    t.hContext Success s.Status s.Type;

/**---------------------------------------------------------------------------*/

/**
  Функции уровня поиска модулей.
*/

/**
  <CreateModule
    t.hContext t.ModuleName
    (Type s.Type)
    t.FrontEndInfo
    t.BackEndInfo
    (BaseDirectory e.BaseDirectory)
  >
    == t.hContext Success
    == t.hContext Fails Redefinition

  t.FrontEndInfo ::=
    (FrontEnd s.FrontEnd e.PathToFrontEnd) | (FrontEnd None)
  t.BackEndInfo ::=
    (BackEnds (s.BackEnd e.PathToBackEnd)*) | (BackEnds Unknowns)
*/
$ENTRY CreateModule_
  t.hContext t.ModuleName
  (Type s.Type)
  (FrontEnd s.FrontEnd e.PathToFrontEnd)
  (BackEnds e.BackEnds) =
    <CreateModule-SwDefined
      <IsKnown_ t.hContext t.ModuleName>
      t.ModuleName
      (Type s.Type)
      (FrontEnd s.FrontEnd e.PathToFrontEnd)
      (BackEnds e.BackEnds)
      (Status Found)
    >;

CreateModule-SwDefined {
  t.hContext NotKnown
  t.ModuleName e.NewOptions =
    <CreateModule-SwCreated
      <UpdateInfo t.hContext t.ModuleName e.NewOptions>
    >;

  t.hContext Known
  t.ModuleName e.NewOptions =
    t.hContext Fails Redefinition;

  t.hContext MismatchCase
  t.ModuleName e.NewOptions =
    t.hContext Fails Redefinition;

  // Вариант UnknownOption не рассматриваем -- нарушение инварианта
}

CreateModule-SwCreated {
  t.hContext Success = t.hContext Success;

  // Вариант UnknownOption не рассматриваем -- нарушение инварианта

  /*
    Вариант MismatchCase не рассматриваем -- в контексте не должна была
    присутствовать иноформация о модуле до добавления
  */
}

/**
  <GetFrontEnd t.hContext t.ModuleName>
    == t.hContext Success None
    == t.hContext Success s.FrontEnd e.Path
    == t.hContext Fails ModuleNotFound
    == t.hContext Fails MismatchCase
*/
$ENTRY GetFrontEnd
  t.hContext t.ModuleName =
    <GetFrontEnd-Aux
      <ExtractOption t.hContext t.ModuleName FrontEnd>
    >;

GetFrontEnd-Aux {
  t.hContext Found None = t.hContext Success None;

  t.hContext Found s.FrontEnd e.Path =
    t.hContext Success s.FrontEnd e.Path;

  t.hContext ModuleNotFound = t.hContext Fails ModuleNotFound;

  t.hContext MismatchCase = t.hContext Fails MismatchCase;
}

/**
  <GetBackEndList t.hContext t.ModuleName>
    == t.hContext Success (s.BackEnd e.Path)*
    == t.hContext Fails ModuleNotFound
    == t.hContext Fails MismatchCase
*/
$ENTRY GetBackEndList
   t.hContext t.ModuleName =
     <GetBackEndList-Aux
       <ExtractOption t.hContext t.ModuleName BackEnds>
     >;

GetBackEndList-Aux {
  t.hContext Found e.BackEndList = t.hContext Success e.BackEndList;

  t.hContext ModuleNotFound = t.hContext Fails ModuleNotFound;

  t.hContext MismatchCase = t.hContext Fails MismatchCase;
}

/**
  <GetBackEnd t.hContext t.ModuleName s.BackEnd>
    == t.hContext Success e.Path
    == t.hContext Fails ModuleNotFound
    == t.hContext Fails MismatchCase
    == t.hContext Fails BackEndNotFound
*/
$ENTRY GetBackEnd
  t.hContext t.ModuleName s.BackEnd =
    <GetBackEnd-Aux
      <GetBackEndList t.hContext t.ModuleName> s.BackEnd
    >;

GetBackEnd-Aux {
  t.hContext Success e.BackEnds-B (s.BackEnd e.Path) e.BackEnds-E
  s.BackEnd =
    t.hContext Success e.Path;

  t.hContext Success e.BackEnds s.BackEnd =
    t.hContext Fails BackEndNotFound;

  t.hContext Fails ModuleNotFound s.BackEnd =
    t.hContext Fails ModuleNotFound;

  t.hContext Fails MismatchCase =
    t.hContext Fails MismatchCase;
}

/**---------------------------------------------------------------------------*/

/**
  Функции "сырого" уровня.
*/

/**
  <SetRawType t.hContext t.ModuleName s.Type>
    == t.hContext Success
    == t.hContext Fails ModuleNotFound
    == t.hContext Fails MismatchCase
    == t.hContext Fails CantUpdate
*/
$ENTRY SetRawType_
  t.hContext t.ModuleName s.Type =
    <SetRawType-SwKnown
      <GetStatusType_ t.hContext t.ModuleName>
      t.ModuleName s.Type
    >;

SetRawType-SwKnown {
  t.hContext Success Found s.OldType t.ModuleName s.NewType =
    <GuardUpdateInfo
      <UpdateInfo
        t.hContext t.ModuleName (Status Raw) (Type s.NewType)
      >
    >;

  t.hContext Success s.OtherStatus s.OldType t.ModuleName s.NewType =
    t.hContext Fails CantUpdate;

  t.hContext Fails ModuleNotFound = t.hContext Fails ModuleNotFound;

  t.hContext Fails MismatchCase = t.hContext Fails MismatchCase;
}

GuardUpdateInfo {
  t.hContext Success = t.hContext Success;

  // Остальные варианты невозможны
}

/**
  <SetMinMaxTime t.hContext t.ModuleName (e.MinTime) (e.MaxTime)>
    == t.hContext Success
    == t.hContext Fails ModuleNotFound
    == t.hContext Fails MismatchCase
*/
$ENTRY SetMinMaxTime_
  t.hContext t.ModuleName (e.MinTime) (e.MaxTime) =
    <SetMinMaxTime-SwKnown
      <IsKnown_ t.hContext t.ModuleName>
      t.ModuleName (e.MinTime) (e.MaxTime)
    >;

SetMinMaxTime-SwKnown {
  t.hContext Known t.ModuleName (e.MinTime) (e.MaxTime) =
    <GuardUpdateInfo
      <UpdateInfo
        t.hContext t.ModuleName (MinMaxTime (e.MinTime) (e.MaxTime))
      >
    >;

  t.hContext NotKnown t.ModuleName (e.MinTime) (e.MaxTime) =
    t.hContext Fails ModuleNotFound;

  t.hContext MismatchCase t.ModuleName (e.MinTime) (e.MaxTime) =
    t.hContext Fails MismatchCase;
}

/**
  <GetMinMaxTime t.hContext t.ModuleName>
    == t.hContext Success Unknown
    == t.hContext Success (e.MinTime) (e.MaxTime)
    == t.hContext Fails ModuleNotFound
    == t.hContext Fails MismatchCase
*/
$ENTRY GetMinMaxTime
  t.hContext t.ModuleName =
    <GetMinMaxTime-Aux
      <ExtractOption t.hContext t.ModuleName MinMaxTime>
    >;

GetMinMaxTime-Aux {
  t.hContext Found Unknown = t.hContext Success Unknown;

  t.hContext Found (e.MinTime) (e.MaxTime) =
    t.hContext Success (e.MinTime) (e.MaxTime);

  t.hContext ModuleNotFound = t.hContext Fails ModuleNotFound;

  t.hContext MismatchCase = t.hContext Fails MismatchCase;
}

/**---------------------------------------------------------------------------*/

/**
  Функции уровня компиляции.
*/

/**
  <FinishModule t.hContext t.RealModuleName s.ModuleType s.Type>
    == t.hContext Success
    == t.hContext Fails ModuleNotFound
    == t.hContext Fails MismatchCase
    == t.hContext Fails CantUpdate
*/
$ENTRY FinishModule_ {
  t.hContext t.RealModuleName s.ModuleType Errors =
    <FinishModule-SwKnown
      <GetStatusType_ t.hContext t.RealModuleName>
      t.RealModuleName
      (Type Errors)
      (Status Ready)
    >;

  t.hContext t.RealModuleName s.ModuleType s.Type =
    <FinishModule-SwKnown
      <GetStatusType_ t.hContext t.RealModuleName>
      t.RealModuleName
      (Type s.Type)
      (Status Ready)
      (RealName t.RealModuleName)
      (ModuleType s.ModuleType)
    >;
}

FinishModule-SwKnown {
  t.hContext Success Raw s.Type t.ModuleName e.NewOptions =
    <FinishModule-Aux
      <UpdateInfo t.hContext t.ModuleName e.NewOptions>
    >;

  t.hContext Success s.Status s.Type t.ModuleName e.NewOptions =
    t.hContext Fails CantUpdate;

  t.hContext Fails ModuleNotFound t.ModuleName e.NewOptions =
    t.hContext Fails ModuleNotFound;

  t.hContext Fails MismatchCase t.ModuleName e.NewOptions =
    t.hContext Fails MismatchCase;
}

FinishModule-Aux {
  t.hContext Success = t.hContext Success;

  // Другие варианты недопустимы
}

/**---------------------------------------------------------------------------*/

/**
  Доступ к некоторым полям
*/

/**
  <GetTarget t.hContext t.ModuleName>
    == Success Default
    == Success e.PtTargetName
    == Fails s.Error

  s.Error ::= ModuleNotFound | MismatchCase
*/
$ENTRY GetTarget {
  t.hContext t.ModuleName =
    <GetTarget-Aux <ExtractOption t.hContext t.ModuleName Target>>;
}

GetTarget-Aux {
  t.hContext Found Default = t.hContext Success Default;
  t.hContext Found e.PtTarget = t.hContext Success e.PtTarget;
  t.hContext ModuleNotFound = t.hContext Fails ModuleNotFound;
  t.hContext MismatchCase = t.hContext Fails MismatchCase;

  // MismatchOption не рассматриваем.
}

/**
  <SetTarget t.hContext t.ModuleName e.PtTargetName>
    == t.hContext Succcess
    == t.hContext Fails s.Error

  s.Error ::= ModuleNotFound | MismatchCase
*/
$ENTRY SetTarget_ {
  t.hContext t.ModuleName e.PtTargetName =
    <SetTarget-SwKnown
      <IsKnown_ t.hContext t.ModuleName>
      t.ModuleName e.PtTargetName
    >;
}

SetTarget-SwKnown {
  t.hContext Known t.ModuleName e.PtTargetName =
    // Должна вернуть Success
    <UpdateInfo t.hContext t.ModuleName (Target e.PtTargetName)>;

  t.hContext MismatchCase t.ModuleName e.PtTargetName =
    t.hContext Fails MismatchCase;

  t.hContext NotKnown t.ModuleName e.PtTargetName =
    t.hContext Fails ModuleNotFound;
}

/**
  <GetCachedImports t.hContext t.ModuleName>
    == Success None
    == Success t.ImportName*
    == Fails s.Error

  s.Error ::= ModuleNotFound | MismatchCase
*/
$ENTRY GetCachedImports {
  t.hContext t.ModuleName =
    <GetCachedImports-Aux
      <ExtractOption t.hContext t.ModuleName CachedImports>
    >;
}

GetCachedImports-Aux {
  t.hContext Found None = t.hContext Success None;
  t.hContext Found e.Imports = t.hContext Success e.Imports;
  t.hContext ModuleNotFound = t.hContext Fails ModuleNotFound;
  t.hContext MismatchCase = t.hContext Fails MismatchCase;

  // MismatchOption не рассматриваем.
}

/**
  <SetCachedImports t.hContext t.ModuleName t.ImportName*>
    == t.hContext Success
    == t.hContext Fails s.Error

  s.Error ::= ModuleNotFound | MismatchCase
*/
$ENTRY SetCachedImports_ {
  t.hContext t.ModuleName e.ImportList =
    <SetCachedImports-SwKnown
      <IsKnown_ t.hContext t.ModuleName>
      t.ModuleName e.ImportList
    >;
}

SetCachedImports-SwKnown {
  t.hContext Known t.ModuleName e.ImportList =
    // Должно вернуть Success
    <UpdateInfo t.hContext t.ModuleName (CachedImports e.ImportList)>;

  t.hContext MismatchCase t.ModuleName e.ImportList =
    t.hContext Fails MismatchCase;

  t.hContext NotKnown t.ModuleName e.ImportList =
    t.hContext Fails ModuleNotFound;
}

$END Driver::MContext.
