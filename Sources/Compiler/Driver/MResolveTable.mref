$MODULE Driver::MResolveTable;

$IMPORT MQualifiedName;

/*------------------------------------------------------------------------------
  Внутренний формат
    t.ResolveTable ::= [ResolveTable t.Pair*]
    t.Pair ::= ((e.InternalSegments) s.ScopeClass e.ExternalSegments)
  e.***Segments образуются применением MQualifiedName::Parse к ***Qualifier
------------------------------------------------------------------------------*/

$DATA ResolveTable;

/**
  <Create> == t.ResolveTable
*/
$ENTRY Create = [ResolveTable ];

/**
  <Destroy t.ResolveTable> == empty
*/
$ENTRY Destroy
  [ResolveTable e.Pairs] = ;

/**
  <Add t.ResolveTable
    t.InternalQualifier
    s.ScopeClass
    t.ExternalQualifier
  > == t.UpdatedResolveTable
*/
$ENTRY Add
  [ResolveTable e.Pairs]
  t.InternalQualifier s.ScopeClass t.ExternalQualifier =
    [ResolveTable
      ( (<MQualifiedName::Parse t.InternalQualifier>)
        s.ScopeClass
        <MQualifiedName::Parse t.ExternalQualifier>
      )
      e.Pairs
    ];

/**
  <AddBrokenImport t.hResolveTable t.ImportName>
    == t.hResolveTable
*/
$ENTRY AddBrokenImport {
  [ResolveTable e.Pairs] t.ImportName =
    [ResolveTable
      ((<MQualifiedName::Parse t.ImportName>) BrokenImport)
      e.Pairs
    ];
}

/**
  <Lookup t.ResolveTable (e.ValidScopes) t.InternalQualifier>
    == t.ResolveTable Success s.ScopeClass t.ExternalQualifier
    == t.ResolveTable Fails NotFound t.InternalQualifier
    == t.ResolveTable Fails BrokenImport t.InternalQualifier
  e.ValidScopes ::= s.ScopeClass*
*/
$ENTRY Lookup
  t.ResolveTable (e.ValidScopes) t.InternalQualifier =
    <Lookup-BySegments
      t.ResolveTable (e.ValidScopes)
      <MQualifiedName::Parse-T t.InternalQualifier>
    >;

Lookup-BySegments {
  [ResolveTable
    e.Pairs-B
    ((e.ImportSegments) BrokenImport)
    e.Pairs-E
  ]
  (e.ValidScopes)
  t.InternalQualifier e.ImportSegments e.InternalSegments =
    [ResolveTable
      ((e.ImportSegments) BrokenImport)
      e.Pairs-B e.Pairs-E
    ]
    Fails BrokenImport t.InternalQualifier;

  [ResolveTable
    e.Pairs-B
    ((e.InternalSegments) s.ScopeClass e.ExternalSegments)
    e.Pairs-E
  ]
  (e.ValidScopes-B s.ScopeClass e.ValidScopes-E)
  t.InternalQualifier e.InternalSegments =
    [ResolveTable
      ((e.InternalSegments) s.ScopeClass e.ExternalSegments)
      e.Pairs-B e.Pairs-E
    ]
    Success s.ScopeClass
    <MQualifiedName::Destroy t.InternalQualifier>
    <MQualifiedName::Create e.ExternalSegments>;

  t.ResolveTable (e.ValidScopes) t.InternalQualifier e.InternalSegments =
    t.ResolveTable Fails NotFound t.InternalQualifier;
}

$END Driver::MResolveTable.
