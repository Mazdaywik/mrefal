$MODULE FE-MRefal::MParser_Common;

$IMPORT MLexer = FE-MRefal::MLexer;
$IMPORT MLambda;
$IMPORT MError;
$IMPORT MIModule;
$IMPORT MQualifiedName;

//==============================================================================
// Служебные функции
//==============================================================================

/**
  <Parse s.FnModuleBody e.FileName>
    == t.hErrorList Success t.IModule
    == t.hErrorList Fails
*/
$ENTRY Parse
  s.FnModuleBody e.FileName =
    <NGenericModule
      s.FnModuleBody
      <MError::CreateErrList>
      <MLexer::NextToken
        <MLexer::Create e.FileName>
      >
    >;

/**
  <GeneralizeInfo
    t.hGenericErrorList t.QualifiedName
    t.hLocalErrorList {Maybe t.hIModule}
  >
    == t.hErrorList Success s.ErrorsLevel t.IModule
    == t.hErrorList Fails
  s.ErrorsLevel ::= Success | Warnings | Errors
  {Maybe t.hIModule} ::= Success t.hIModule | Fails
*/
$ENTRY GeneralizeInfo {
  t.hGenericErrorList t.QualifiedName
  t.hErrorList e.SuccessFailResult =
    <GeneralizeInfo-Aux
      t.hGenericErrorList
      <GeneralizeInfo-PrepareLocal t.hErrorList e.SuccessFailResult>
    >
    <MQualifiedName::Destroy t.QualifiedName>;
}

GeneralizeInfo-Aux {
  t.hGenericErrorList t.hLocalErrorList e.SuccessFailResult =
    <MError::MergeLists t.hGenericErrorList t.hLocalErrorList>
    e.SuccessFailResult;
}

GeneralizeInfo-PrepareLocal {
  t.hErrorList Success t.hIModule =
    <GeneralizeInfo-PrepareLocal-Aux
      <GetErrorLevel t.hErrorList> t.hIModule
    >;

  t.hErrorList Fails =
    t.hErrorList Fail;
}

GeneralizeInfo-PrepareLocal-Aux {
  t.hErrorList s.ErrorsLevel t.hIModule =
    t.hErrorList Success s.ErrorsLevel t.hIModule;
}

GetErrorLevel {
  t.hErrorList =
    <GetErrorLevel-Transform
      <MError::ContentLevel t.hErrorList>
    >;
}

GetErrorLevel-Transform {
  t.hErrorList Empty =
    t.hErrorList Success;

  t.hErrorList Warnigs =
    t.hErrorList Warnings;

  t.hErrorList Errors =
    t.hErrorList Errors;
}

//------------------------------------------------------------------------------
// Функции добавления в таблицу символов
//------------------------------------------------------------------------------

$ENTRY AddNewFunction
  t.hIModule t.SrcPos t.NewName e.Attribs t.hErrorList =
    <AddNewFunction-SwRedeclaration
      t.SrcPos t.NewName (e.Attribs) t.hErrorList
      <MIModule::GetKind
        <MIModule::AbsolutName t.hIModule t.NewName>
      >
    >;

AddNewFunction-SwRedeclaration {
  t.SrcPos t.NewName (e.Attribs) t.hErrorList
  t.hIModule Success s.Kind =
    t.hIModule
    <MError::AddRedeclaredIdentifier
      t.hErrorList t.SrcPos t.NewName
    >;

  t.SrcPos t.NewName (e.Attribs) t.hErrorList
  t.hIModule NoName =
    <UpdateEntity-GuardInvariant
      t.hErrorList
      <MIModule::UpdateFunction
        t.hIModule t.NewName e.Attribs (Position t.SrcPos)
      >
      t.SrcPos t.NewName
    >;
}

$ENTRY AddNewADT
  t.hIModule t.SrcPos t.NewName e.Attribs t.hErrorList =
    <AddNewFunctionAndADT-SwRedeclaration
      t.SrcPos t.NewName (e.Attribs) t.hErrorList
      & MIModule.UpdateADT
      <MIModule::GetKind
        <MIModule::AbsolutName t.hIModule t.NewName>
      >
    >;

AddNewFunctionAndADT-SwRedeclaration {
  t.SrcPos t.NewName (e.Attribs) t.hErrorList s.FnAdder
  t.hIModule Success s.Kind =
    t.hIModule
    <MError::AddRedeclaredIdentifier
      t.hErrorList t.SrcPos t.NewName
    >;

  t.SrcPos t.NewName (e.Attribs) t.hErrorList s.FnAdder
  t.hIModule NoName =
    <UpdateEntity-GuardInvariant
      t.hErrorList
      <s.FnAdder
        t.hIModule t.NewName e.Attribs (Position t.SrcPos)
      >
      t.SrcPos t.NewName
    >;
}

$ENTRY AddNewImport
  t.hIModule t.SrcPos t.NewName (RealName t.RealName) t.hErrorList =
    <AddNewImport-SwRedeclaration-Alias
      t.SrcPos t.NewName (RealName t.RealName) t.hErrorList
      <MIModule::GetKind t.hIModule t.NewName>
    >;

AddNewImport-SwRedeclaration-Alias {
  t.SrcPos t.NewName (RealName t.RealName) t.hErrorList
  t.hIModule Success s.Kind =
    t.hIModule
    <MError::AddRedeclaredIdentifier
      t.hErrorList t.SrcPos t.NewName
    >;

  t.SrcPos t.NewName (RealName t.RealName) t.hErrorList
  t.hIModule NoName =
    <AddNewImport-SwRedeclaration-RealName
      t.SrcPos t.NewName (RealName t.RealName) t.hErrorList
      <MIModule::GetKind t.hIModule t.RealName>
    >;
}

AddNewImport-SwRedeclaration-RealName {
  t.SrcPos t.NewName (RealName t.RealName) t.hErrorList
  t.hIModule Success ModuleName =
    t.hIModule
    <MError::AddRecursiveLink
      t.hErrorList t.SrcPos t.RealName
    >;

  t.SrcPos t.NewName (RealName t.RealName) t.hErrorList
  t.hIModule Success ImportName =
    t.hIModule
    <MError::AddRedeclaredIdentifier
      t.hErrorList t.SrcPos t.RealName
    >;

  t.SrcPos t.NewName (RealName t.RealName) t.hErrorList
  t.hIModule e.OtherResult =
    <UpdateEntity-GuardInvariant
      t.hErrorList
      <MIModule::UpdateImport-X
        t.hIModule
        (Position t.SrcPos)
        (RealName t.RealName)
        (AliasName t.NewName)
      >
      t.SrcPos t.NewName
    >;
}

UpdateEntity-GuardInvariant {
  t.hErrorList t.IModule Success t.SrcPos t.NewName =
    t.IModule t.hErrorList;

  t.hErrorList t.IModule InvalidKind t.SrcPos t.NewName =
    t.IModule
    <MError::AddRedeclaredIdentifier
      t.hErrorList t.SrcPos t.NewName
    >;

  /*
    Две другие ошибки -- InvalidAttrib и InvariantError означают
    нарушение инварианта. Программа автоматически умрёт.
  */
}

/**-----------------------------------------------------------------------------
  Функции синтаксического анализа делятся на три категории:
  (1) Функции, распознающие основные секции файла: начало, конец, объявления,
импорты, определения. Эти функции после себя на стеке ничего не оставляют.
Даже в формате этих функций стек отсутствует.
  После распознавания глобальной структуры эта информация сразу помещается
в IModule, поэтому после работы этих функций стека не остаётся.
  (2) Функции, распознающие остальные нетерминалы. Эти функции активно рабо-
тают со стеком. Эти функции после распознавания оставляют на стеке распознан-
ный нетерминал.
  (3) Промежуточные функции. Они могут работать так, как хотят.
------------------------------------------------------------------------------*/

//==============================================================================
// Общие нетерминалы.
//==============================================================================

//------------------------------------------------------------------------------
// Распознавание заголовка и окончания модуля.
//------------------------------------------------------------------------------

/*
  <NGenericModule s.FnBody t.hErrorList t.Lexer t.NextToken>
    == t.hErrorList Success t.IModule
    == t.hErrorList Fails
*/
NGenericModule
  s.FnBody t.hErrorList t.Lexer t.NextToken =
    <NGenericModule-SwParseHeader
      s.FnBody
      <NModuleHeader
        t.hErrorList t.Lexer t.NextToken
      >
    >;

NGenericModule-SwParseHeader {
  s.FnBody
  t.hErrorList Success t.IModule t.Lexer t.NextToken =
    <s.FnBody
      t.IModule t.hErrorList t.Lexer t.NextToken
    >;

  s.FnBody t.hErrorList Fails =
    t.hErrorList Fails;
}

//-------------------------------------------------------------------------------

// ModuleHeader = '$module' ModuleName ';' .

NModuleHeader
  t.hErrorList t.Lexer t.NextToken =
    <NModuleHeader-ModuleName
      <NModuleHeader-Directive
        t.hErrorList t.Lexer t.NextToken
      >
    >;

NModuleHeader-Directive {
  t.hErrorList t.Lexer (Directive t.SrcPos Module) =
    Regular
    t.hErrorList <MLexer::NextToken t.Lexer>;

  t.hErrorList t.Lexer (Directive t.SrcPos Program) =
    Main
    t.hErrorList <MLexer::NextToken t.Lexer>;

  t.hErrorList t.Lexer (Directive t.SrcPos ClearModule) =
    Regular
    t.hErrorList <MLexer::NextToken t.Lexer>;

  /* Обработка ошибок в данном случае -- добавляем в список ошибок неожиданный
  символ, считаем, что прочли текущую лексему как '$module' и продолжаем
  дальнейший разбор.
  */
  t.hErrorList t.Lexer t.OtherToken =
    Regular
    <MError::AddUnexpected
      t.hErrorList t.OtherToken 'directive \'$MODULE\''
    >
    <MLexer::NextToken t.Lexer>;
}

NModuleHeader-ModuleName {
  s.ModuleType
  t.hErrorList t.Lexer (Identifier t.SrcPos e.Name) =
    <NModuleHeader-ModuleName-CheckQualified
      s.ModuleType t.SrcPos
      <NQuadQualifiedName
        t.hErrorList
        t.Lexer (Identifier t.SrcPos e.Name)
      >
    >;

  /* Здесь мы пропускаем текущую неопознанную лексему до тех пор, пока
  не встретим идентификатор или конец ввода */
  s.ModuleType
  t.hErrorList t.Lexer (EndOfText t.SrcPos) =
    <MError::AddUnexpected
      t.hErrorList (EndOfText t.SrcPos) 'module name'
    >
    Fails
    <MLexer::Destroy t.Lexer>;

  s.ModuleType t.hErrorList t.Lexer t.OtherToken =
    <NModuleHeader-ModuleName
      s.ModuleType
      <MError::AddUnexpected
        t.hErrorList t.OtherToken 'module name'
      >
      <MLexer::NextToken t.Lexer>
    >;
}

CheckSuccess {
  (e.FnName) t.IModule Success = t.IModule;

  /* Нарушение инварианта -- fail down
  В случае fail down в отладочном дампе будет виден параметр
  e.FnName -- это позволяет отловить место вылета ошибки */
}

NModuleHeader-ModuleName-CheckQualified {
  s.ModuleType t.SrcPos Success t.QualifiedName
  t.hErrorList t.Lexer t.NextToken =
    <NModuleHeader-ExpectEndSent
      <CheckSuccess
        ('NMH-MN-CQ')
        <MIModule::Create
          t.QualifiedName
          (Position t.SrcPos)
          (ModuleType s.ModuleType)
        >
      >
      t.hErrorList t.Lexer t.NextToken
    >;

  /* Вообще-то эта ветвь не должна выполняться, т.к.
  функция NQuadQualifiedName возвращает Fails только если на входе
  она не получает идентификатор. А мы ей именно идентификатор посылаем. */
  s.ModuleType t.SrcPos Fails
  t.hErrorList t.Lexer t.NextToken =
    t.hErrorList Fails
    <MLexer::Destroy t.Lexer>;
}

NModuleHeader-ExpectEndSent {
  t.IModule t.hErrorList t.Lexer (EndSent t.SrcPos) =
    t.hErrorList
    Success t.IModule <MLexer::NextToken t.Lexer>;

  t.IModule t.hErrorList t.Lexer t.OtherToken =
    <MError::AddUnexpected
      t.hErrorList t.OtherToken 'semicolon'
    >
    Success t.IModule t.Lexer t.OtherToken;
}

//------------------------------------------------------------------------------

/*
  <NModuleTailer
    t.IModule t.hErrorList t.Lexer t.NextToken
  >
    == t.hErrorList Success t.IModule
    == t.hErrorList Fails
*/
$ENTRY NModuleTailer {
  t.hIModule t.hErrorList t.hLexer (Directive t.SrcPos End) =
    <NModuleTailer-ModuleName
      t.hIModule t.hErrorList t.hLexer
    >;
}

NModuleTailer-ModuleName {
  t.hIModule t.hErrorList t.hLexer =
    <NModuleTailer-ModuleName-Aux
      <MIModule::GetModuleName t.hIModule>
      <SrcPosQualifiedName
        t.hErrorList <MLexer::NextToken t.hLexer>
      >
    >;
}

SrcPosQualifiedName
  t.hErrorList t.Lexer (s.Token t.SrcPos e.Info) =
    t.SrcPos
    <NQuadQualifiedName
      t.hErrorList t.Lexer (s.Token t.SrcPos e.Info)
    >;

NModuleTailer-ModuleName-Aux {
  t.IModule t.HeadModuleName
  t.SrcPos Success t.TailModuleName
  t.hErrorList t.Lexer t.NextToken =
    <NModuleTailer-Dot
      <NModuleTailer-CompareNames
        t.SrcPos
        <MQualifiedName::Equals-T t.HeadModuleName t.TailModuleName>
        t.hErrorList
      >
      t.Lexer t.NextToken
    >
    Success
    t.IModule;

  /* Пропускаем следующую лексему, до тех пор, пока не встретим
  конец файла или идентификатор */
  t.IModule t.HeadModuleName
  t.SrcPos Fails
  t.hErrorList t.Lexer (EndOfText t.SrcPos) =
    <MError::AddUnexpected
      t.hErrorList (EndOfText t.SrcPos) 'repeat of module name'
    >
    Success
    t.IModule;

  t.hIModule t.HeadModuleName
  t.SrcPos Fails
  t.hErrorList t.Lexer t.OtherToken =
    <NModuleTailer-ModuleName-Aux
      t.hIModule t.HeadModuleName
      <SrcPosQualifiedName
        <MError::AddUnexpected
          t.hErrorList t.OtherToken 'repeat of module name'
        >
        <MLexer::NextToken t.Lexer>
      >
    >;
}

NModuleTailer-CompareNames {
  t.SrcPos
  True t.HeadName t.TailName t.hErrorList =
    t.hErrorList;

  t.SrcPos
  False t.HeadName t.TailName t.hErrorList =
    <MError::AddNamesMismatch
      t.hErrorList t.SrcPos t.HeadName t.TailName
    >;
}

/*
  <NModuleTailer-Dot t.hErrorList t.Lexer t.NextToken>
    == t.hErrorList
*/
NModuleTailer-Dot {
  t.hErrorList t.Lexer (Dot t.SrcPos) =
    <NModuleTailer-EOF
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  /* Считаем, что точка была */
  t.hErrorList t.Lexer t.OtherToken =
    <NModuleTailer-EOF
      <MError::AddUnexpected
        t.hErrorList t.OtherToken 'end dot'
      >
      t.Lexer t.OtherToken
    >;
}

NModuleTailer-EOF {
  t.hErrorList t.Lexer (EndOfText t.SrcPos) =
    t.hErrorList <MLexer::Destroy t.Lexer>;

  /* Пропускаем все другие лексемы до тех пор, пока не доберёмся до конца файла */
  t.hErrorList t.Lexer t.OtherToken =
    <NModuleTailer-EOF
      <MError::AddUnexpected
        t.hErrorList t.OtherToken 'end of file'
      >
      <MLexer::NextToken t.Lexer>
    >;
}

/*------------------------------------------------------------------------------
  Распознавание блока импорта. Импорт используется как в исходных, так и в
  символических файлах.
------------------------------------------------------------------------------*/

/*
  ImportBlock = ModuleImport { ',' ModuleImport } ';' .
  ModuleImport = [ ModuleAlias '=' ] ModuleName .
  Последнюю строчку для удобства разбора преобразуем:
  ModuleImport =
    IDENTIFIER '::' QuadQualifiedName |
    IDENTIFIER '=' QuadQualifiedName |
    IDENTIFIER .

  Таким образом, получим почти автоматную грамматику:
  ImportBlock =
    IDENTIFIER ImportBlock-AfterName .
  ImportBlock-AfterName =
    '::' ImportBlock-Qualified |
    '=' ImportBlock-ModuleRenaming |
    ',' ImportBlock |
    ';' . // конец
  ImportBlock-Qualified =
    QuadQualifiedName ImportBlock-AfterQualified .
  ImportBlock-AfterQualified =
    ',' ImportBlock |
    ';' . // конец
  ImportBlock-ModuleRenaming =
    QuadQualifiedName ImportBlock-AfterModuleRenaming
  ImportBlock-AfterModuleRenaming =
    ',' ImportBlock |
    ';' . // конец

  Не смотря на то, что описание грамматики для нетерминалов
  ImportBlock-Qualified и ImportBlock-ModuleRenaming
  одинаковы, записаны они отдельно, т.к. семантически они различаются.
*/
$ENTRY NImportBlock {
  t.IModule
  t.hErrorList t.Lexer (Identifier t.SrcPos e.ModuleName) =
    <NImportBlock-AfterName
      t.IModule
      ( Import t.SrcPos e.ModuleName )
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.IModule
  t.hErrorList t.Lexer t.OtherToken =
    t.IModule
    <MError::AddUnexpected
      t.hErrorList t.OtherToken 'module name or module alias'
    >
    <MLexer::NextToken t.Lexer>;
}

NImportBlock-AfterName {
  t.IModule (Import t.ImportPos e.ModuleName)
  t.hErrorList t.Lexer (DoubleColon t.SrcPos) =
    <NImportBlock-Qualified
      t.IModule
      (Import t.ImportPos e.ModuleName)
      <NQuadQualifiedName
        t.hErrorList <MLexer::NextToken t.Lexer>
      >
    >;

  t.IModule (Import t.ImportPos e.ModuleName)
  t.hErrorList t.Lexer (Replaces t.SrcPos) =
    <NImportBlock-ModuleRenaming
      t.IModule
      (Alias t.ImportPos e.ModuleName)
      <NQuadQualifiedName
        t.hErrorList <MLexer::NextToken t.Lexer>
      >
    >;

  t.IModule (Import t.ImportPos e.ModuleName)
  t.hErrorList t.Lexer (EndSent t.SrcPos) =
    <AddNewImport
      t.IModule t.ImportPos
      <MQualifiedName::Create (e.ModuleName)>
      (RealName
        <MQualifiedName::Create (e.ModuleName)>
      )
      t.hErrorList
    >
    <MLexer::NextToken t.Lexer>;

  t.IModule (Import t.ImportPos e.ModuleName)
  t.hErrorList t.Lexer (Comma t.SrcPos) =
    <NImportBlock
      <AddNewImport
        t.IModule t.ImportPos
        <MQualifiedName::Create (e.ModuleName)>
        (RealName
          <MQualifiedName::Create (e.ModuleName)>
        )
        t.hErrorList
      >
      <MLexer::NextToken t.Lexer>
    >;

  // Считаем, что пользователь пропустил точку с запятой
  t.IModule (Import t.ImportPos e.ModuleName)
  t.hErrorList t.Lexer t.OtherToken =
    t.IModule
    <MError::AddUnexpected
      t.hErrorList t.OtherToken 'comma, semicolon or replace'
    >
    t.Lexer t.OtherToken;
}

NImportBlock-Qualified {
  t.IModule (Import t.ImportPos e.FirstSegment)
  Success t.QualifiedName
  t.hErrorList t.Lexer t.NextToken =
    <NImportBlock-AfterQualified
      <AddNewImport
        t.IModule t.ImportPos
        <MQualifiedName::AddSuperName t.QualifiedName e.FirstSegment>
        (RealName
          <MQualifiedName::AddSuperName t.QualifiedName e.FirstSegment>
        )
        t.hErrorList
      >
      t.Lexer t.NextToken
    >;

  t.IModule (Import t.ImportPos e.FirstSegment)
  Fails
  t.hErrorList t.Lexer t.NextToken =
    <NImportBlock-AfterQualified
      t.IModule
      <MError::AddUnexpected
        t.hErrorList t.NextToken 'import name'
      >
      t.Lexer t.NextToken
    >;
}

NImportBlock-AfterQualified {
  t.IModule t.hErrorList
  t.Lexer (EndSent t.SrcPos) =
    t.IModule t.hErrorList
    <MLexer::NextToken t.Lexer>;

  t.IModule t.hErrorList
  t.Lexer (Comma t.SrcPos) =
    <NImportBlock
      t.IModule t.hErrorList
      <MLexer::NextToken t.Lexer>
    >;

  t.IModule t.hErrorList
  t.Lexer t.OtherToken =
    t.IModule
    <MError::AddUnexpected
      t.hErrorList t.OtherToken 'comma or semicolon'
    >
    t.Lexer t.OtherToken;
}

NImportBlock-ModuleRenaming {
  t.IModule (Alias t.AliasPos e.Alias)
  Success t.QualifiedName
  t.hErrorList t.Lexer t.NextToken =
    <NImportBlock-AfterModuleRenaming
      <AddNewImport
        t.IModule t.AliasPos
        <MQualifiedName::Create (e.Alias)>
        (RealName t.QualifiedName)
        t.hErrorList
      >
      t.Lexer t.NextToken
    >;

  t.IModule (Alias t.AliasPos e.Alias)
  Fails
  t.hErrorList t.Lexer t.NextToken =
    <NImportBlock-AfterModuleRenaming
      t.IModule
      <MError::AddUnexpected
        t.hErrorList t.NextToken 'import name'
      >
      t.Lexer t.NextToken
    >;
}

NImportBlock-AfterModuleRenaming {
  t.IModule
  t.hErrorList t.Lexer (Comma t.SrcPos) =
    <NImportBlock
      t.IModule
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.IModule
  t.hErrorList t.Lexer (EndSent t.SrcPos) =
    t.IModule
    t.hErrorList <MLexer::NextToken t.Lexer>;

  t.IModule
  t.hErrorList t.Lexer t.OtherToken =
    t.IModule
    <MError::AddUnexpected
      t.hErrorList t.OtherToken 'comma or semicolon'
    >
    t.Lexer t.OtherToken;
}

//------------------------------------------------------------------------------
// Распознавание квалифицированных имён.
//------------------------------------------------------------------------------

/**
  AnyQualifiedName = DotQualifiedName | QuadQualifiedName .
  DotQualifiedName =
    IDENTIFIER | DotQualifiedName '.' IDENTIFIER .
  QuadQualifiedName =
    IDENTIFIER | QuadQualifiedName '::' IDENTIFIER .

  <N***QualifiedName
    t.hErrorList t.Lexer t.NextToken
  >
    == e.ScannedQName t.hErrorList t.Lexer t.NextToken

  e.ScannedQName ::=
    Success t<MQualifiedName::QName>.QualifiedName | Fails

  В случае ошибки на стеке оставляется первый неожиданный терм.
  Если имя было частично определено, например так:
    'PackageName::ModuleName::222',
  то имя считается распознанным успешно. Информация об ошибке
  падает в t.hErrorList, но синтаксически инвариант соблюдётся.
*/
$ENTRY NAnyQualifiedName {
  t.hErrorList t.Lexer (Identifier t.SrcPos e.Name) =
    Success
    <NAnyQualifiedName-AfterName
      <MQualifiedName::Create (e.Name)>
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.hErrorList t.Lexer t.Unexpected =
    Fails
    <MError::AddUnexpected
      t.hErrorList t.Unexpected 'identifier'
    >
    t.Lexer t.Unexpected;
}

NAnyQualifiedName-AfterName {
  t.StartName t.hErrorList t.Lexer (DoubleColon t.SrcPos) =
    <NQuadQualifiedName-Loop
      t.StartName
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.StartName t.hErrorList t.Lexer (Dot t.SrcPos) =
    <NDotQualifiedName-Loop
      t.StartName
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.StartName t.hErrorList t.Lexer t.OtherToken =
    t.StartName
    t.hErrorList t.Lexer t.OtherToken;
}

$ENTRY NQuadQualifiedName {
  t.hErrorList t.Lexer (Identifier t.SrcPos e.Name) =
    Success
    <NQuadQualifiedName-AfterName
      <MQualifiedName::Create (e.Name)>
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.hErrorList t.Lexer t.Unexpected =
    Fails
    <MError::AddUnexpected
      t.hErrorList t.Unexpected 'identifier'
    >
    t.Lexer t.Unexpected;
}

NQuadQualifiedName-AfterName {
  t.PartOfName t.hErrorList t.Lexer (DoubleColon t.SrcPos) =
    <NQuadQualifiedName-Loop
      t.PartOfName
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.PartOfName t.hErrorList t.Lexer t.Unexpected =
    t.PartOfName
    t.hErrorList t.Lexer t.Unexpected;
}

NQuadQualifiedName-Loop {
  t.PartOfName t.hErrorList t.Lexer (Identifier t.SrcPos e.Name) =
    <NQuadQualifiedName-AfterName
      <MQualifiedName::AddSubName
        t.PartOfName e.Name
      >
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.PartOfName t.hErrorList t.Lexer t.Unexpected =
    t.PartOfName
    <MError::AddUnexpected
      t.hErrorList t.Unexpected 'identifier'
    >
    t.Lexer t.Unexpected;
}

$ENTRY NDotQualifiedName {
  t.hErrorList t.Lexer (Identifier t.SrcPos e.Name) =
    Success
    <NDotQualifiedName-AfterName
      <MQualifiedName::Create (e.Name)>
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.hErrorList t.Lexer t.Unexpected =
    Fails
    <MError::AddUnexpected
      t.hErrorList t.Unexpected 'identifier'
    >
    t.Lexer t.Unexpected;
}

NDotQualifiedName-AfterName {
  t.PartOfName t.hErrorList t.Lexer (Dot t.SrcPos) =
    <NDotQualifiedName-Loop
      t.PartOfName
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.PartOfName t.hErrorList t.Lexer t.Unexpected =
    t.PartOfName
    t.hErrorList t.Lexer t.Unexpected;
}

NDotQualifiedName-Loop {
  t.PartOfName t.hErrorList t.Lexer (Identifier t.SrcPos e.Name) =
    <NDotQualifiedName-AfterName
      <MQualifiedName::AddSubName
        t.PartOfName e.Name
      >
      t.hErrorList <MLexer::NextToken t.Lexer>
    >;

  t.PartOfName t.hErrorList t.Lexer t.Unexpected =
    t.PartOfName
    <MError::AddUnexpected
      t.hErrorList t.Unexpected 'identifier'
    >
    t.Lexer t.Unexpected;
}

$END FE-MRefal::MParser_Common.
