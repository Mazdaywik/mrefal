$MODULE FE-MRefal::MVarTable;

$IMPORT MError;

//------------------------------------------------------------------------------
// Работа с переменными
//------------------------------------------------------------------------------

$DATA VarTable;

$ENTRY Create = [VarTable];

$ENTRY Destroy
  [VarTable e.?Vars] = ;

$ENTRY AddVariable {
  /* Повторное вхождение переменной -- ничего не добавляем */
  t.hErrorList
  [VarTable e.Vars-B (s.VarType e.Index) e.Vars-E]
  t.SrcPos s.VarType e.Index =
    t.hErrorList
    [VarTable e.Vars-B (s.VarType e.Index) e.Vars-E]
    Success;

  /* Две разные переменные не могут иметь один индекс */
  t.hErrorList
  [VarTable e.Vars-B (s.OneVarType e.Index) e.Vars-E]
  t.SrcPos s.OtherVarType e.Index =
    <MError::AddVarTypeMismatch
      t.hErrorList t.SrcPos e.Index
    >
    [VarTable e.Vars-B (s.OneVarType e.Index) e.Vars-E]
    Fails;

  /* Просто добавляем переменную */
  t.hErrorList
  [VarTable e.Variables]
  t.SrcPos s.VarType e.Index =
    t.hErrorList [VarTable e.Variables (s.VarType e.Index)] Success;
}

$ENTRY CheckVariable {
  t.hErrorList
  [VarTable e.Vars-B (s.VarType e.Index) e.Vars-E]
  t.SrcPos s.VarType e.Index =
    t.hErrorList
    [VarTable e.Vars-B (s.VarType e.Index) e.Vars-E]
    Success;

  t.hErrorList
  [VarTable e.OtherVariables]
  t.SrcPos s.VarType e.Index =
    <MError::AddUndeclaredVariable
      t.hErrorList t.SrcPos s.VarType e.Index
    >
    [VarTable e.OtherVariables]
    Fails;
}

$END FE-MRefal::MVarTable.
