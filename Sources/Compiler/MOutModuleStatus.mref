$MODULE MOutModuleStatus;

$IMPORT InOut = MInOut;
$IMPORT Math = MMath;
$IMPORT MLambda;
$IMPORT MSwapSupport;
$IMPORT MQualifiedName;

$ENTRY Updated-Q
  t.QualifiedName =
    <FlushCompilingRecompiled>
    <Out-Q Updated t.QualifiedName>;

$ENTRY Compiling-Q
  t.ModuleName =
    <MSwapSupport::PushBack & G_Compiling (Compiling t.ModuleName)>;

$ENTRY Compiled-Q
  t.ModuleName =
    <Compiled-Aux
      ( <MSwapSupport::Read & G_Compiling> ) t.ModuleName
    >;

Compiled-Aux {
  ( e.Compiling (Compiling t.ModuleName) ) t.ModuleName =
    <MSwapSupport::Write & G_Compiling e.Compiling>
    <MSwapSupport::PushBack & G_Recompiled (Recompiled t.ModuleName)>;

  ( e.Compiling ) t.ModuleName =
    <FlushCompilingRecompiled>
    <Out-Q Compiled t.ModuleName>;
}

$ENTRY Library-Q
  t.QualifiedName =
    <FlushCompilingRecompiled>
    <Out-Q Library t.QualifiedName>;

$ENTRY Fails-Q
  t.QualifiedName =
    <FlushCompilingRecompiled>
    <Out-Q Fails t.QualifiedName>;

$ENTRY Linking
  e.ProgName =
    <FlushCompilingRecompiled>
    <Out Linking e.ProgName>;

$ENTRY XLinx
  e.ProgName =
    <FlushCompilingRecompiled>
    <Out XLinx e.ProgName>;

$SWAP G_Compiling, G_Recompiled;

FlushCompilingRecompiled =
  <MLambda::Map
    <MLambda::Composite
      & MLambda.UnBracket
      & Out-Q
    >
    <G_Compiling> <G_Recompiled>
  >;

//------------------------------------------------------------------------------

$SWAP G_CurrentStatus, G_Buffer;

Out-Q
 s.NewStatus t.QualifiedName =
   <Out s.NewStatus <MQualifiedName::ToPrintable-Quad t.QualifiedName>>;

Out
  s.NewStatus e.ModuleName =
    <SwOut
      s.NewStatus <G_CurrentStatus>
      '`' e.ModuleName '\''
    >;

SwOut {
  s.NewStatus Startup e.ModuleName =
    <BeginNewStatus s.NewStatus e.ModuleName>;

  s.OldStatus s.OldStatus e.ModuleName =
    <G_CurrentStatus s.OldStatus>
    <AppendCurrentStatus e.ModuleName>;

  s.NewStatus s.OldStatus e.ModuleName =
    <NewLine>
    <BeginNewStatus s.NewStatus e.ModuleName>;
}

StatusStr {
  Updated =    ' Updated ';
  Compiling =  '_Compiling ';
  Compiled =   '^Compiled ';
  Recompiled = '=Recompiled ';
  Library =    '+Load library ';
  Linking =    '*Linking ';
  XLinx =      '*Creating xlinx tables ';
  Fails =      '>  FAILS ';
}

MaxLength = 66;

CheckLength
  e.Line = <DoCheckLength <MaxLength> e.Line>;

Dec {
  1 = Limit;
  Limit = Limit;
  s.Other = <Math::Dec s.Other>;
}

DoCheckLength {
  Limit e.Line = Long;
  s.Other = Short;
  s.Other s.Char e.Line =
    <DoCheckLength
      <Dec s.Other> e.Line
    >;
}

BeginNewStatus {
  s.Status e.Name =
    <NewLine>
    <MSwapSupport::Write & G_CurrentStatus s.Status>
    <MSwapSupport::Write
      & G_Buffer
      <StatusStr s.Status> e.Name
    >;
}

NewLine = <SwNewLine <G_Buffer>>;

SwNewLine {
  = /* <InOut::WriteLine> */;
  e.Line = <InOut::WriteLine e.Line '.'>;
}

AppendCurrentStatus
  e.Name =
    <AppendCurrentStatus-CheckLength (<G_Buffer>) e.Name>;

AppendCurrentStatus-CheckLength {
  () e.Name = <BeginNewStatus <G_CurrentStatus> e.Name>;
  (e.Buffer) e.Name =
    <SwAppendCurrentStatus-CheckLength
      <CheckLength e.Buffer ', ' e.Name>
      <G_CurrentStatus> (e.Buffer) e.Name
    >;
}

SwAppendCurrentStatus-CheckLength {
  Short s.Status (e.Buffer) e.Name =
    <G_CurrentStatus s.Status> <G_Buffer e.Buffer ', ' e.Name>;

  Long s.Status (e.Buffer) e.Name =
    <G_Buffer e.Buffer>
    <BeginNewStatus s.Status e.Name>;
}

$ENTRY Flush = <SwFlush <G_Buffer>>;

SwFlush {
  = ;
  e.Line = <InOut::WriteLine e.Line '.'>;
}


$ENTRY Init = <G_CurrentStatus Starting>;
$ENTRY Final = <Flush>;

$END MOutModuleStatus.
