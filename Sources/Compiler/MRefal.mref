$PROGRAM MRefal;

$IMPORT MCompiler;
$IMPORT MConfig = Config::MConfig;
$IMPORT MContext;
$IMPORT MError;
$IMPORT MFileSystem;
$IMPORT MInOut;
$IMPORT MLambda;
$IMPORT MLoadConfig = Config::MLoad;
$IMPORT MModules;
$IMPORT MOS;
$IMPORT MQualifiedName;
$IMPORT MVersion;

$ENTRY Go =
  <MVersion::Show>
  <CompileList
    <MContext::Create>
    <MLoadConfig::Load
      <MError::CreateErrList>
      <MOS::ArgList>
    >
  >;

CompileList {
  t.hContext t.hErrorList Success (Help e.Message) =
    <MContext::Destroy t.hContext>
    <MError::DestroyErrList t.hErrorList>
    <MInOut::WriteText e.Message>;

  t.hContext t.hErrorList Success t.Config e.PtModules =
    <CompileList-Aux
      t.Config t.hContext t.hErrorList e.PtModules
    >;

  t.hContext t.hErrorList Fails =
    <MContext::Destroy t.hContext>
    <MError::PrintErrors t.hErrorList>;
}

CompileList-Aux {
  t.Config t.hContext t.hErrorList e.PtModules =
    <CheckFoundResult
      t.Config
      t.hErrorList
      <MLambda::MapReduce
        <MLambda::BindLeft
          & AddCommandLineModule t.Config
        >
        t.hContext
        e.PtModules
      >
    >;
}

AddCommandLineModule {
  t.L-Config t.hContext (e.PtModuleName t.Target) =
    <AddCommandLineModule-Aux
      (e.PtModuleName) t.Target
      <MModules::LookupCmdLineModule
        t.hContext t.L-Config e.PtModuleName
      >
    >;
}

AddCommandLineModule-Aux {
  (e.PtModuleName) t.Target t.hContext NotFount =
    t.hContext (NotFound e.PtModuleName);

  (e.PtModuleName) NoTarget
  t.hContext Found t.QualifiedName s.FrontEnd e.PtBaseDirectory =
    t.hContext (Found t.QualifiedName);

  (e.PtModuleName) (e.PtTargetName)
  t.hContext Found t.QualifiedName s.FrontEnd e.PtBaseDirectory =
    <AddCommandLineModule-Aux-Guard
      <MContext::SetTarget t.hContext t.QualifiedName e.PtTargetName>
    >
    (Found t.QualifiedName);
}

AddCommandLineModule-Aux-Guard {
  t.hContext Success = t.hContext;
}

CheckFoundResult {
  t.Config t.hErrorList
  t.hContext e.Results-B ( NotFound e.Module ) e.Results-E =
    <MContext::Destroy t.hContext>
    <MError::DestroyErrList t.hErrorList>
    <MLambda::Map
      & PrintNotFound
      e.Results-B ( (e.Module) NotFound ) e.Results-E
    >;

  t.Config t.hErrorList t.hContext e.ResultsFound =
    <MLambda::TerminateAccumulator
      & FinishMake
      <MLambda::Reduce
        <MLambda::BindLeft & Make t.Config>
        (t.hContext t.hErrorList Success)
        e.ResultsFound
      >
    >;
}

PrintNotFound {
  ( NotFound e.PtModule ) =
    <MInOut::WriteLine
      'CMDLINE ERROR: module '
      <MFileSystem::StrFromPath e.PtModule>
      ' not found'
    >;

  ( (e.Module) Found t.QualifiedName s.FrontEnd e.PtBaseDirectory ) =
    /* пусто */;
}

Make {
  t.Config (t.hContext t.hErrorList Success) ( Found t.QualifiedName ) =
    (
      <MCompiler::Make
        t.hContext
        t.hErrorList
        t.Config
        t.QualifiedName
      >
    );

  t.Config (t.hContext t.hErrorList Fails) ( Found t.QualifiedName ) =
    (t.hContext t.hErrorList Fails);
}

FinishMake {
  ( t.hContext t.hErrorList Success ) =
    <MContext::Destroy t.hContext>
    <MError::PrintErrors t.hErrorList>;

  ( t.hContext t.hErrorList Fails ) =
    <MContext::Destroy t.hContext>
    <MError::PrintErrors t.hErrorList>;
}

$END MRefal.
