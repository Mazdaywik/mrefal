$PROGRAM MRefal;

$IMPORT Config::MLoad;
$IMPORT Driver::MCompiler;
$IMPORT Driver::MModuleFinder;
$IMPORT Driver::MTables;
$IMPORT Driver::Tables::MLookup;
$IMPORT Driver::Tables::MModuleTree;
$IMPORT Driver::Tables::MModules;
$IMPORT MError;
$IMPORT MFileSystem;
$IMPORT MInOut;
$IMPORT MLambda;
$IMPORT MOS;
$IMPORT MSrcPos;
$IMPORT MVersion;

$IMPORT MProfiler;

$ENTRY Go =
  <MVersion::Show>
  <MProfiler::SetOutputName '_MRefal_profiler.log'>
  <MProfiler::Quantify-Flush & Profiling-Go>;

Profiling-Go =
  <CompileList
    <Driver::MTables::Create
      &Driver.Tables.MLookup.Create
      &Driver.Tables.MModules.Create
      &Driver.Tables.MModuleTree.Create
    >
    <Config::MLoad::Load
      <MError::CreateErrList>
      <MOS::ArgList>
    >
  >;

DestroyContext {
  t.hContext =
    <Driver::MTables::Destroy
      t.hContext
      &Driver.Tables.MLookup.Destroy
      &Driver.Tables.MModules.Destroy
      &Driver.Tables.MModuleTree.Destroy
    >;
}

CompileList {
  t.hContext t.hErrorList Success (Help e.Message) =
    <DestroyContext t.hContext>
    <MError::DestroyErrList t.hErrorList>
    <MInOut::WriteText e.Message>;

  t.hContext t.hErrorList Success t.Config e.PtModules =
    <CompileList-Aux
      t.Config t.hContext t.hErrorList e.PtModules
    >;

  t.hContext t.hErrorList Fails =
    <DestroyContext t.hContext>
    <MError::PrintErrors t.hErrorList>;
}

CompileList-Aux {
  t.Config t.hContext t.hErrorList e.PtModules =
    <CheckFoundResult
      t.Config
      t.hErrorList
      <MLambda::MapReduce
        <MLambda::BindLeft
          & AddCommandLineModule t.Config
        >
        t.hContext
        e.PtModules
      >
    >;
}

AddCommandLineModule {
  t.L-Config t.hContext (t.SrcPos e.PtModuleName t.Target) =
    <AddCommandLineModule-Aux
      t.SrcPos (e.PtModuleName) t.Target
      <Driver::MModuleFinder::LookupCmdLineModule
        t.hContext t.L-Config e.PtModuleName
      >
    >;
}

AddCommandLineModule-Aux {
  t.SrcPos (e.PtModuleName) t.Target t.hContext NotFound =
    t.hContext (NotFound t.SrcPos e.PtModuleName);

  t.SrcPos (e.PtModuleName) t.Target t.hContext Redefinition =
    t.hContext (Redefinition t.SrcPos e.PtModuleName);

  t.SrcPos (e.PtModuleName) NoTarget
  t.hContext Found t.QualifiedName s.FrontEnd e.PtBaseDirectory =
    t.hContext (Found t.SrcPos t.QualifiedName);

  t.SrcPos (e.PtModuleName) (e.PtTargetName)
  t.hContext Found t.QualifiedName s.FrontEnd e.PtBaseDirectory =
    <AddCommandLineModule-Aux-Guard
      <Driver::MTables::Modules
        t.hContext
	& Driver.Tables.MModules.SetTarget t.QualifiedName e.PtTargetName
      >
    >
    (Found t.SrcPos t.QualifiedName);
}

AddCommandLineModule-Aux-Guard {
  t.hContext Success = t.hContext;
}

CheckFoundResult {
  t.Config t.hErrorList
  t.hContext e.Results-B ( NotFound t.SrcPos e.Module ) e.Results-E =
    <DestroyContext t.hContext>
    <MError::PrintErrors
      <MLambda::Reduce
        & PrintErrorLookup
        t.hErrorList
        e.Results-B ( NotFound t.SrcPos e.Module ) e.Results-E
      >
    >;

  t.Config t.hErrorList
  t.hContext e.Results-B ( Redefinition t.SrcPos e.Module ) e.Results-E =
    <DestroyContext t.hContext>
    <MError::PrintErrors
      <MLambda::Reduce
        & PrintErrorLookup
        t.hErrorList
        e.Results-B ( Redefinition t.SrcPos e.Module ) e.Results-E
      >
    >;

  t.Config t.hErrorList t.hContext e.ResultsFound =
    <MLambda::TerminateAccumulator
      & FinishMake
      <MLambda::Reduce
        <MLambda::BindLeft & Make t.Config>
        (t.hContext t.hErrorList Success)
        e.ResultsFound
      >
    >;
}

PrintErrorLookup {
  t.hErrorList ( NotFound t.SrcPos e.PtModule ) =
    <MError::AddCmdLineError
      t.hErrorList t.SrcPos
      'module ' <MFileSystem::StrFromPath e.PtModule> ' not found'
    >;

  t.hErrorList ( Redefinition t.SrcPos e.PtModule ) =
    <MError::AddCmdLineError
      t.hErrorList t.SrcPos
      'module ' <MFileSystem::StrFromPath e.PtModule> ' redefined'
    >;

  t.hErrorList ( Found t.SrcPos t.QualifiedName ) = t.hErrorList;
}

Make {
  t.Config (t.hTables t.hErrorList Success)
  (Found t.SrcPos t.ModuleName) =
    (
      <Driver::MCompiler::Make
        t.hTables t.hErrorList t.Config t.SrcPos t.ModuleName
      >
    );

  t.Config (t.hTables t.hErrorList Fails)
  (Found t.SrcPos t.ModuleName) =
    (t.hTables t.hErrorList Fails);
}

FinishMake {
  ( t.hContext t.hErrorList Success ) =
    <DestroyContext t.hContext>
    <MError::PrintErrors t.hErrorList>;

  ( t.hContext t.hErrorList Fails ) =
    <DestroyContext t.hContext>
    <MError::PrintErrors t.hErrorList>;
}

$END MRefal.
