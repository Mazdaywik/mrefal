$PROGRAM MRefal;

// Эти модули используются

$IMPORT MCompiler;
$IMPORT MConfig;
$IMPORT MContext;
$IMPORT MError;
$IMPORT MLoadConfig;
$IMPORT InOut;
$IMPORT MLambda;
$IMPORT MModules;
$IMPORT MOS;
$IMPORT MQualifiedName;
$IMPORT MVersion;

$ENTRY Go =
  <MVersion::Show>
  <CompileList
    <MContext::Create>
    <MLoadConfig::Load
      <MError::CreateErrList>
      <MOS::ArgList>
    >
  >;

CompileList {
  t.hContext t.hErrorList Success t.Config e.Modules =
    <CompileList-Aux
      t.Config t.hContext t.hErrorList e.Modules
    >;

  t.hContext t.hErrorList Fails =
    <MContext::Destroy t.hContext>
    <MError::PrintErrors t.hErrorList>;
}

CompileList-Aux {
  t.Config t.hContext t.hErrorList e.Modules =
    <CheckFoundResult
      t.Config
      t.hErrorList
      <MLambda::MapReduce
        <MLambda::Composite
          & MLambda.UnBracket-H
          <MLambda::Parallel-H
            & MLambda.Bracket-H
            <MLambda::BindLeft-H
              & MModules.LookupCmdLineModule t.Config
            >
          >
          & MLambda.Bracket-H
        >
        t.hContext
        e.Modules
      >
    >;
}

CheckFoundResult {
  t.Config t.hErrorList
  t.hContext e.Results-B ( (e.Module) NotFound ) e.Results-E =
    <MContext::Destroy t.hContext>
    <MError::DestroyErrList t.hErrorList>
    <MLambda::Map
      & PrintNotFound
      e.Results-B ( (e.Module) NotFound ) e.Results-E
    >;

  t.Config t.hErrorList t.hContext e.ResultsFound =
    <MLambda::TerminateAccumulator
      & FinishMake
      <MLambda::Reduce
        <MLambda::BindLeft & Make t.Config>
        (t.hContext t.hErrorList Success)
        e.ResultsFound
      >
    >;
}

PrintNotFound {
  ( (e.Module) NotFound ) =
    <InOut::WriteLine
      'CMDLINE ERROR: module ' e.Module ' not found'
    >;

  ( (e.Module) Found t.QualifiedName s.FrontEnd e.BaseDirectory ) =
    <InOut::WriteLine
      'FOUND: ' e.Module ' '
      <MQualifiedName::Parse t.QualifiedName>
      ' ' s.FrontEnd ' ' e.BaseDirectory
    >;
}

Make {
  t.Config
  (t.hContext t.hErrorList Success)
  ( (e.Module) Found t.QualifiedName s.FrontEnd e.BaseDirectory ) =
    (
      <MCompiler::Make
        t.hContext
        t.hErrorList
        t.Config
        t.QualifiedName
      >
    );

  t.Config
  (t.hContext t.hErrorList Fails)
  ( (e.Module) Found t.QualifiedName s.FrontEnd e.BaseDirectory ) =
    (t.hContext t.hErrorList Fails);
}

FinishMake {
  ( t.hContext t.hErrorList Success ) =
    <MContext::Destroy t.hContext>
    <MError::PrintErrors t.hErrorList>;

  ( t.hContext t.hErrorList Fails ) =
    <MContext::Destroy t.hContext>
    <MError::PrintErrors t.hErrorList>;
}

$END MRefal.
