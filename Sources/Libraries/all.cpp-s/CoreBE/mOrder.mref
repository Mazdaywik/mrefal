$MODULE CoreBE::MOrder;

$IMPORT Math;
$IMPORT MRefalRTS;

/**
  Сравнение по типам:
  Macro > Character > Other
  Макроцифры сравниваются по арифметическим правилам.
  Characters сравниваются по ASCII-значениям.
  Остальные символы считаются равными.
*/

/**
  <SymbCompare s.Left s.Right> == '<' | '=' | '>'
*/
$ENTRY SymbCompare
  s.Left s.Right =
    <SwSymbCompare <Type-T s.Left> <Type-T s.Right>>;

Type-T
  s.Symb =
    <CheckAtom
      s.Symb
      <MRefalRTS::Ord s.Symb>
      <MRefalRTS::Chr s.Symb>
    >;

CheckAtom {
  s.Atom s.Atom s.Atom = (Other s.Atom);

  s.Symb s.Numb s.Symb = (Char s.Symb s.Numb);

  s.Numb s.Numb s.Symb = (Macro s.Numb); 
}

SwSymbCompare {
  (Char s.Chr1 s.Ord1) (Char s.Chr2 s.Ord2) =
    <Math::Compare s.Ord1 s.Ord2>;

  (Macro s.Num1) (Macro s.Num2) =
    <Math::Compare s.Num1 s.Num2>;

  (Other s.Symb1) (Other s.Symb2) =
    '=';

  (Macro s.Val) t.Other = '>';
  t.Other (Macro s.Val) = '<';

  (Char s.Chr s.Ord) t.Other = '>';
  t.Other (Char s.Chr s.Ord) = '<';
}

$END CoreBE::MOrder.
