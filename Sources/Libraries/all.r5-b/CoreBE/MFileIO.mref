$MODULE CoreBE::MFileIO;

$IMPORT Platform::Refal5;


/*
  Функция намеренно оставлена в виде нативной вставки ради лучшей диагностики
  ошибок: благодаря условиям и блокам в дампе будет падение именно
  CoreBE::FileIO::Open, а не каких-то её «кишок».
*/
$NATIVE Refal5 FUNCTION
$ENTRY Open
@@'
c {
c   'b' s.Mode e.FileName
c     , 'rwa'
c       : e.X s.Mode e.Y
c     , <Dg
c         '@@FreeHandles@@'
c       > :
c     {
r       //Найден свободный дескриптор
c       '@@Handles@@'
c       s.FreeHandle e.Handles
c         = <Open
c             <'@@&Implode@@'
c                s.Mode 'b'
c             >
c             s.FreeHandle e.FileName
c           >
c           <Br
c             '@@FreeHandles@@'
c             '='
c             '@@Handles@@'
c             e.Handles
c           >
c           s.FreeHandle;

r       //Свободных дескрипторов не осталось --- функция должна падать
c     };

c   s.Mode e.FileName
c     , 'rwa'
c       : e.X s.Mode e.Y
c     , <Dg
c         '@@FreeHandles@@'
c       > :
c     {
r       //Найден свободный дескриптор
c       '@@Handles@@'
c       s.FreeHandle e.Handles
c         = <Open s.Mode s.FreeHandle e.FileName >
c           <Br
c             '@@FreeHandles@@'
c             '='
c             '@@Handles@@'
c             e.Handles
c           >
c           s.FreeHandle;

r       //Свободных дескрипторов не осталось --- функция должна падать
c     };
c }
'@@;

$NATIVE Refal5 FUNCTION Rb ALIAS 'rb';
$NATIVE Refal5 FUNCTION Wb ALIAS 'wb';
$NATIVE Refal5 FUNCTION Ab ALIAS 'ab';

Implode {
  'rb' = &Rb;
  'wb' = &Wb;
  'ab' = &Ab;
}

$INIT
  <Platform.Refal5.Br
    FreeHandles '=' Handles 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17
  >;

$ENTRY Close {
  s.Handle =
    <Platform.Refal5.Close s.Handle >
    <Platform.Refal5.Br
      FreeHandles '=' <Platform.Refal5.Dg FreeHandles> s.Handle
    >;
}

$ENTRY ReadLine {
  &StdIn = <Platform.Refal5.Card>;
  s.Handle = <Platform.Refal5.Get s.Handle>;
}

$ENTRY StdIn { = &StdIn; }
$ENTRY StdOut { = &StdOut; }

$ENTRY Write {
  &StdOut e.Text '\n' =
    <Platform.Refal5.Prout <Platform.Refal5.Dg CachedLine> e.Text >;

  &StdOut e.Text =
    <Platform.Refal5.Br
      CachedLine '=' <Platform.Refal5.Dg CachedLine> e.Text
    >;

  s.Handle e.Text '\n' = <Platform.Refal5.Putout s.Handle e.Text>;
  s.Handle e.Text  = <Platform.Refal5.Write s.Handle e.Text>;
}

$ENTRY FlushIO {
  = <Platform.Refal5.Prout <Platform.Refal5.Dg CachedLine>>;
}

$END CoreBE::MFileIO.
