$MODULE CoreBE::OS::MOS;

$ENTRY CommandLine {
  = <Load 1 <Arg 0>>;
}

$NATIVE Refal5 FUNCTION Arg ALIAS Arg;

Load {
  s.Next = ;

  // Пропуск "особых" параметров
  s.Next '++' e.Arg = <Load <Add s.Next 1> <Arg s.Next>>;

  s.Next e.Arg =
    '\"' <Quote-Slash e.Arg> '\" '
    <Load <Add s.Next 1> <Arg s.Next>>;
}

$NATIVE Refal5 FUNCTION Add ALIAS Add;

Quote {
  e.Arg '\"' = <Quote-Slash e.Arg> '\\\"';
  e.Arg s.Other = <Quote e.Arg> s.Other;
  = ;
}

Quote-Slash {
  e.Arg '\\' = <Quote-Slash e.Arg> '\\\\';
  e.Arg = <Quote e.Arg>;
}

$ENTRY CreateProcess {
  Wait (e.ExeModule) (e.CommandLine) =
    // Кривое решение --- игнорировать параметр e.ExeModule,
    // но любые другие пути будут костылявее.
    <PerformSystem e.CommandLine>;

  NoWait (e.ExeModule) (e.CommandLine) =
    <PerformSystem 'start ' e.CommandLine>;
}


// Интерпретатор refgo не поддерживает длинные аргументы команды System,
// поэтому создаётся сценарий с выполняемой командой
PerformSystem {
  e.CommandLine =
    <PerformSystem-CreateFile (e.CommandLine) '~' <Symb <Random 1>> '.tmp.bat'>;
}

$NATIVE Refal5 FUNCTION Symb ALIAS Symb;
$NATIVE Refal5 FUNCTION Random ALIAS Random;

PerformSystem-CreateFile {
  (e.CommandLine) e.TempBat =
    <PerformSystem-Return
      (<Open 'w' 20 e.TempBat>)
      (<Write 20 '@' e.CommandLine>)
      (<Close 20>)
      (<System e.TempBat>)
      (<RemoveFile e.TempBat>)
    >;
}

$NATIVE Refal5 FUNCTION Open ALIAS Open;
$NATIVE Refal5 FUNCTION Write ALIAS Write;
$NATIVE Refal5 FUNCTION Close ALIAS Close;
$NATIVE Refal5 FUNCTION System ALIAS System;
$NATIVE Refal5 FUNCTION RemoveFile ALIAS RemoveFile;

PerformSystem-Return {
  () () () (s.RetCode) (e.RemoveResult) = s.RetCode;
}

$ENTRY EnvList = <EnvList-SwLoaded <Cp EnvList>>;

EnvList-SwLoaded {
  Loaded e.List = e.List;

  /* пусто */ =
    <EnvList-Load
      (<System 'set>~envlist.tmp'>)
      (<Open 'r' 20 '~envlist.tmp'>)
      (<LoadEnvs <Get 20>>)
      (<RemoveFile '~envlist.tmp'>)
    >;
}

$NATIVE Refal5 FUNCTION Cp ALIAS Cp;
$NATIVE Refal5 FUNCTION Get ALIAS Get;

EnvList-Load {
  (s.Ret1) () (e.Result) (e.RemoveResult) = <Br Loaded '=' e.Result> e.Result;
}

$NATIVE Refal5 FUNCTION Br ALIAS Br;

LoadEnvs {
  0 = <Close 20>;
  e.Line 0 = (e.Line) <Close 20>;
  e.Line = (e.Line) <LoadEnvs <Get 20>>;
}


$NATIVE Refal5 FUNCTION
$ENTRY Time ALIAS Time;

$END CoreBE::OS::MOS.
