$MODULE CoreBE::OS::MOS;

$IMPORT Platform::Refal5;

$ENTRY CommandLine {
  = <Load 1 <Platform.Refal5.Arg 0>>;
}

Load {
  s.Next = ;

  // Пропуск "особых" параметров
  s.Next '++' e.Arg =
    <Load <Platform.Refal5.Add s.Next 1> <Platform.Refal5.Arg s.Next>>;

  s.Next e.Arg =
    '\"' <Quote-Slash e.Arg> '\" '
    <Load <Platform.Refal5.Add s.Next 1> <Platform.Refal5.Arg s.Next>>;
}

Quote {
  e.Arg '\"' = <Quote-Slash e.Arg> '\\\"';
  e.Arg s.Other = <Quote e.Arg> s.Other;
  = ;
}

Quote-Slash {
  e.Arg '\\' = <Quote-Slash e.Arg> '\\\\';
  e.Arg = <Quote e.Arg>;
}

$ENTRY CreateProcess {
  Wait (e.ExeModule) (e.CommandLine) =
    // Кривое решение --- игнорировать параметр e.ExeModule,
    // но любые другие пути будут костылявее.
    <PerformSystem e.CommandLine>;

  NoWait (e.ExeModule) (e.CommandLine) =
    <PerformSystem 'start ' e.CommandLine>;
}


// Интерпретатор refgo не поддерживает длинные аргументы команды System,
// поэтому создаётся сценарий с выполняемой командой
PerformSystem {
  e.CommandLine =
    <PerformSystem-CreateFile
      (e.CommandLine)
      '~' <Platform.Refal5.Symb <Platform.Refal5.Random 1>> '.tmp.bat'
    >;
}

PerformSystem-CreateFile {
  (e.CommandLine) e.TempBat =
    <PerformSystem-Return
      (<Platform.Refal5.Open 'w' 20 e.TempBat>)
      (<Platform.Refal5.Write 20 '@' e.CommandLine>)
      (<Platform.Refal5.Close 20>)
      (<Platform.Refal5.System e.TempBat>)
      (<Platform.Refal5.RemoveFile e.TempBat>)
    >;
}

PerformSystem-Return {
  () () () (s.RetCode) (e.RemoveResult) = s.RetCode;
}

$ENTRY EnvList = <EnvList-SwLoaded <Platform.Refal5.Cp EnvList>>;

EnvList-SwLoaded {
  Loaded e.List = e.List;

  /* пусто */ =
    <EnvList-Load
      (<Platform.Refal5.System 'set>~envlist.tmp'>)
      (<Platform.Refal5.Open 'r' 20 '~envlist.tmp'>)
      (<LoadEnvs <Platform.Refal5.Get 20>>)
      (<Platform.Refal5.RemoveFile '~envlist.tmp'>)
    >;
}

EnvList-Load {
  (s.Ret1) () (e.Result) (e.RemoveResult) =
    <Platform.Refal5.Br Loaded '=' e.Result> e.Result;
}

LoadEnvs {
  0 = <Platform.Refal5.Close 20>;
  e.Line 0 = (e.Line) <Platform.Refal5.Close 20>;
  e.Line = (e.Line) <LoadEnvs <Platform.Refal5.Get 20>>;
}


$ENTRY Time { = <Platform.Refal5.Time>; }

$END CoreBE::OS::MOS.
