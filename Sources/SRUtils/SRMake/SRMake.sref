//FROM Library
$EXTERN WriteLine, System;

//FROM LibraryEx
$EXTERN ArgList, Map;

//FROM FileScanner
$EXTERN CreateFileList;

//FROM ParseCmdLine
$EXTERN ParseCommandLine, NoCompile, CompileCommand, CmdLineError;

$FORWARD Main;

$ENTRY Go { = <Main <ArgList>>; }

$FORWARD MakeProject;

Main {
  (e.Program) e.Arguments =
    <MakeProject
      <ParseCommandLine e.Arguments>
    >;
}

$FORWARD Make;

MakeProject {
  (CmdLineError e.Message) =
    <WriteLine 'COMMAND LINE ERROR: ' e.Message>;

  (NoCompile) (e.Folders) (e.File) =
    <WriteLine
      'COMMAND LINE ERROR: Compiler not selected'
    >;

  (CompileCommand e.Command) (e.Folders) (e.File) =
    <Make
      (e.Command) (e.Folders) <CreateFileList (e.Folders) e.File>
    >;

  t.Compiler (e.Folders) e.ManyFiles =
    <WriteLine
      'COMMAND LINE ERROR: many files selected'
    >;
}

//FROM FindFile
$EXTERN Source, Output, NotFound;

$FORWARD PrintNotFound, PrintDirectory, PrintUnit;

Make {
  (e.CompilerCommand) (e.Directories)
  e.Units-B (NotFound e.UnitName) e.Units-E =
    <Map PrintNotFound (NotFound e.UnitName) e.Units-E>;

  (e.CompilerCommand) (e.Directories) e.Units =
    <System
      'srefc -c "' e.CompilerCommand '" '
      <Map PrintDirectory e.Directories>
      <Map PrintUnit e.Units>
    >;
}

PrintNotFound {
  (NotFound e.UnitName) =
    <WriteLine
      'COMMAND LINE ERROR: Unit ' e.UnitName ' not found'
    >;

  (Output e.Output) = ;
  (Source (e.Source) e.Output) = ;
}

PrintDirectory {
  (e.Folder) = '-d "' e.Folder '" ';
}

PrintUnit {
  (Output e.Output) = '"' e.Output '" ';
  (Source (e.Source) e.Output) = '"' e.Source '" ';
}
