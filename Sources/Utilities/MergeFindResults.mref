$PROGRAM MergeFindResults;

$IMPORT InOut = MInOut, MUtilitiesBanner;

$ENTRY Go =
	<MUtilitiesBanner::Type 'MergeFindResults'>
	<InOut::WriteText
		<ProcessMerge
			<LoadText>
		>
	>;

LoadText = <DoLoadText <InOut::ReadLine>>;

DoLoadText {
	0 = ;
	e.Line = (e.Line) <LoadText>;
}

ProcessMerge
	e.Lines =
		<UnBracket
			<MergeGroups
				<FindGroups e.Lines>
			>
		>;

UnBracket {
	((e.OneLine) e.Lines-B ()() e.Lines-E) e.Tail =
		<UnBracket
			((e.OneLine) e.Lines-B () e.Lines-E)
			e.Tail
		>;

	((e.OneLine)()) e.Tail =
		<UnBracket e.Tail>;

	((e.OneLine)) e.Tail =
		<UnBracket e.Tail>;

	(e.InGroup) e.Tail =
		() e.InGroup <UnBracket e.Tail>;

	= ;
}

FindGroups {
	() e.Lines () e.Tail =
		(e.Lines) <FindGroups () e.Tail>;

	() e.Tail = (e.Tail);
}

MergeGroups {
	((e.FirstLine) e.Lines1) e.Groups 
	((e.FirstLine) e.Lines2) e.Tail =
		<MergeGroups
			((e.FirstLine) e.Lines1 () e.Lines2) e.Groups e.Tail
		>;

	t.FirstGroup e.Tail =
		t.FirstGroup <MergeGroups e.Tail>;

	= ;
}

$END MergeFindResults.