/**=============================================================================
  Модуль реализует хранилище данных для диспетчеров входных и генерирующих
частей. Работает по принципу атрибутной таблицы символов. Таблица индексируются
двумя символами: именем диспетчера и именем атрибута.
==============================================================================*/

$MODULE MBEFE-Holder;

$IMPORT MSwapSupport;

/*------------------------------------------------------------------------------
  Внутренний формат:
  e<(s<name>.MgrName e<(s<name>.Option e.Value)>)>.Info
------------------------------------------------------------------------------*/

/**
  <UpdateEntry
    s.FnSwap
    s.MgrName
    (s.Option e.Value)*
  > == empty
*/
$ENTRY UpdateEntry
  s.FnSwap s.MgrName e.Options =
    <UpdateEntry-Aux
      s.FnSwap s.MgrName (e.Options)
      <s.FnSwap>
    >;

UpdateEntry-Aux {
  s.FnSwap s.MgrName (e.NewOptions)
  e.Info-B (s.MgrName e.OldOptions) e.Info-E =
    <s.FnSwap
      e.Info-B
      (s.MgrName <UpdateOptions (e.NewOptions) e.OldOptions>)
      e.Info-E
    >;

  s.FnSwap s.MgrName (e.Options) e.Info =
    <s.FnSwap e.Info (s.MgrName e.Options)>;
}

UpdateOptions {
  () e.OldOptions = e.OldOptions;

  ((s.Option e.NewValue) e.NewOptions)
  e.OldOptions-B (s.Option e.OldValue) e.OldOptions-E =
    (s.Option e.NewValue)
    <UpdateOptions (e.NewOptions) e.OldOptions-B e.OldOptions-E>;

  ((s.Option e.Value) e.NewOptions) e.OldOptions =
    (s.Option e.Value) <UpdateOptions (e.NewOptions) e.OldOptions>;
}

//------------------------------------------------------------------------------

/**
  <GetInfo s.FnSwap s.MgrName s.OptionName>
    == Success e.Value
    == Fails s.Error
  s.Error ::= InvalidMgr | InvalidOption
*/
$ENTRY GetInfo
  s.FnSwap s.MgrName s.OptionName =
    <SwGetInfo
      <MSwapSupport::Read s.FnSwap>
      s.MgrName s.OptionName
    >;

SwGetInfo {
  e.Info-B (s.MgrName e.Options) e.Info-E s.MgrName s.OptionName =
    <SwGetInfo--LookupOptions s.OptionName e.Options>;

  e.Info s.MgName s.OptionName =
    Fails InvalidMgr;
}

SwGetInfo--LookupOptions {
  s.OptionName e.Options-B (s.OptionName e.Value) e.Options-E =
    Success e.Value;

  s.OptionName e.Options =
    Fails InvalidOption;
}

//------------------------------------------------------------------------------

/**
  <GetMgrList s.FnSwap>
    == e<name>.MgrNames
*/
$ENTRY GetMgrList
  s.FnMgr =
    <DoGetMgrList <MSwapSupport::Read s.FnMgr>>;

DoGetMgrList {
  (s.MgrName e.Options) e.Tail =
    s.MgrName <DoGetMgrList e.Tail>;

  = ;
}

$END MBEFE-Holder.