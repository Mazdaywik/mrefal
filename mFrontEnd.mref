$MODULE MFrontEnd;

$IMPORT MIModule;
$IMPORT MParser;

/**
	<LoadModule e.SourceName>
		== t.Module s.Status t.ErrorList
	s.Status ::= Success | Warnings | Errors | Fatal
*/
$ENTRY LoadModule
	e.SourceName =
		<MParser::SrcModule e.SourceName>;

/**
	<SymInfo-Old t.Module>
		== t.Module
		( Module (e.Directory) e.ModuleName )
		( Functions (e.FnName)* )
		( Imports ((e.ImportDirectory) e.ImportName)* )
*/
$ENTRY SymInfo-Old
	t.Module =
		<SymInfo-Imports
			<MIModule::GetImportList t.Module>
		>;

SymInfo-Imports
	t.Module e.Imports =
		<SymInfo-Functions
			<MIModule::GetFunctionList t.Module>
		>
		<MakeImports e.Imports>;

SymInfo-Functions
	t.Module e.Functions =
		<FilterEntry
			t.Module () e.Functions
		>;

FilterEntry {
	t.Module (e.Entries) (e.NextFn) e.Tail =
		<FilterEntry
			<CheckEntry
				<MIModule::GetFunctionAttribute t.Module ScopeClass e.NextFn>
				(e.Entries) e.NextFn
			>
		>;

	t.Module (e.Entries) =
		<SymInfo-ModuleName
			<MIModule::GetModuleName t.Module>
		>
		<MakeEntries e.Entries>;
}

CheckEntry {
	t.Module Found Entry (e.Entries) e.Name =
		t.Module (e.Entries (e.Name));

	t.Module Found s.Other (e.Entries) e.Name =
		t.Module (e.Entries);
}

SymInfo-ModuleName
	t.Module e.ModuleName =
		t.Module
		(Module () e.ModuleName);

MakeEntries
	e.Entries =
		(Functions e.Entries);

MakeImports
	e.Imports =
		(Imports <AddEmpty e.Imports>);

AddEmpty {
	(e.Next) e.Tail =
		(() e.Next) <AddEmpty e.Tail>;

	= ;
}

$END MFrontEnd.
