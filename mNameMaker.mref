$MODULE MNameMaker;

/**=============================================================================
  Служебный модуль, используемый только в диспетчерах front- и back-end'ов.
==============================================================================*/

$IMPORT MContext;

/**
  <MakeName t.Context t.QualifiedName s.(BE/FE)Name s.FnRelativeNameMaker>
    == t.Context t.QualifiedName Success e.FilePath
    == t.Context t.QualifiedName Fails

  <s.FnRelativeNameMaker t.QualifiedName s.(BE/FE)Name>
    == t.QualifiedName Success e.RelativeName
    == t.QualifiedName Fails
*/
$ENTRY MakeName
  t.Context t.QualifiedName s.MgrName s.FnRelative =
    <MakeName--SwInContext
      ( <MContext::GetBaseDirectory t.Context t.QualifiedName> )
      t.QualifiedName s.MgrName s.FnRelative
    >;

MakeName--SwInContext {
  ( t.hContext Success e.BaseName )
  t.QualifiedName s.MgrName s.FnRelative =
    <MakeName--SwMakeRelative
      t.hContext (e.BaseName)
      <s.FnRelative t.QualifiedName s.MgrName>
    >;

  ( t.hContext Fails ModuleNotFound )
  t.QualifiedName s.MgrName s.FnRelative =
    t.hContext t.QualifiedName Fails;

  ( t.hContext Fails MismatchCase )
  t.QualifiedName s.MgrName s.FnRelative =
    t.hContext t.QualifiedName Fails;

  /*
    OptionNotFound не проверяем -- нарушение инварианта.
  */
}

MakeName--SwMakeRelative {
  t.Context (Current)
  t.QualifiedName Success e.RelativeName =
    t.Context t.QualifiedName Success e.RelativeName;

  t.Context (e.BaseName)
  t.QualifiedName Success e.RelativeName =
    t.Context t.QualifiedName Success e.BaseName '/' e.RelativeName;

  t.Context (e.BaseName)
  t.QualifiedName Fails =
    t.Context t.QualifiedName Fails;
}

$END MNameMaker.