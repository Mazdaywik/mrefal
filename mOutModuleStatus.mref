$MODULE MOutModuleStatus;

$IMPORT InOut;
$IMPORT Math;
$IMPORT MSwapSupport;
$IMPORT MQualifiedName;

$ENTRY Updated-Q
  t.QualifiedName =
    <Out-Q Updated t.QualifiedName>;

$ENTRY Compiling-Q
  t.QualifiedName =
    <Out-Q Compiling t.QualifiedName>;

$ENTRY Compiled-Q
  t.QualifiedName =
    <Out-Q Compiled t.QualifiedName>;

$ENTRY Library-Q
  t.QualifiedName =
    <Out-Q Library t.QualifiedName>;

$ENTRY Fails-Q
  t.QualifiedName =
    <Out-Q Fails t.QualifiedName>;

$ENTRY Linking
  e.ProgName =
    <Out Linking e.ProgName>;

$SWAP G_OldStatus, G_Buffer;

Out-Q
 s.NewStatus t.QualifiedName =
   <Out s.NewStatus <MQualifiedName::ToPrintable-Quad t.QualifiedName>>;

Out
  s.NewStatus e.ModuleName =
    <SwOut
      s.NewStatus <G_OldStatus>
      '`' e.ModuleName '\''
    >;

SwOut {
  s.NewStatus Startup e.ModuleName =
    <WriteLine s.NewStatus e.ModuleName>;

  s.OldStatus s.OldStatus e.ModuleName =
    <G_OldStatus s.OldStatus>
    <Write e.ModuleName>;

  s.NewStatus s.OldStatus e.ModuleName =
    <NewLine>
    <WriteLine s.NewStatus e.ModuleName>;
}

StatusStr {
  Updated =   'Updated ';
  Compiling = 'Compiling ';
  Compiled =  'Compiled ';
  Library =   'Load library ';
  Linking =   'Linking ';
  Fails =     '  FAILS';
}

MaxLength = 80;

CheckLength
  e.Line = <DoCheckLength <MaxLength> e.Line>;

Dec {
  1 = Limit;
  Limit = Limit;
  s.Other = <Math::Dec s.Other>;
}

DoCheckLength {
  Limit e.Line = Long;
  s.Other = Short;
  s.Other s.Char e.Line =
    <DoCheckLength
      <Dec s.Other> e.Line
    >;
}

WriteLine {
  s.Status e.Name =
    <NewLine>
    <MSwapSupport::Write & G_OldStatus s.Status>
    <MSwapSupport::Write
      & G_Buffer
      <StatusStr s.Status> e.Name
    >;
}

NewLine = <SwNewLine <G_Buffer>>;

SwNewLine {
  = /* <InOut::WriteLine> */;
  e.Line = <InOut::WriteLine e.Line '.'>;
}

Write
  e.Name =
    <Write-CheckLength (<G_Buffer>) e.Name>;

Write-CheckLength {
  () e.Name = <WriteLine <G_OldStatus> e.Name>;
  (e.Buffer) e.Line =
    <SwWrite-CheckLength
      <CheckLength e.Buffer e.Line>
      <G_OldStatus> (e.Buffer) e.Line
    >;
}

SwWrite-CheckLength {
  Short s.Status (e.Buffer) e.Name =
    <G_OldStatus s.Status> <G_Buffer e.Buffer ', ' e.Name>;

  Long s.Status (e.Buffer) e.Name =
    <G_Buffer e.Buffer>
    <WriteLine s.Status e.Name>;
}

$ENTRY Flush = <SwFlush <G_Buffer>>;

SwFlush {
  = ;
  e.Line = <InOut::WriteLine e.Line '.'>;
}


$ENTRY Init = <G_OldStatus Starting>;
$ENTRY Final = <Flush>;

$END MOutModuleStatus.