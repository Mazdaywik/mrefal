$MODULE MVarTable;

$IMPORT MError;

*-------------------------------------------------------------------------------
* Работа с переменными
*-------------------------------------------------------------------------------

$DATA VarTable;

$ENTRY Create = [VarTable];

$ENTRY Destroy
	[VarTable e.?Vars] = ;

$ENTRY AddVariable {
	/* Повторное вхождение переменной -- ничего не добавляем */
	t.ErrorList
	[VarTable e.Vars-B (s.VarType e.Index) e.Vars-E]
	t.SrcPos s.VarType e.Index =
		t.ErrorList
		[VarTable e.Vars-B (s.VarType e.Index) e.Vars-E]
		Success;

	/* Две разные переменные не могут иметь один индекс */
	t.ErrorList
	[VarTable e.Vars-B (s.OneVarType e.Index) e.Vars-E]
	t.SrcPos s.OtherVarType e.Index =
		<MError::AddVarTypeMismatch
			t.ErrorList t.SrcPos e.Index
		> [VarTable e.Vars-B (s.OneVarType e.Index) e.Vars-E]
		Fail;

	/* Просто добавляем переменную */
	t.ErrorList
	[VarTable e.Variables]
	t.SrcPos s.VarType e.Index =
		t.ErrorList [VarTable e.Variables (s.VarType e.Index)] Success;
}

$ENTRY CheckVariable {
	t.ErrorList
	[VarTable e.Vars-B (s.VarType e.Index) e.Vars-E]
	t.SrcPos s.VarType e.Index =
		t.ErrorList
		[VarTable e.Vars-B (s.VarType e.Index) e.Vars-E]
		Success;

	t.ErrorList
	[VarTable e.OtherVariables]
	t.SrcPos s.VarType e.Index =
		<MError::AddUndeclaredVariable
			t.ErrorList t.SrcPos s.VarType e.Index
		> [VarTable e.OtherVariables]
		Fail;
}

$END MVarTable.
