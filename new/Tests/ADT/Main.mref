$PROGRAM Main;

$IMPORT InOut, MStrings;

$DATA SymTable, VarTable;

Create = [SymTable];

AddName {
	[SymTable e.Names-B (e.Name) e.Names-E] e.Name =
		<InOut::WriteLine 'Identifier redeclared ' e.Name>
		[SymTable e.Names-B (e.Name) e.Names-E];

	[SymTable e.Names] e.Name =
		[SymTable e.Names (e.Name)];
}

Destroy [SymTable e.Names] = ;

$ENTRY Go = <MainLoop Start>;

MainLoop {
	Start =
		<MainLoop Next <Create>>;
	Next t.SymTable =
		<MainLoop Line t.SymTable <InOut::ReadLine>>;
	Finish t.SymTable =
		<Destroy t.SymTable>;

	Line t.SymTable 'exit' =
		<MainLoop Finish t.SymTable>;

	Line t.SymTable e.Line 0 =
		<MainLoop Finish t.SymTable>;

	Line t.SymTable e.Line =
		<MainLoop
			Next
			<AddName
				t.SymTable <MStrings::Trim e.Line>
			>
		>;
}

$END Main.